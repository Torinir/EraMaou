;従順のLvUP処理とその可否判定
;eratohoA ver1,204のスクリプトをベースに処理を簡略化
;eraIM@Sから導入しました(eramaou)

;-------------------------------------------------
;従順のLvUP
;-------------------------------------------------
@ABLUP10
DRAWLINE
;PRINTL 奴隷の従順さを高めます
;PRINTL 従順さが高くなると命令に従いやすくなります
;PRINTL ..............................................................................
;従順はLv5が上限
;[愛][妄信]が付いている場合は10まで上昇可能
IF ABL:10 >= 5 && (TALENT:85 == 0 && TALENT:86 == 0)
	PRINTW Character qualities must be improved before this can be raised further.
	RETURN 0
ELSEIF ABL:10 >= 10
	PRINTW Already At MAX.
	RETURN 0
ENDIF

;必要な恐怖の珠
A = 0
;必要な恭順の珠
B = 0
;必要な欲情の珠
C = 0
;必要な屈服の珠
D = 0
;必要な異常経験回数
E = 0

;条件別にＯＫかダメかを記録する
;恐怖の珠による可否(I=0:可,I&1:珠不足,I&2:経験不足)
I = 0
;恭順の珠による可否(J=0:可,J&1:珠不足,J&2:経験不足)
J = 0
;欲情の珠による可否(K=0:可,K&1:珠不足,K&2:経験不足,K=256:そのレベルでは自動不可)
K = 0
;屈服の珠による可否(L=0:可,L&1:珠不足,L&2:経験不足,L=256:そのレベルでは自動不可)
L = 0

CALL DECIDE_ABLUP10

;異常経験が必要か
SIF E > 0
	PRINTFORML %EXPNAME:50%{E}以上(現在{EXP:50})かつ

IF A > 0
	;恐怖の珠で上げる
	PRINTFORM [0] - %PALAMNAME:10%の珠×{JUEL:10}/{A} ......
	IF I == 0
		PRINT OK
	ELSE
		SIF I & 1
			PRINT Not enough points 
		SIF I & 2
			PRINT Not enough exp 
	ENDIF
	PRINTL 
ENDIF

;恭順の珠で上げる
PRINTFORM [1] - %PALAMNAME:4%の珠×{JUEL:4}/{B} ......
IF J == 0
	PRINT OK
ELSE
	SIF J & 1
		PRINT Not enough points 
	SIF J & 2
		PRINT Not enough exp 
ENDIF
PRINTL 

IF C > 0
	;欲情の珠で上げる
	PRINTFORM [2] - %PALAMNAME:5%の珠×{JUEL:5}/{C} ......
	IF K == 0
		PRINT OK
	ELSE
		SIF K & 1
			PRINT Not enough points 
		SIF K & 2
			PRINT Not enough exp 
	ENDIF
	PRINTL 
ENDIF

IF D > 0
	;屈服の珠で上げる
	PRINTFORM [3] - %PALAMNAME:6%の珠×{JUEL:6}/{D} ......
	IF L == 0
		PRINT OK
	ELSE
		SIF L & 1
			PRINT Not enough points 
		SIF L & 2
			PRINT Not enough exp 
	ENDIF
	PRINTL 
ENDIF

PRINTL [100] - やめる

INPUT
IF (RESULT < 0 || RESULT > 3) && RESULT != 100
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL Requirements haven't been met
	RESTART
ELSEIF J != 0 && RESULT == 1
	PRINTL Requirements haven't been met
	RESTART
ELSEIF K == 256 && RESULT == 2
	RESTART
ELSEIF K != 0 && RESULT == 2
	PRINTL Requirements haven't been met
	RESTART
ELSEIF L == 256 && RESULT == 3
	RESTART
ELSEIF L != 0 && RESULT == 3
	PRINTL Requirements haven't been met
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:10 += 1

IF RESULT == 0
	JUEL:10 -= A
ELSEIF RESULT == 1
	JUEL:4 -= B
ELSEIF RESULT == 2
	JUEL:5 -= C
ELSEIF RESULT == 3
	JUEL:6 -= D
ENDIF

PRINTFORML %ABLNAME:10%のレベルが{ABL:10}になりました

RETURN 0

;-------------------------------------------------
;従順のLvUP可否判定
;-------------------------------------------------
@DECIDE_ABLUP10
;従順はLv5が上限,[愛][妄信]が付いている場合はLv10まで解放
SIF ABL:10 >= 5 && (TALENT:85 == 0 && TALENT:86 == 0)
	RETURN 0
SIF ABL:10 >= 10
	RETURN 0

;判定変数を空に
I = 0
J = 0
K = 0
L = 0

A = 0
B = 0
C = 0
D = 0
E = 0

IF ABL:10 == 0
	A = 10
	B = 10
	C = 300
	D = 200
ELSEIF ABL:10 == 1
	A = 150
	B = 100
	C = 1000
	D = 1200
ELSEIF ABL:10 == 2
	A = 1000
	B = 800
	C = 2000
	D = 3000
ELSEIF ABL:10 == 3
	A = 3000
	B = 3000
	C = 0
	D = 12000
ELSEIF ABL:10 == 4
	A = 8000
	B = 5000
	C = 0
	D = 0
ELSEIF ABL:10 == 5
	A = 12000
	B = 10000
	C = 0
	D = 0
ELSEIF ABL:10 == 6
	A = 25000
	B = 20000
	C = 0
	D = 0
ELSEIF ABL:10 == 7
	A = 0
	B = 40000
	C = 0
	D = 0
ELSEIF ABL:10 == 8
	A = 0
	B = 80000
	C = 0
	D = 0
ELSEIF ABL:10 == 9
	A = 0
	B = 150000
	C = 0
	D = 0
ENDIF

;ＬV４から5に上げるときは異常経験必要
;(素質:[臆病][素直][即落ち][淫乱][愛][妄信]なら無視できる)
IF ABL:10 == 4 && (TALENT:10 == 0 && TALENT:13 == 0 && TALENT:76 == 0 && TALENT:73 == 0 && TALENT:85 == 0 && TALENT:86 == 0)
	E = 1
;ＬV７から８に上げるときは異常経験が2以上必要
;(素質:[臆病][素直][即落ち][淫乱][愛][妄信]なら無視できる)
ELSEIF ABL:10 == 7 && (TALENT:10 == 0 && TALENT:13 == 0 && TALENT:76 == 0 && TALENT:73 == 0 && TALENT:85 == 0 && TALENT:86 == 0)
	E = 2
ENDIF

;臆病
IF TALENT:10
	TIMES A , 0.50
	TIMES B , 0.90
	TIMES D , 0.90
ENDIF
;反抗的
IF TALENT:11
	TIMES A , 2.00
	TIMES B , 1.50
	TIMES C , 1.20
	TIMES D , 1.50
ENDIF
;気丈
IF TALENT:12
	TIMES A , 3.00
	TIMES B , 1.50
	TIMES C , 1.20
	TIMES D , 1.50
ENDIF
;素直
IF TALENT:13
	TIMES B , 0.80
	TIMES D , 0.90
ENDIF
;生意気
IF TALENT:16
	TIMES A , 1.20
	TIMES B , 1.50
	TIMES D , 1.20
ENDIF
;プライド高い
IF TALENT:15
	TIMES A , 1.20
	TIMES B , 1.50
	TIMES D , 2.00
;プライド低い
ELSEIF TALENT:17
	TIMES B , 0.80
	TIMES D , 0.80
ENDIF

;抑圧
IF TALENT:32
	TIMES A , 1.20
	TIMES B , 1.20
	TIMES C , 2.00
	TIMES D , 1.20
;解放
ELSEIF TALENT:33
	TIMES C , 0.50
ENDIF
;抵抗
IF TALENT:34
	TIMES A , 1.50
	TIMES B , 1.50
	TIMES C , 2.00
	TIMES D , 2.00
ENDIF

;淫乱
SIF TALENT:76
	TIMES C , 0.50
;愛
SIF TALENT:85
	TIMES B , 0.75
;妄信
SIF TALENT:86
	TIMES B , 0.20

;嫉妬
IF TALENT:84
	TIMES B , 5.00
	TIMES C , 0.80
	TIMES D , 2.00
ENDIF

;最低でも1個は必要
SIF B < 1
	B = 1

;恐怖の珠は必要か?
IF A > 0
	;恐怖の珠は足りている?
	SIF JUEL:10 < A
	I |= 1
ELSE
	I = 256
ENDIF

;恭順の珠は足りている?
SIF JUEL:4 < B
	J |= 1

;欲情の珠は必要か?
IF C > 0
	;欲情の珠は足りている?
	SIF JUEL:5 < C
		K |= 1
ELSE
	K = 256
ENDIF

;屈服の珠は足りている?
IF D > 0
	;屈服の珠で上げる
	SIF JUEL:6 < D
		L |= 1
ELSE
	L = 256
ENDIF

;異常経験は足りている?
IF E > EXP:50
	I |= 2
	J |= 2
	K |= 2
	L |= 2
ENDIF

IF I == 0 || J == 0 || K == 0 || L ==0
	RETURN 1
ELSE
	RETURN 0
ENDIF

;
;
;