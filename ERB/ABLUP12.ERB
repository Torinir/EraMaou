;技巧のLvUP処理とその可否判定
;eratohoA ver1,204のスクリプトをベースに処理を簡略化
;eraIM@Sから導入しました(eramaou)

;-------------------------------------------------
;技巧のLvUP
;-------------------------------------------------
@ABLUP12
DRAWLINE
;PRINTL 奴隷の技巧を高めます
;PRINTL 技巧が高いと,性行為や奉仕の際に相手をイカせやすくなります
;PRINTL ..............................................................................
;技巧はLv10が上限
IF ABL:12 >= 10
	PRINTW Already At MAX.
	RETURN 0
;技巧+話術は1６以上にならない
ELSEIF ABL:12 + ABL:15 >= 15
	PRINTFORMW 技巧({ABL:12})+話術({ABL:15})の上限は15です
	RETURN 0
ENDIF

;必要な習得の珠
A = 0

;条件別にＯＫかダメかを記録する
;習得の珠による可否(I=0:可,I&1:珠不足,I&2:経験不足)
I = 0

CALL DECIDE_ABLUP12

PRINTFORM [0] - %PALAMNAME:7%の珠×{JUEL:7}/{A} ......
IF I == 0
	PRINT OK
ELSE
	SIF I & 1
		PRINT Not enough points 
	SIF I & 2
		PRINT Not enough exp 
ENDIF
PRINTL 

PRINTL [100] - やめる

INPUT
IF (RESULT < 0 || RESULT > 0) && RESULT != 100
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL Requirements haven't been met
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:12 += 1

IF RESULT == 0
	JUEL:7 -= A
ENDIF

PRINTFORML %ABLNAME:12%のレベルが{ABL:12}になりました

RETURN 0

;-------------------------------------------------
;技巧のLvUP可否判定
;-------------------------------------------------
@DECIDE_ABLUP12
;技巧はLv10が上限
SIF ABL:12 >= 10
	RETURN 0
;技巧+話術は1６以上にならない
SIF ABL:12 + ABL:15 >= 15
	RETURN 0

;判定変数を空に
A = 0
I = 0

IF ABL:12 == 0
	A = 1
ELSEIF ABL:12 == 1
	A = 25
ELSEIF ABL:12 == 2
	A = 200
ELSEIF ABL:12 == 3
	A = 3000
ELSEIF ABL:12 == 4
	A = 8000
ELSEIF ABL:12 == 5
	A = 12000
ELSEIF ABL:12 == 6
	A = 16000
ELSEIF ABL:12 == 7
	A = 22000
ELSEIF ABL:12 == 8
	A = 28000
ELSEIF ABL:12 == 9
	A = 35000
ENDIF

;一線越えない
IF TALENT:27
	SIF ABL:12 == 3
		TIMES A , 1.50
	SIF ABL:12 == 4
		TIMES A , 2.00
	SIF ABL:12 == 5
		TIMES A , 2.50
	SIF ABL:12 >= 6
		TIMES A , 3.00
ENDIF

;素直
SIF TALENT:13
	TIMES A , 0.95
;無関心
SIF TALENT:21
	TIMES A , 1.05
;好奇心
SIF TALENT:23
	TIMES A , 0.95
;保守的
SIF TALENT:24
	TIMES A , 1.10

;抑圧
IF TALENT:32
	TIMES A , 1.10
;解放
ELSEIF TALENT:33
	TIMES A , 0.90
ENDIF

;抵抗
SIF TALENT:34
	TIMES A , 1.20

;恥じらい
IF TALENT:35
	TIMES A , 1.05
;恥薄い
ELSEIF TALENT:36
	TIMES A , 0.95
ENDIF

;習得早い
IF TALENT:50
	TIMES A , 0.80
;習得遅い
ELSEIF TALENT:51
	TIMES A , 1.50
ENDIF

;舌使い
SIF TALENT:52
	TIMES A , 0.95
;献身的
SIF TALENT:63
	TIMES A , 0.95
;汚れ無視
SIF TALENT:64
	TIMES A , 0.95

;最低でも1個は必要
SIF A < 1
	A = 1

;習得の珠は足りている?
SIF JUEL:7 < A
	I |= 1

IF I == 0
	RETURN 1
ELSE
	RETURN 0
ENDIF
;
;
;