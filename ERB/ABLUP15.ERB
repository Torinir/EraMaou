;話術のLvUP処理とその可否判定
;eraIm@s_ver.0.17βdのスクリプトをベースに処理を簡略化
;eraIM@Sから導入しました(eramaou)


;-------------------------------------------------
;話術のLvUP
;-------------------------------------------------
@ABLUP15
DRAWLINE
;PRINTL 奴隷の話術を高めます
;PRINTL 話術が高いと,娼館での営業や接待の成功率が高まります
;PRINTL ..............................................................................
;話術はLv10が上限
IF ABL:15 >= 10
	PRINTW Already At MAX.
	RETURN 0
;技巧+話術は1６以上にならない
ELSEIF ABL:12 + ABL:15 >= 15
	PRINTFORMW Technique({ABL:12})+speech({ABL:15}) must be below 15
	RETURN 0
ENDIF


;条件別にＯＫかダメかを記録する
;必要な習得の珠
A = 0
;必要な調教会話経験
B = 0
;必要な売春経験
C = 0

;条件別にＯＫかダメかを記録する
;習得の珠による可否(I=0:可,I&1:珠不足,I&2:経験不足)
I = 0

CALL DECIDE_ABLUP15

PRINTFORM [0] - %PALAMNAME:7% points ×{JUEL:7}/{A} ......
IF I == 0
	PRINT OK
ELSE
	SIF I & 1
		PRINT Not enough points 
	SIF I & 2
		PRINT Not enough exp 
ENDIF
PRINTL 
PRINTFORML       %EXPNAME:73%　{EXP:73}/{B} or
PRINTFORML       %EXPNAME:74%　{EXP:74}/{C}

PRINTL [100] - Stop

INPUT
IF (RESULT < 0 || RESULT > 0) && RESULT != 100
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL Requirements haven't been met
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:15 += 1

IF RESULT == 0
	JUEL:7 -= A
ENDIF

PRINTFORML %ABLNAME:15% became level {ABL:15}

RETURN 0

;--------------------------------------------------
;レベルアップに必要な珠計算,レベルアップ予告処理
;--------------------------------------------------
@DECIDE_ABLUP15
;話術はLv10が上限
SIF ABL:15 >= 10
	RETURN 0
SIF ABL:12 + ABL:15 >= 15
	RETURN 0

;条件別にＯＫかダメかを記録する
A = 0
B = 0
C = 0
I = 0

IF ABL:15 == 0
	A = 1
	B = 3
	C = 5
ELSEIF ABL:15 == 1
	A = 10
	B = 10
	C = 20
ELSEIF ABL:15 == 2
	A = 100
	B = 30
	C = 50
ELSEIF ABL:15 == 3
	A = 1500
	B = 50
	C = 100
ELSEIF ABL:15 == 4
	A = 3000
	B = 100
	C = 150
ELSEIF ABL:15 == 5
	A = 4000
	B = 120
	C = 180
ELSEIF ABL:15 == 6
	A = 5200
	B = 150
	C = 250
ELSEIF ABL:15 == 7
	A = 7500
	B = 180
	C = 320
ELSEIF ABL:15 == 8
	A = 9000
	B = 220
	C = 350
ELSEIF ABL:15 == 9
	A = 13000
	B = 250
	C = 400
ENDIF

;一線越えない
IF TALENT:27
	IF ABL:15 == 3
		TIMES A , 1.50
		TIMES B , 1.25
		TIMES C , 1.25
	ELSEIF ABL:15 == 4
		TIMES A , 2.00
		TIMES B , 1.50
		TIMES C , 1.50
	ELSEIF ABL:15 == 5
		TIMES A , 2.50
		TIMES B , 1.75
		TIMES C , 1.75
	ELSEIF ABL:15 >= 6
		TIMES A , 3.00
		TIMES B , 2.00
		TIMES C , 2.00
	ENDIF
ENDIF

;反抗的
IF TALENT:11
	TIMES A , 1.50
	TIMES B , 1.20
	TIMES C , 1.20
ENDIF
;素直
IF TALENT:13
	TIMES A , 0.90
	TIMES B , 0.95
	TIMES C , 0.95
ENDIF
;生意気
IF TALENT:16
	TIMES A , 1.25
	TIMES B , 1.15
	TIMES C , 1.15
ENDIF
;プライド高い
IF TALENT:15
	TIMES A , 1.20
	TIMES B , 1.10
	TIMES C , 1.10
ENDIF
;無関心
IF TALENT:21
	TIMES A , 1.50
	TIMES B , 1.20
	TIMES C , 1.20
ENDIF
;感情乏しい
IF TALENT:22
	TIMES A , 1.50
	TIMES B , 1.20
	TIMES C , 1.20
ENDIF
;好奇心
IF TALENT:23
	TIMES A , 0.95
	TIMES B , 0.95
	TIMES C , 0.95
ENDIF

;楽観的
IF TALENT:25
	TIMES A , 0.95
	TIMES B , 0.95
	TIMES C , 0.95
;悲観的
ELSEIF TALENT:26
	TIMES A , 1.20
	TIMES B , 1.10
	TIMES C , 1.10
ENDIF

;目立ちたがり
IF TALENT:28
	TIMES A , 0.95
	TIMES B , 0.95
	TIMES C , 0.95
ENDIF

;抑圧
IF TALENT:32
	TIMES A , 1.20
	TIMES B , 1.10
	TIMES C , 1.10
;解放
ELSEIF TALENT:33
	TIMES A , 0.90
	TIMES B , 0.90
	TIMES C , 0.90
ENDIF

;抵抗
IF TALENT:34
	TIMES A , 1.20
	TIMES B , 1.10
	TIMES C , 1.10
ENDIF

;恥じらい
IF TALENT:35
	TIMES A , 1.20
	TIMES B , 1.10
	TIMES C , 1.10
;恥薄い
ELSEIF TALENT:36
	TIMES A , 0.95
	TIMES B , 0.95
	TIMES C , 0.95
ENDIF

;習得早い
IF TALENT:50
	TIMES A , 0.80
	TIMES B , 0.80
	TIMES C , 0.80
;習得遅い
ELSEIF TALENT:51
	TIMES A , 1.20
	TIMES B , 1.10
	TIMES C , 1.10
ENDIF

;謎の魅力
IF TALENT:92
	TIMES A , 0.90
	TIMES B , 0.90
	TIMES C , 0.90
ENDIF
;小悪魔
IF TALENT:87
	TIMES A , 0.95
	TIMES B , 0.95
	TIMES C , 0.95
ENDIF
;饒舌
IF TALENT:182
	TIMES A , 0.60
	TIMES B , 0.80
	TIMES C , 0.80
ENDIF

;最低でも1回-1個は必要
SIF A < 1
	A = 1
SIF B < 1
	B = 1
SIF C < 1
	C = 1

;習得の珠は足りている?
SIF JUEL:7 < A
	I |= 1
;調教会話経験か売春経験は足りている?
SIF EXP:73 < B && EXP:74 < C
	I |= 2

IF I == 0
	RETURN 1
ELSE
	RETURN 0
ENDIF
;
;
;
