
;大型イベント専用処理
;----------------------------------
;メニュー
;----------------------------------
@CAMPAIGN_MENU
#DIM CHARA

PRINT 現在選択中のイベント...

IF FLAG:400 > 0
	TRYCALLFORM CAMPAIGN_NAME_{FLAG:400}
ELSE
	PRINT なし
ENDIF

PRINTL  
DRAWLINE

$INPUT_LOOP

IF FLAG:400 == 0
	PRINTL [0] キャンペ-ン選択
ELSE
	PRINTL [1] 奴隷選別(気力-100)
	PRINTL [2] 奴隷の派遣
	;PRINTL [3] 奴隷の強化状況確認
ENDIF

PRINTL [999] 戻る

INPUT

IF RESULT == 999
	RETURN 0
ELSEIF RESULT == 0 && FLAG:400 != 0
	PRINTL *キャンペ-ン中は変更できません*
	GOTO INPUT_LOOP
ELSEIF RESULT == 0
	CALL SELECT_CAMPAIGN
ELSEIF FLAG:400 == 0
	PRINTL *キャンペ-ンを選択してください*
	GOTO INPUT_LOOP
ELSEIF RESULT == 1
	;奴隷選別
	;キャラメイクできる
	PRINTL 気力を100消費します......
	IF BASE:0:1 < 100
		PRINTW *気力が足りない!*
		GOTO INPUT_LOOP
	ENDIF
	
	CALL RAND_CHARA_MAKE
	SIF RESULT == 0
		GOTO INPUT_LOOP
	CHARA = RESULT
	;成功した場合気力100消費
	BASE:0:1 -= 100
	;素質ON
	LOCAL = FLAG:400 + 360
	TALENT:CHARA:LOCAL = 1
	
ELSEIF RESULT == 2
	;奴隷派遣
	$INPUT_LOOP_2

	PRINTL 対象の奴隷を敵地に派遣します
	PRINTL 誰を派遣させますか?

	CALL LIFE_LIST
	PRINTL  [999] - 戻る
	
	INPUT
	
	;イベント素質
	LOCAL = FLAG:400 + 360
	IF RESULT == 999
		GOTO INPUT_LOOP
	ELSEIF RESULT < 0 || RESULT >= CHARANUM
		GOTO INPUT_LOOP_2
	;臨死中のキャラは排除
	ELSEIF BASE:RESULT:0 < 1
		GOTO INPUT_LOOP_2
	ELSEIF TALENT:RESULT:LOCAL == 0
		PRINTFORMW [%TALENTNAME:LOCAL%]を持たない奴隷は派遣できません
		GOTO INPUT_LOOP_2
	ELSEIF TALENT:RESULT:292
		PRINTFORMW %SAVESTR:RESULT%は影のため派遣できません
		GOTO INPUT_LOOP_2
	ELSEIF CFLAG:RESULT:1 == 12
		PRINTFORMW %SAVESTR:RESULT%はすでに派遣中です
		GOTO INPUT_LOOP_2
	ELSEIF CFLAG:RESULT:1 != 0
		PRINTFORMW %SAVESTR:RESULT%は派遣できる状態にありません
		GOTO INPUT_LOOP_2
	ENDIF
	CHARA = RESULT
	CFLAG:CHARA:1 = 12
	;現在階層と目標階層をリセット
	CFLAG:CHARA:501 = 1
	CFLAG:CHARA:507 = 0
	CFLAG:CHARA:520 = 0
	CFLAG:CHARA:521 = 1
	PRINTFORMW %SAVESTR:CHARA%を派遣しました
	GOTO INPUT_LOOP_2
ENDIF

GOTO INPUT_LOOP

;----------------------------------
@SELECT_CAMPAIGN
;----------------------------------
;キャンペーン選び放題
;最大20個だけど,20個もイベント作らないだろう...的な

FOR LOCAL, 1, 21
	;イベントERBがあればメニューが表示される
	TRYCALLFORM CAMPAIGN_EXIST_{LOCAL}
NEXT
PRINTL [999] 戻る

INPUT

SIF RESULT == 999
	RETURN 0
;イベントERBがあれば初期設定をしてくれる
TRYCALLFORM CAMPAIGN_SET_{RESULT}
;共通初期設定
;深度リセット
FLAG:401 = 0

RETURN 0

;----------------------------------
@CAMPAIGN_ROOM,ARG:0
;----------------------------------
;敵ダンジョンの施設をロードする
;ARG:0=階層

SIF FLAG:400 < 1
	RETURN 0

RESULT = 0
TRYCALLFORM CAMPAIGN_ROOM_{FLAG:400},ARG:0

RETURN RESULT

;----------------------------------
@CAMPAIGN_ROOM_EXTRA,ARG:0
;----------------------------------
;敵ダンジョンの施設拡張をロードする

SIF FLAG:400 < 1
	RETURN 0

RESULT = 0
TRYCALLFORM CAMPAIGN_ROOM_EXTRA_{FLAG:400},ARG:0

RETURN RESULT

;----------------------------------
@CAMPAIGN_QUEST,ARG:0
;----------------------------------
;階層踏破時に発生するイベント
;ARG:0はリーダー

SIF FLAG:400 < 1
	RETURN 0
;階層を見て進行度と比較
LOCAL = CFLAG:(ARG:0):501
IF LOCAL > FLAG:401
	PRINTL ―STORY―
	FORCEWAIT
	TRYCALLFORM CAMPAIGN_STORY_{FLAG:400}
	;ストーリーが進む
	FLAG:401 += 1
ENDIF
RESULT = 0
TRYCALLFORM CAMPAIGN_QUEST_{FLAG:400},ARG:0
RETURN RESULT

;----------------------------------
@CAMPAIGN_TRAP,ARG:0
;----------------------------------
;階層ごとに発生する罠
;ARG:0はTRAP_NUM(@DUNGEON_TRAP参照)

SIF FLAG:400 < 1
	RETURN 0
RESULT = 0
TRYCALLFORM CAMPAIGN_TRAP_{FLAG:400},ARG:0
RETURN RESULT

;----------------------------------
@CAMPAIGN_EQUIP_SELECT,ARG:0
;----------------------------------
;敵ダンジョンの指輪宝箱をロードする
;ARG:0=階層
SIF FLAG:400 < 1
	RETURN 0
RESULT = 0
TRYCALLFORM CAMPAIGN_EQUIP_SELECT_{FLAG:400},ARG:0
RETURN RESULT

;----------------------------------
@CAMPAIGN_MONSTER_LIST,ARG:0
;----------------------------------
;敵ダンジョンの出現敵をロードする
;ARG:0=階層
;イベント未発の場合,スケルトン
SIF FLAG:400 < 1
	RETURN 190
RESULT = 190
TRYCALLFORM CAMPAIGN_MONSTER_LIST_{FLAG:400},ARG:0
RETURN RESULT

;----------------------------------
@CAMPAIGN_MONSTER_EXTRA,ARG:0
;----------------------------------
;敵ダンジョンの出現敵拡張をロードする
;ARG:0=INUM
;イベント未発の場合,スケルトン
SIF FLAG:400 < 1
	RETURN 190
RESULT = 190
TRYCALLFORM CAMPAIGN_MONSTER_EXTRA_{FLAG:400},ARG:0
RETURN RESULT

;----------------------------------
@CAMPAIGN_DUNGEON_LV
;----------------------------------
;ダンジョンLVをロードする
SIF FLAG:400 < 1
	RETURN 0
RESULT = 0
TRYCALLFORM CAMPAIGN_DUNGEON_LV_{FLAG:400}
RETURN RESULT

;----------------------------------
@CAMPAIGN_GAMEOVER
;----------------------------------
;ゲームオーバー判定
SIF FLAG:400 < 1
	RETURN 0

IF BASE:0:1 <= 0
	PRINTFORMW ***あなたの体力が尽きました***
	PRINTFORMW キャンペ-ンを終了します
	FLAG:400 = 0
	BASE:0:1 = 1
	FOR LOCAL, 0, CHARANUM
		;ダンジョン攻略キャンセル
		SIF CFLAG:LOCAL:1 == 12
			CFLAG:LOCAL:1 = 0
	NEXT
ENDIF

RETURN 0

;----------------------------------
@CAMPAIGN_EXP_PILLORY,ARG:0
;----------------------------------
;晒し台による経験値取得
SIF FLAG:400 < 1
	RETURN 0

LOCAL:1 = (CFLAG:(ARG:0):661+CFLAG:(ARG:0):662+CFLAG:(ARG:0):663+CFLAG:(ARG:0):664+CFLAG:(ARG:0):665) / 5 + 1

FOR LOCAL, 0, CHARANUM
	;ダンジョン攻略支援.経験値取得
	SIF CFLAG:LOCAL:1 == 12
		EXP:LOCAL:80 += LOCAL:1
NEXT

PRINTFORMW 攻略中の奴隷は搾り取られたエネルギ-を受けて{LOCAL:1}の経験値を得た

RETURN 0

;----------------------------------
@CAMPAIGN_ENDING,ARG:0
;----------------------------------
;エンディング
SIF FLAG:400 < 1
	RETURN 0
RESULT = 0
TRYCALLFORM CAMPAIGN_ENDING_{FLAG:400}
FLAG:400 = 0
RETURN RESULT

