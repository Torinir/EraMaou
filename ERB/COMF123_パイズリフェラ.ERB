;eraIM@Sから導入しました(eramaou)

;-------------------------------------------------
;パイズリフェラ
;屈服と習得が他の奉仕系と比べると多めに取得できる
;-------------------------------------------------
@COM123
SELECTCOM = 123
PRINTL パイズリフェラ

;-------------------------------------------------
;実行できるかの判定
;-------------------------------------------------
A = 0
S = 0

;すべての命令に共通の要素を考慮
;(従順が高いと命令に従いやすいなど)
CALL COM_ORDER

;ABL:欲望
IF ABL:11
	SIF S
		PRINT  + 
	A += ABL:11 * 1
	PRINTS ABLNAME:11
	PRINTV 'LV,ABL:11,'(,ABL:11 * 1,')
	S = 1
ENDIF
;ABL:奉仕精神
IF ABL:16
	SIF S
		PRINT  + 
	A += ABL:16 * 4
	PRINTS ABLNAME:16
	PRINTV 'LV,ABL:16,'(,ABL:16 * 4,')
	S = 1
ENDIF
;ABL:精液中毒
IF ABL:32
	SIF S
		PRINT  + 
	A += ABL:32 * 3
	PRINTS ABLNAME:32
	PRINTV 'LV,ABL:32,'(,ABL:32 * 3,')
	S = 1
ENDIF

;快楽刻印
IF MARK:1
	SIF S
		PRINT  + 
	A += MARK:1 * 1
	PRINTS MARKNAME:1
	PRINTV 'LV,MARK:1,'(,MARK:1 * 1,')
	S = 1
ENDIF


;PALAM:欲情
IF PALAM:5 < PALAMLV:1
	L = 0
ELSEIF PALAM:5 < PALAMLV:2
	L = 1
ELSEIF PALAM:5 < PALAMLV:3
	L = 2
ELSEIF PALAM:5 < PALAMLV:4
	L = 3
ELSEIF PALAM:5 < PALAMLV:5
	L = 4
ELSE
	L = 5
ENDIF
IF L
	SIF S
		PRINT  + 
	A += L * 1
	PRINTS PALAMNAME:5
	PRINTV 'LV,L,'(,L * 1,')
	S = 1
ENDIF

;恥じらい
IF TALENT:35
	PRINT  - 
	A -= 3
	PRINTS TALENTNAME:35
	PRINTV '(,3,')
	S = 1
ENDIF
;汚臭鈍感
IF TALENT:61
	SIF S
		PRINT  + 
	A += 1
	PRINTS TALENTNAME:61
	PRINTV '(,1,')
	S = 1
ENDIF
;汚臭敏感
IF TALENT:62
	PRINT  - 
	A -= 3
	PRINTS TALENTNAME:62
	PRINTV '(,3,')
	S = 1
ENDIF
;献身的
IF TALENT:63
	SIF S
		PRINT  + 
	A += 6
	PRINTS TALENTNAME:63
	PRINTV '(,6,')
	S = 1
ENDIF
;快感の否定
IF TALENT:71
	PRINT  - 
	A -= 3
	PRINTS TALENTNAME:71
	PRINTV '(,3,')
	S = 1
ENDIF
;男嫌い
IF TALENT:82 && TALENT:PLAYER:122
	PRINT  - 
	A -= 12
	PRINTS TALENTNAME:82
	PRINTV '(,12,')
	S = 1
ENDIF
;愛
IF TALENT:85 && ASSIPLAY == 0
	SIF S
		PRINT  + 
	A += 5
	PRINTS TALENTNAME:85
	PRINTV '(,5,')
	S = 1
ENDIF
;調教者がふたなり
IF TALENT:PLAYER:121
	SIF S
		PRINT  + 
	A += 8
	PRINTS TALENTNAME:121
	PRINTV '(,8,')
	S = 1
ENDIF

Y = 0
;愛液の汚れ
SIF STAIN:PLAYER:2 & 1
	Y += 1
;精液の汚れ
SIF STAIN:PLAYER:2 & 4
	Y += 3
;アナルの汚れ
SIF STAIN:PLAYER:2 & 8
	Y += 7
;母乳の汚れ
SIF STAIN:PLAYER:2 & 16
	Y += 1
;尿の汚れ
SIF STAIN:PLAYER:4 & 32
	Y += 3

SIF TALENT:61
	Y /= 3
SIF TALENT:62
	Y *= 2
;汚れあり
IF Y
	PRINT  - 
	A -= Y
	;汚臭鈍感
	IF TALENT:61
		PRINT 汚れあり,
		PRINTS TALENTNAME:61
	ELSEIF TALENT:62
		PRINT 汚れあり,
		PRINTS TALENTNAME:62
	ELSE
		PRINT 汚れあり
	ENDIF
	PRINTV '(,Y,')
	S = 1
ENDIF

;合計を表示(36以上で実行)
PRINT  = 
PRINTV A

V = 36
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT Move points:
PRINTV V

WAIT

;実行できない
SIF A < V
	RETURN 0

;SAVESTR:22 = パイズリフェラ
CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;実行決定
;-------------------------------------------------
PRINTFORML %EXPNAME:22%+1
EXP:22 += 1

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
IF TALENT:47
	LOSEBASE:0 += 20
	LOSEBASE:1 += 80
ELSE
	LOSEBASE:0 += 30
	LOSEBASE:1 += 160
ENDIF

SOURCE:13 = 2200
SOURCE:14 = 900

;上のほうで計算した汚れデータ
SOURCE:8 = Y*40 + 100

;ABL:奉仕精神をみる
IF ABL:16 == 0
	SOURCE:4 = 620
	SOURCE:5 = 150
	TIMES SOURCE:8 , 4.00
ELSEIF ABL:16 == 1
	SOURCE:4 = 700
	SOURCE:5 = 300
	TIMES SOURCE:8 , 2.50
ELSEIF ABL:16 == 2
	SOURCE:4 = 820
	SOURCE:5 = 600
	TIMES SOURCE:8 , 1.50
ELSEIF ABL:16 == 3
	SOURCE:4 = 940
	SOURCE:5 = 900
	TIMES SOURCE:8 , 1.00
ELSEIF ABL:16 == 4
	SOURCE:4 = 1100
	SOURCE:5 = 1500
	TIMES SOURCE:8 , 0.50
ELSE
	SOURCE:4 = 1260
	SOURCE:5 = 2200
	TIMES SOURCE:8 , 0.10
ENDIF

;ABL:Ｂ感覚をみる
IF ABL:1 == 0
	A = 100
ELSEIF ABL:1 == 1
	A = 200
ELSEIF ABL:1 == 2
	A = 400
ELSEIF ABL:1 == 3
	A = 800
ELSEIF ABL:1 == 4
	A = 1200
ELSE
	A = 1500
ENDIF

;巨乳\超乳
IF TALENT:110
	TIMES A , 1.20
ELSEIF TALENT:119
	TIMES A , 1.30
ENDIF
IF TALENT:108
	TIMES A , 1.20
ELSEIF TALENT:107
	TIMES A , 0.70
ENDIF

SOURCE:17 += A
;ABL:技巧をみる
IF ABL:12 == 0
	TIMES SOURCE:4 , 0.80
	TIMES SOURCE:5 , 0.80
ELSEIF ABL:12 == 1
	TIMES SOURCE:4 , 1.00
	TIMES SOURCE:5 , 1.00
ELSEIF ABL:12 == 2
	TIMES SOURCE:4 , 1.20
	TIMES SOURCE:5 , 1.20
ELSEIF ABL:12 == 3
	TIMES SOURCE:4 , 1.50
	TIMES SOURCE:5 , 1.40
ELSEIF ABL:12 == 4
	TIMES SOURCE:4 , 2.00
	TIMES SOURCE:5 , 1.80
ELSE
	TIMES SOURCE:4 , 2.50
	TIMES SOURCE:5 , 2.20
ENDIF

;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
B = 0

;ABL:技巧をみる
IF ABL:12 == 0
	B = 1200
ELSEIF ABL:12 == 1
	B = 1700
ELSEIF ABL:12 == 2
	B = 2300
ELSEIF ABL:12 == 3
	B = 3000
ELSEIF ABL:12 == 4
	B = 3600
ELSE
	B = 4200
ENDIF

;ABL:従順をみる
IF ABL:10 == 0
	TIMES B , 0.80
ELSEIF ABL:10 == 1
	TIMES B , 0.90
ELSEIF ABL:10 == 2
	TIMES B , 1.00
ELSEIF ABL:10 == 3
	TIMES B , 1.10
ELSEIF ABL:10 == 4
	TIMES B , 1.20
ELSE
	TIMES B , 1.30
ENDIF

;ABL:奉仕技術をみる
IF ABL:13 == 0
	TIMES B , 0.50
ELSEIF ABL:13 == 1
	TIMES B , 0.80
ELSEIF ABL:13 == 2
	TIMES B , 1.20
ELSEIF ABL:13 == 3
	TIMES B , 1.50
ELSEIF ABL:13 == 4
	TIMES B , 1.80
ELSE
	TIMES B , 2.40
ENDIF

;ABL:精液中毒をみる
IF ABL:32 == 0
	TIMES B , 1.00
ELSEIF ABL:32 == 1
	TIMES B , 1.20
ELSEIF ABL:32 == 2
	TIMES B , 1.30
ELSEIF ABL:32 == 3
	TIMES B , 1.50
ELSEIF ABL:32 == 4
	TIMES B , 1.70
ELSE
	TIMES B , 2.00
ENDIF

;TALENT:舌使いをみる
SIF TALENT:52
	TIMES B , 2.00

;プレイヤーのABL:Ｃ感覚をみる
IF ABL:PLAYER:0 == 0
	TIMES B , 1.00
ELSEIF ABL:PLAYER:0 == 1
	TIMES B , 1.50
ELSEIF ABL:PLAYER:0 == 2
	TIMES B , 2.00
ELSEIF ABL:PLAYER:0 == 3
	TIMES B , 2.50
ELSEIF ABL:PLAYER:0 == 4
	TIMES B , 3.50
ELSE
	TIMES B , 5.00
ENDIF

;超乳
IF TALENT:119
	TIMES B , 1.30
;爆乳
ELSEIF TALENT:114
	TIMES B , 1.20
;巨乳
ELSEIF TALENT:110
	TIMES B , 1.10
;貧乳
ELSEIF TALENT:109
	TIMES B , 0.90
ELSE
	TIMES B , 1.00
ENDIF

SIF TALENT:PLAYER:119 || TALENT:PLAYER:122  || TALENT:PLAYER:121
	BASE:PLAYER:2 += B

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
S = BASE:PLAYER:2
EJAC = MAXBASE:PLAYER:2

IF  S > EJAC * 2
	E = 2
ELSEIF S > EJAC
	E = 1
ELSE
	E = 0
ENDIF

IF E
;射精している
	TIMES SOURCE:4 , 3.00

	;ABL:精液中毒をみる
	IF ABL:32 == 0
		SOURCE:7 = 0
		TIMES SOURCE:5 , 2.00
		TIMES SOURCE:13, 6.00
	ELSEIF ABL:32 == 1
		SOURCE:7 = 500
		TIMES SOURCE:5 , 3.00
		TIMES SOURCE:13, 4.50
	ELSEIF ABL:32 == 2
		SOURCE:7 = 1200
		TIMES SOURCE:5 , 4.00
		TIMES SOURCE:13, 3.50
	ELSEIF ABL:32 == 3
		SOURCE:7 = 3000
		TIMES SOURCE:5 , 6.00
		TIMES SOURCE:13, 3.00
	ELSEIF ABL:32 == 4
		SOURCE:7 = 6000
		TIMES SOURCE:5 , 9.00
		TIMES SOURCE:13, 2.00
	ELSE
		SOURCE:7 = 12000
		TIMES SOURCE:5 , 15.00
		TIMES SOURCE:13, 1.50
	ENDIF
ENDIF

;大量射精
IF E == 2
	TIMES SOURCE:7 , 2.00
	TIMES SOURCE:5 , 1.50

	EXP:PLAYER:3 += 2
	EXP:20 += 9
	PRINTL High quantity ejaculation
	PRINTL Semen exp+９
	;精液汚れ
	STAIN:PLAYER:2 |= 4

	BASE:PLAYER:2 -= EJAC*2
	SIF BASE:PLAYER:2 >= EJAC
		BASE:PLAYER:2 = EJAC-1

	;口で射精させたフラグ
	TFLAG:0 = 2
;通常の射精
ELSEIF E == 1
	EXP:PLAYER:3 += 1
	EXP:20 += 3
	PRINTL Ejaculation
	PRINTL Semen exp+3
	;精液汚れ
	STAIN:PLAYER:2 |= 4

	BASE:PLAYER:2 -= EJAC
	SIF BASE:PLAYER:2 >= EJAC
		BASE:PLAYER:2 = EJAC-1

	;口で射精させたフラグ
	TFLAG:0 = 1
ENDIF

;-------------------------------------------------
;汚れ
;-------------------------------------------------
;奴隷の口⇔調教者のＰの汚れが移動
STAIN:0 |= STAIN:PLAYER:2
STAIN:PLAYER:2 |= STAIN:0

;奉仕精神ＬV2以上,技巧ＬV2以上なら汚れをなめ取る
IF ABL:16 >= 2 && ABL:12 >= 2
	STAIN:PLAYER:2 = 2
	SIF E >= 1
		TFLAG:8 = 1
ENDIF

;-------------------------------------------------
;経験上昇
;-------------------------------------------------
IF TALENT:122 == 0 && TALENT:PLAYER:122 == 0
	PRINTS EXPNAME:40
	PRINTL +7
	EXP:40 += 7
ELSEIF TALENT:122 == 1 && TALENT:PLAYER:122 == 1
	PRINTS EXPNAME:41
	PRINTL +7
	EXP:41 += 7
ENDIF

SIF ASSIPLAY == 0 && EXP:0 >= EXPLV:3
		TFLAG:30 += 1

IF TALENT:122
	E = 2
ELSE
	E = 1
ENDIF
;ファーストキス
IF CFLAG:16 == -1
	CFLAG:16 = NO:PLAYER + 201
	CSTR:4 = %SAVESTR:PLAYER%
	TFLAG:13 = 1
ENDIF
;愛情経験
IF CFLAG:2 >= 1000 && ASSIPLAY == 0
	PRINTFORML %EXPNAME:23%+{E}
	EXP:23 += E
ENDIF
E = 0

;調教者がふたなり
IF TALENT:PLAYER:121
	SOURCE:13 /= 2
ENDIF

TFLAG:100 = 1

;屈服刻印３に相当
TFLAG:200 = 3

RETURN 1
