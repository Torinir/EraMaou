;eramaou追加コマンド

;-------------------------------------------------
;オーク
;くそっ,オークめ...なんて卑劣な! レベル40以上
;-------------------------------------------------
@COM204

PRINTL オ-ク

CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;戦闘値の計算
;-------------------------------------------------
LOSEBASE:0 += CFLAG:0:9 * 2
SIF BASE:0 <= 0
	LOSEBASE:0 /= 4
LOSEBASE:1 += CFLAG:0:9 * 15

CALL ARENA_SLAVE_POINT

;戦闘ポイントが低ければ追加ダメージ
IF RESULT < (3 * CFLAG:0:9)
	;気力が0なら追加ダメージ無し
	IF BASE:1 <= 0
		PRINTFORMW オ-クは%SAVESTR:TARGET%の両足を掴んで広げると股間にむしゃぶりつくように匂いをかぎ始めた
	ELSE
		;追加ダメージ
		PRINTFORMW %SAVESTR:TARGET%はオ-クの豪傑相手に苦戦しているようだ
		LOSEBASE:0 += CFLAG:0:9 * 2
		LOSEBASE:1 += CFLAG:0:9 * 2
		IF BASE:1 < LOSEBASE:1
			PRINTFORMW そしてオ-クの痛恨の一撃で%SAVESTR:TARGET%は武器を取り落としてしまった
			PRINTL <Slave capitulation>
		ENDIF
	ENDIF
ELSE
	PRINTFORMW %SAVESTR:TARGET%はオ-クの豪傑の一撃をなんとかかわしている
ENDIF

;コロシアム収入
;TFLAG:402 += (LOSEBASE:0 * 3) + RAND:RESULT

TFLAG:400 = 204
;戦闘後の処理
CALL COM_AFTER_ARENA
SIF RESULT == 0
	RETURN 1

;-------------------------------------------------
;各種コマンドへ
;-------------------------------------------------
$INPUT_LOOP_0
PRINTL どこを陵辱しますか?
PRINTL [0] - 口
PRINTL [1] - 胸
SIF TALENT:122 == 0
	PRINTL [2] - 膣
PRINTL [3] - アナル
PRINTL [999] 陵辱しない

INPUT

IF RESULT == 0
	PRINTL <オ-ク-フェラチオ>
	SELECTCOM = 31
	CALL COM31
	;フェラチオ実行不可時
	SIF RESULT == 0
		RETURN 0
	;コロシアム収入
	TFLAG:402 += (LOSEBASE:0 * 3) + RAND:RESULT
ELSEIF RESULT == 1
	PRINTL <オ-ク-胸愛撫>
	SELECTCOM = 5
	CALL COM5
	;コロシアム収入
	TFLAG:402 += (LOSEBASE:0 * 3) + RAND:RESULT
ELSEIF RESULT == 2
	;対象がオトコなら戻る
	SIF TALENT:122
		RETURN 0
	PRINTL <オ-ク-後背位>
	SELECTCOM = 21
	CALL COM21
	;処女を奪わせなかった場合
	SIF RESULT == 0
		RETURN 0
	;コロシアム収入
	TFLAG:402 += (LOSEBASE:0 * 3) + RAND:RESULT
ELSEIF RESULT == 3
	PRINTL <オ-ク-後背位アナル>
	SELECTCOM = 27
	CALL COM27
	;コロシアム収入
	TFLAG:402 += (LOSEBASE:0 * 3) + RAND:RESULT
ELSEIF RESULT == 999
	PRINTFORMW %NAME:MASTER%はオ-クを下がらせた......
	RETURN 0
ELSE
	GOTO INPUT_LOOP_0
ENDIF

;-------------------------------------------------
;射精チェック
;獣姦コマンドの改編です.
;-------------------------------------------------
SIF MAXBASE:MASTER:4 == 0
	GOTO END_EJAC

B = 0

;ABL:技巧をみる
IF ABL:12 == 0
	B = 450
ELSEIF ABL:12 == 1
	B = 1000
ELSEIF ABL:12 == 2
	B = 1600
ELSEIF ABL:12 == 3
	B = 2200
ELSEIF ABL:12 == 4
	B = 2700
ELSE
	B = 3200
ENDIF

;ABL:従順をみる
IF ABL:10 == 0
	TIMES B , 0.30
ELSEIF ABL:10 == 1
	TIMES B , 0.50
ELSEIF ABL:10 == 2
	TIMES B , 0.70
ELSEIF ABL:10 == 3
	TIMES B , 1.00
ELSEIF ABL:10 == 4
	TIMES B , 1.20
ELSE
	TIMES B , 1.30
ENDIF

;PALAM:欲情をみる
IF PALAM:5 < PALAMLV:1
	TIMES B , 1.00
ELSEIF PALAM:5 < PALAMLV:2
	TIMES B , 1.10
ELSEIF PALAM:5 < PALAMLV:3
	TIMES B , 1.20
ELSEIF PALAM:5 < PALAMLV:4
	TIMES B , 1.30
ELSEIF PALAM:5 < PALAMLV:5
	TIMES B , 1.40
ELSE
	TIMES B , 1.50
ENDIF

;キス
IF SELECTCOM == 6
	B = 0
;後背位
ELSEIF SELECTCOM == 21
	TIMES B , 1.00
;後背位アナル
ELSEIF SELECTCOM == 27
	TIMES B , 1.50
;手淫
ELSEIF SELECTCOM == 30
	TIMES B , 0.80
;フェラチオ
ELSEIF SELECTCOM == 31
	TIMES B , 1.20
;騎乗位
ELSEIF SELECTCOM == 34
	TIMES B , 1.50
ELSE
	B = 0
ENDIF

BASE:MASTER:4 += B

S = BASE:MASTER:4
EJAC = MAXBASE:MASTER:4

IF  S > EJAC * 2
	E = 2
ELSEIF S > EJAC
	E = 1
ELSE
	E = 0
ENDIF

IF E
;射精している
	TIMES SOURCE:4 , 3.00

	;ABL:精液中毒をみる
	IF ABL:32 == 0
		SOURCE:7 = 0
		TIMES SOURCE:5 , 2.00
		TIMES SOURCE:13, 2.00
	ELSEIF ABL:32 == 1
		SOURCE:7 = 200
		TIMES SOURCE:5 , 2.50
		TIMES SOURCE:13, 1.60
	ELSEIF ABL:32 == 2
		SOURCE:7 = 500
		TIMES SOURCE:5 , 3.00
		TIMES SOURCE:13, 1.00
	ELSEIF ABL:32 == 3
		SOURCE:7 = 1200
		TIMES SOURCE:5 , 4.50
		TIMES SOURCE:13, 0.70
	ELSEIF ABL:32 == 4
		SOURCE:7 = 2500
		TIMES SOURCE:5 , 6.00
		TIMES SOURCE:13, 0.40
	ELSE
		SOURCE:7 = 5000
		TIMES SOURCE:5 , 8.00
		TIMES SOURCE:13, 0.10
	ENDIF
ENDIF

;大量射精
IF E == 2
	EXP:20 += 3
	PRINTL モンスタ-High quantity ejaculation
	PRINTL Semen exp+3

	BASE:MASTER:4 -= EJAC*2
	SIF BASE:MASTER:4 >= EJAC
		BASE:MASTER:4 = EJAC-1
	;膣内射精フラグ(モンスター)
	SIF SELECTCOM == 21 || SELECTCOM == 34
		TFLAG:38 = 2
	;フェラチオで射精フラグ
	SIF SELECTCOM == 31
		TFLAG:0 = 2
	;セックスで射精フラグ
	SIF SELECTCOM == 21 || SELECTCOM == 27
		TFLAG:2 = 2
;通常の射精
ELSEIF E == 1
	EXP:20 += 1
	PRINTL モンスタ-Ejaculation
	PRINTL Semen exp+1

	BASE:MASTER:4 -= EJAC
	SIF BASE:MASTER:4 >= EJAC
		BASE:MASTER:4 = EJAC-1
	;膣内射精フラグ(モンスター)
	SIF SELECTCOM == 21 || SELECTCOM == 34
		TFLAG:38 = 1
	;フェラチオで射精フラグ
	SIF SELECTCOM == 31
		TFLAG:0 = 1
	;セックスで射精フラグ
	SIF SELECTCOM == 21 || SELECTCOM == 27
		TFLAG:2 = 1
ENDIF

$END_EJAC

;-------------------------------------------------
;汚れ
;-------------------------------------------------
SIF SELECTCOM == 21
	STAIN:3 |= 2
SIF SELECTCOM == 27
	STAIN:4 |= 2
SIF SELECTCOM == 30
	STAIN:1 |= 2
SIF SELECTCOM == 31
	STAIN:0 |= 2
SIF SELECTCOM == 37
	STAIN:0 |= 8
SIF SELECTCOM == 21 && E > 0
	STAIN:3 |= 4
SIF SELECTCOM == 27 && E > 0
	STAIN:4 |= 4
SIF SELECTCOM == 30 && E > 0
	STAIN:1 |= 4
SIF SELECTCOM == 31 && E > 0
	STAIN:0 |= 4

;コロシアムモンスターが射精フラグ
TFLAG:15 = E

RETURN 1
;
;
;
