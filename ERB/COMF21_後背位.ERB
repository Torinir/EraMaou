;eraIM@Sから導入しました(eramaou)

;-------------------------------------------------
;後背位
;セックス系コマンド:調教者のペニスを調教対象のヴァギナに挿入
;正常位に比べて露出が高く情愛が低い
;-------------------------------------------------
@COM21
;処女を奪うかどうかの確認
CALL CONFIRM_LOST_VIRGIN
SIF RESULT == 0
	RETURN 0

;コンドームを使うかの確認
CALL CONFIRM_CONDOM
SIF !RESULT
	RETURN 0
	
IF (ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0)
	;まず後背位-胸愛撫か後背位-スパンキング,次に立ちバックを実行し,かつ実行条件を満たすと後背位SPに
	;(前々回のコマンドが後背位ＳＰで立ちバックからでも可能)
	IF (TFLAG:59 == 131 || TFLAG:59 == 132 || TFLAG:59 == 134) && PREVCOM == 133
		CALL COM_ABLE134
		SIF RESULT == 1
			JUMP COM134
	ENDIF
ENDIF

;前回の調教が3Pのとき3Pへ
TFLAG:42 = 0
IF PREVCOM == 64
	CALL COM_ABLE64
	IF RESULT == 1
		TFLAG:42 = 1
		JUMP COM64
	ENDIF
;調教者が前回:助手で今回:主人か,前回:主人で今回:助手のとき
ELSEIF (ASSIPLAY && TFLAG:50 == 0) || (ASSIPLAY == 0 && TFLAG:50)
	;前回の調教が後背位アナルかフェラチオかイラマチオのときは3Pへ
	IF PREVCOM == 27 || PREVCOM == 31 || PREVCOM == 80
		CALL COM_ABLE64
		SIF RESULT == 1
			JUMP COM64
	ENDIF
ENDIF

;前のプレイが後背位か後背位-胸愛撫か後背位-スパンキングか立ちバックか後背位SPかつ調教者の技巧3以上の場合,挿入Ｇスポ責めまたは挿入子宮口責めへ派生
;V感覚が高く欲情しているほど子宮口へ行きやすい
;***FLAG:71の判定をキャンセル
;IF FLAG:71 == 0
	IF (PREVCOM == 21 || PREVCOM == 131 || PREVCOM == 132 || PREVCOM == 133 || PREVCOM == 134) && ABL:PLAYER:12 > 2 && ((ASSIPLAY && TFLAG:50) || (ASSIPLAY == 0 && TFLAG:50 == 0))
		;欲情
		IF PALAM:5 < PALAMLV:1
			A = 1
		ELSEIF PALAM:5 < PALAMLV:2
			A = 2
		ELSEIF PALAM:5 < PALAMLV:3
			A = 3
		ELSEIF PALAM:5 < PALAMLV:4
			A = 4
		ELSE
			A = 5
		ENDIF
		B = RAND:11
		A *= B
		B = RAND:11
		A += B*ABL:2
		;Aの値は0～100
		;後背位だと50以上で子宮口
		IF A >= 50
			CALL COM_ABLE121
			SIF RESULT == 1
			JUMP COM121
		ELSE
			CALL COM_ABLE120
			SIF RESULT == 1
			JUMP COM120
		ENDIF
	ENDIF
;ENDIF

PRINTL Doggy Style
;SAVESTR:22 = 後背位
CALL TRAIN_MESSAGE_B

;V経験を伴うコマンドのフラグ(処女膜再生対応)
TFLAG:19 = 1
SIF ASSIPLAY == 0 && EXP:0 == 0 && TEQUIP:89 == 0 && TEQUIP:55 == 0
	TFLAG:20 = 1

;調教者の射精チェック
CALL COM_EJAC_PLAYER_SEX

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOSEBASE:0 += 50
LOSEBASE:1 += 100

SOURCE:12 = 800

;ABL:V感覚をみる
IF ABL:2 == 0
	SOURCE:1 = 40
	SOURCE:3 = 50
ELSEIF ABL:2 == 1
	SOURCE:1 = 150
	SOURCE:3 = 150
ELSEIF ABL:2 == 2
	SOURCE:1 = 400
	SOURCE:3 = 250
ELSEIF ABL:2 == 3
	SOURCE:1 = 1000
	SOURCE:3 = 350
ELSEIF ABL:2 == 4
	SOURCE:1 = 1700
	SOURCE:3 = 600
ELSE
	SOURCE:1 = 2200
	SOURCE:3 = 850
ENDIF

;EXP:V経験をみる
IF EXP:0 < EXPLV:1
	TIMES SOURCE:1 , 0.20
	SOURCE:6 = 5000

	IF ASSIPLAY && TALENT:PLAYER:122 == 0
		EXP:50 += 1
		PRINTFORML %EXPNAME:50%+1
	ENDIF
ELSEIF EXP:0 < EXPLV:2
	TIMES SOURCE:1 , 0.60
	SOURCE:6 = 220
ELSEIF EXP:0 < EXPLV:3
	TIMES SOURCE:1 , 1.00
	SOURCE:6 = 30
ELSEIF EXP:0 < EXPLV:4
	TIMES SOURCE:1 , 1.20
	SOURCE:6 = 5
ELSEIF EXP:0 < EXPLV:5
	TIMES SOURCE:1 , 1.30
	SOURCE:6 = 0
ELSE
	TIMES SOURCE:1 , 1.80
	SOURCE:6 = 0
ENDIF

;PALAM:潤滑をみる
IF PALAM:3 < PALAMLV:1
	TIMES SOURCE:1 , 0.10
	SOURCE:6 += 900
	TIMES SOURCE:6 , 3.00
ELSEIF PALAM:3 < PALAMLV:2
	TIMES SOURCE:1 , 0.40
	SOURCE:6 += 250
	TIMES SOURCE:6 , 1.00
ELSEIF PALAM:3 < PALAMLV:3
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:6 , 0.50
ELSEIF PALAM:3 < PALAMLV:4
	TIMES SOURCE:1 , 1.40
	TIMES SOURCE:6 , 0.20
ELSEIF PALAM:3 >= PALAMLV:4
	TIMES SOURCE:1 , 1.80
	TIMES SOURCE:6 , 0.10
ENDIF

;調教者がふたなり
IF ASSIPLAY
	SIF TALENT:ASSI:121
		TIMES SOURCE:1 , 2.50
ENDIF

;大柄
SIF TALENT:99
	TIMES SOURCE:6 , 1.80
;小柄体形
SIF TALENT:100
	TIMES SOURCE:6 , 2.00

;貞操観念
IF TALENT:30
	IF EXP:0 == 0
		TIMES SOURCE:3 , 0.60
		SOURCE:15 = 10000
	ELSE
		TIMES SOURCE:3 , 0.60
		SOURCE:15 = 1000
	ENDIF
;貞操無頓着
ELSEIF TALENT:31
	IF EXP:0 == 0
		TIMES SOURCE:3 , 0.60
		SOURCE:15 = 300
	ENDIF
ELSE
	IF EXP:0 == 0
		SOURCE:15 = 3000
	ENDIF
ENDIF

;PALAM:欲情をみる
IF PALAM:5 < PALAMLV:1
	TIMES SOURCE:1 , 0.60
	TIMES SOURCE:3 , 0.30
ELSEIF PALAM:5 < PALAMLV:2
	TIMES SOURCE:1 , 0.80
	TIMES SOURCE:3 , 0.60
ELSEIF PALAM:5 < PALAMLV:3
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:3 , 1.00
ELSEIF PALAM:5 < PALAMLV:4
	TIMES SOURCE:1 , 1.20
	TIMES SOURCE:3 , 1.50
ELSEIF PALAM:5 >= PALAMLV:4
	TIMES SOURCE:1 , 1.50
	TIMES SOURCE:3 , 1.80
ENDIF

;ABL:従順をみる
IF ABL:10 == 0
	TIMES SOURCE:1 , 0.50
	TIMES SOURCE:3 , 0.60
	TIMES SOURCE:15 , 2.00
ELSEIF ABL:10 == 1
	TIMES SOURCE:1 , 0.80
	TIMES SOURCE:3 , 0.80
	TIMES SOURCE:15 , 1.50
ELSEIF ABL:10 == 2
	TIMES SOURCE:1 , 1.00
	TIMES SOURCE:3 , 1.00
	TIMES SOURCE:15 , 1.00
ELSEIF ABL:10 == 3
	TIMES SOURCE:1 , 1.30
	TIMES SOURCE:3 , 1.20
	TIMES SOURCE:15 , 0.80
ELSEIF ABL:10 == 4
	TIMES SOURCE:1 , 1.60
	TIMES SOURCE:3 , 1.40
	TIMES SOURCE:15 , 0.60
ELSE
	TIMES SOURCE:1 , 2.00
	TIMES SOURCE:3 , 1.60
	TIMES SOURCE:15 , 0.30
ENDIF

;セックス後の処理
CALL COM_AFTER_VAGINA_SEX

RETURN 1
;
