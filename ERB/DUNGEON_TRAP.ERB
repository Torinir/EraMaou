;-------------------------------
@DUNGEON_TRAP
#DIM TRAP_COUNT
#DIM TRAP_NUM
#DIM TRAP_ID
#DIM TRAP_NOUSE
#DIM TRAP_MISS
;------------------------------
;ダンジョンで発動する罠です
;TRAP_COUNT = 罠試行回数カウンター
;TRAP_NUM   = 罠のFLAG上のID
;TRAP_ID    = 罠のアイテム番号
;TRAP_NOUSE = 罠が作動しなかった時に立てるフラグ

SIF D:4 <= 0
	D:4 = 1

FOR TRAP_COUNT, 0, D:4
	;迎撃中?
	SIF CFLAG:A:1 == 3
		CALL SLAVE_TRAP_SET
	
	;侵攻中の勇者?
	SIF CFLAG:A:1 != 2 && CFLAG:A:1 != 12
		RETURN 0
	
	;FLAG:TRAP_NUMは各階層の罠ABCに何の罠が設置されているかのフラグになる
	;FLAG:300～FLAG:308 = 階層設置罠A
	;FLAG:310～FLAG:318 = 階層設置罠B
	;FLAG:320～FLAG:328 = 階層設置罠C
	;CFLAG:A:501は現在の階層数

	;罠がAにあるか?
	TRAP_NUM = CFLAG:A:501 + 299
	TRAP_ID = FLAG:TRAP_NUM
	IF CFLAG:A:1 == 12
		CALL CAMPAIGN_TRAP,TRAP_NUM
		TRAP_ID = RESULT
	ENDIF
	
	;Aになければ罠がＢにあるか?
	IF TRAP_ID < 0
		TRAP_NUM = CFLAG:A:501 + 309
		TRAP_ID = FLAG:TRAP_NUM
		IF CFLAG:A:1 == 12
			CALL CAMPAIGN_TRAP,TRAP_NUM
			TRAP_ID = RESULT
		ENDIF
	ENDIF

	$TRAP_LOOP
	
	TRAP_ID = FLAG:TRAP_NUM
	IF CFLAG:A:1 == 12
		CALL CAMPAIGN_TRAP,TRAP_NUM
		TRAP_ID = RESULT
	ENDIF

	;罠がＣにあるか?
	IF TRAP_ID < 0
		TRAP_NUM = CFLAG:A:501 + 319
		TRAP_ID = FLAG:TRAP_NUM
		IF CFLAG:A:1 == 12
			CALL CAMPAIGN_TRAP,TRAP_NUM
			TRAP_ID = RESULT
		ENDIF
		SIF TRAP_ID < 0
			RETURN 0
	ENDIF
	
	;後にTRAP_NOUSEにRESULTを代入する際,想定外の結果にならないための保険
	;(在庫なしやTRAP_IDの値が異常な場合に起こり得る)
	RESULT = 0
	
	;同一罠発動による回避率上昇
	SIF CFLAG:A:513 == TRAP_ID
		CFLAG:A:512 += 1
	;罠回避率の減少
	SIF CFLAG:A:513 == 0
		CFLAG:A:512 -= 1
	
	;記憶リセット
	;罠の回避や失敗で連鎖が途切れるように
	CFLAG:A:513 = 0
	
	;回避率は負にはならない
	SIF CFLAG:A:512 < 0
		CFLAG:A:512 = 0
	
	;同一罠発動失敗判定
	TRAP_MISS = 20 -  CFLAG:A:512
	
	;在庫なしの場合は同一チェック外し
	IF ITEM:TRAP_ID < 1
		CFLAG:A:512 -= 1
	ELSEIF TRAP_MISS < RAND:20
		PRINT ≪同一罠発動制限≫
		PRINTL 勇者は罠を回避した......
		;罠回避率の減少
		CFLAG:A:512 -= 1
	ELSEIF TRAP_ID == 60
		CALL PIT_TRAP
	ELSEIF TRAP_ID == 61
		CALL ARROW_TRAP
	ELSEIF TRAP_ID == 62
		CALL TELEPORT_TRAP
	ELSEIF TRAP_ID == 63
		CALL ONE_WAY_TRAP
	ELSEIF TRAP_ID == 64
		CALL LOVE_GAS_TRAP
	ELSEIF TRAP_ID == 65
		CALL SYOKUSYU_FLOOR_TRAP
	ELSEIF TRAP_ID == 66
		CALL LOVE_BATH_TRAP
	ELSEIF TRAP_ID == 67
		CALL SELF_SAIMIN_TRAP
	ELSEIF TRAP_ID == 68
		CALL IMITATER_TRAP
	ELSEIF TRAP_ID == 69
		CALL SUMMON_TRAP
	ELSEIF TRAP_ID == 70
		CALL SUCCUBUS_TRAP
	ELSEIF TRAP_ID == 71
		CALL SLIME_ROOM_TRAP
	ELSEIF TRAP_ID == 72
		CALL NET_TRAP
	ELSEIF TRAP_ID == 73
		CALL SHOP_TRAP
	ELSEIF TRAP_ID == 74
		CALL BLACKOUT_TRAP
	ELSEIF TRAP_ID == 75
		CALL SHOOT_TRAP
	ELSEIF TRAP_ID == 76
		CALL DISPELL_TRAP
	ELSEIF TRAP_ID == 77
		CALL OIL_TRAP
	ELSEIF TRAP_ID == 78
		CALL FIRE_TRAP
	ELSEIF TRAP_ID == 79
		CALL A_WORM_TRAP
	ELSEIF TRAP_ID == 80
		CALL LOVE_BUG_TRAP
	ELSEIF TRAP_ID == 81
		CALL DARK_JUEL_TRAP
	ELSEIF TRAP_ID == 82
		CALL DEF_DOWN_TRAP
	ELSEIF TRAP_ID == 83
		CALL ATK_DOWN_TRAP
	ELSEIF TRAP_ID == 84
		CALL MAG_DOWN_TRAP
	ELSEIF TRAP_ID == 85
		CALL ALL_DOWN_TRAP
	ENDIF
	
	;罠が作動したかどうかをRETURNの値をTRAP_NOUSEに格納することで判断
	;これにより未作動時に下の罠消費と自動補充を素通りするように
	TRAP_NOUSE = RESULT
	
	SIF TRAP_ID >= 60 && TRAP_ID <= 89 && ITEM:TRAP_ID > 0 && TRAP_NOUSE == 0 && CFLAG:A:1 == 2
		ITEM:TRAP_ID -= 1
	
	;罠発動記憶
	SIF TRAP_NOUSE == 0
		CFLAG:A:513 = TRAP_ID
	
	;罠自動補充
	IF FLAG:5 & 64 && TRAP_NOUSE == 0 && CFLAG:A:1 == 2
		P = TRAP_ID
		CALL TRAP_PRICE
		IF MONEY >= RESULT && ITEM:TRAP_ID < 99 && TRAP_ID >=60 && TRAP_ID <= 89
			ITEM:TRAP_ID += 1
			MONEY -= RESULT
		ENDIF
	ENDIF
	
	TRAP_NUM += 10
	
	
	
	SIF TRAP_NUM < 330
		GOTO TRAP_LOOP
	
	;ABCのループが終わり,D:4の試行回数が残っているなら
	;またAから罠を回していく
	
NEXT

WAIT

RETURN 0

;------------------------------
@PIT_TRAP
#DIM DICE
;------------------------------
;落とし穴 

IF CFLAG:A:503 & 64
	SIF FLAG:5 & 32
		PRINTFORM 連続落下だ!(気力-10) 
	BASE:A:1 -= 10
ELSE
	SIF FLAG:5 & 32
		PRINT 落とし穴だ! 
	;落下フラグON
	CFLAG:A:503 += 64
ENDIF

DICE = RAND:100

IF TALENT:A:314 == 6
	;天使
	SIF FLAG:5 & 32
		PRINTFORM 天使の%SAVESTR:A%は滞空して落とし穴を回避した... 
	RETURN 1
ELSEIF TALENT:A:314 == 8
	;堕天使
	SIF FLAG:5 & 32
		PRINTFORM 堕天使の%SAVESTR:A%は滞空して落とし穴を回避した... 
	RETURN 1
ENDIF

IF DICE < 10
	SIF FLAG:5 & 32
		PRINTFORM %SAVESTR:A%は落とし穴を回避した... 
	RETURN 1
ELSEIF DICE >= 80
	DICE = RAND:40 + FLAG:85 * 10 + 1
	DICE *= 2
	IF TALENT:0:328
		;魔蟲知識によってダメージ1.5倍に
		DICE += DICE / 2
		SIF FLAG:5 & 32
			PRINTFORM 穴の底は毒蟲の群れだ! 
	ENDIF
	SIF FLAG:5 & 32
		PRINTFORM %SAVESTR:A%は落ちた時に足を挫いてしまった!{DICE}のダメ-ジ! 
ELSE
	DICE = RAND:40 + FLAG:85 * 10 + 1
	IF TALENT:0:328
		;魔蟲知識によってダメージ1.5倍に
		DICE += DICE / 2
		SIF FLAG:5 & 32
			PRINTFORM 穴の底は毒蟲の群れだ! 
	ENDIF
	SIF FLAG:5 & 32
		PRINTFORM %SAVESTR:A%に{DICE}のダメ-ジ!
ENDIF
BASE:A:0 -= DICE
IF TALENT:A:10 == 1
	SIF FLAG:5 & 32
		PRINTFORM 臆病な%SAVESTR:A%は死ぬほど驚いている...(気力-10) 
	BASE:A:1 -= 10
ENDIF

SIF FLAG:5 & 32
	PRINTL  

RETURN 0

;------------------------------
@ARROW_TRAP
#DIM DICE
;------------------------------
;矢の罠

SIF FLAG:5 & 32
	PRINT 矢の罠だ! 

DICE = RAND:100

IF DICE < 30
	SIF FLAG:5 & 32
		PRINTFORM %SAVESTR:A%はとっさに矢を回避した... 
	RETURN 1
ELSEIF DICE >= 80
	DICE *= 2
	SIF FLAG:5 & 32
		PRINTFORM %SAVESTR:A%の急所に当たり{DICE}のダメ-ジ! 
	DICE += FLAG:85 * 10
	SIF FLAG:5 & 32 && FLAG:85 > 0
		PRINTFORM 矢は更に深く刺さり%SAVESTR:A%に{FLAG:85 * 10}の追加ダメ-ジ! 
	BASE:A:0 -= DICE
	IF TALENT:A:40 == 1
		SIF FLAG:5 & 32
			PRINTFORM 痛みに弱い%SAVESTR:A%は痛みに耐えかねている...(気力-30) 
		BASE:A:1 -= 30
	ENDIF
ELSE
	SIF FLAG:5 & 32
		PRINTFORM %SAVESTR:A%に{DICE}のダメ-ジ!
	DICE += FLAG:85 * 10
	SIF FLAG:5 & 32 && FLAG:85 > 0
		PRINTFORM 矢は更に深く刺さり%SAVESTR:A%に{FLAG:85 * 10}の追加ダメ-ジ! 
	BASE:A:0 -= DICE
	IF TALENT:A:40 == 1
		SIF FLAG:5 & 32
			PRINTFORM 痛みに弱い%SAVESTR:A%は痛みに耐えかねている...(気力-30) 
		BASE:A:1 -= 30
	ENDIF
ENDIF

SIF FLAG:5 & 32
	PRINTL  

RETURN 0

;---------------------
@TELEPORT_TRAP
;---------------------
;テレポートの罠


SIF FLAG:5 & 32
	PRINT おおっとテレポ-ト! 

Z = RAND:100

IF Z > 70
	SIF FLAG:5 & 32
		PRINTFORM %SAVESTR:A%はとっさにテレポ-トを回避した... 
	RETURN 1
ELSEIF Z < 20
	SIF FLAG:5 & 32
		PRINTFORM %SAVESTR:A%は階層入り口まで吹っ飛ばされた! 
	D:20 = 1
	D:1 = 1
ELSE
	SIF FLAG:5 & 32
		PRINTFORM %SAVESTR:A%は吹っ飛ばされた! 
	D:20 = RAND:100
	D:1 = 1
ENDIF

IF TALENT:A:10 == 1
	SIF FLAG:5 & 32
		PRINTFORM 臆病な%SAVESTR:A%は死ぬほど驚いている...(気力-10) 
	BASE:A:1 -= 10
ENDIF

IF FLAG:85 > 0
	SIF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%はテレポ-ト酔いで朦朧としている...(気力-{FLAG:85 * 5})
	BASE:A:1 -= FLAG:85 * 5
ENDIF

SIF FLAG:5 & 32
	PRINTL  

RETURN 0

;---------------------
@ONE_WAY_TRAP
;---------------------
;一方通行の罠

IF D:20 < 40
	;個数増加による帳尻合わせではなくRETURNの戻り値を変えることで,そもそもの個数を減少させないように
	;ITEM:63 += 1
	RETURN 1
ENDIF

;落下フラグ
IF CFLAG:A:503 & 64
	SIF FLAG:5 & 32
		PRINTFORM 落下先は新たな通路だ......這いあがることは不可能だ(気力-20) 
	BASE:A:1 -= 20
ELSE
	SIF FLAG:5 & 32
		PRINT ドアをくぐると後ろから鍵がかかる音が聞こえた! 
ENDIF


Z = RAND:3

IF Z > 1
	SIF FLAG:5 & 32
		PRINTFORM %SAVESTR:A%はすぐさま帰り道を見つけた... 
	RETURN 1
ELSE
	SIF FLAG:5 & 32
		PRINTFORM %SAVESTR:A%は迷ってしまった... 
	CFLAG:A:509 = 1
	D:1 = 1
	SIF FLAG:5 & 32
		PRINTFORM %SAVESTR:A%は焦っている (気力-{20 + FLAG:85 * 5}) 
	BASE:A:1 -= (20 + FLAG:85 * 5)
ENDIF

IF TALENT:A:10 == 1
	SIF FLAG:5 & 32
		PRINTFORM 臆病な%SAVESTR:A%は死ぬほど驚いている...(気力-10) 
	BASE:A:1 -= 10
ENDIF

SIF FLAG:5 & 32
	PRINTL  

RETURN 0

;---------------------
@LOVE_GAS_TRAP
;---------------------
;催淫ガスの罠


SIF FLAG:5 & 32
	PRINT 突然壁の隙間から甘いガスが噴出してきた...... 

Z = RAND:100

IF Z > 60
	SIF FLAG:5 & 32
		PRINTFORM %SAVESTR:A%は鼻と口を覆い脱出した... 
	RETURN 1
ELSEIF Z < 10
	IF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%は大量のガスを吸い込み淫らな気分になってしまった...
		PRINTFORML 欲情の珠+{40 + FLAG:85}
	ENDIF
	JUEL:A:5 += (40 + FLAG:85)
	SIF FLAG:5 & 32
		PRINTFORM %SAVESTR:A%は息が荒くなってきた(気力-{40 + FLAG:85 * 2})
	BASE:A:1 -= (40 + FLAG:85 * 2)
ELSE
	IF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%は淫らな気分になってしまった...
		PRINTFORML 欲情の珠+{20 + FLAG:85}
	ENDIF
	JUEL:A:5 += (20 + FLAG:85)
	SIF FLAG:5 & 32
		PRINTFORM %SAVESTR:A%は息が荒くなってきた(気力-{20 + FLAG:85 * 2})
	BASE:A:1 -= (20 + FLAG:85 * 2)
ENDIF

IF TALENT:A:60 == 1
	IF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%は思わず自慰をしてしまった
		PRINTL 自慰経験+1
		PRINTL 欲情の珠+20
		PRINTL 快Cの珠+10
	ENDIF
	EXP:A:10 += 1
	JUEL:A:0 += 20
	BASE:A:1 -= 10
	JUEL:A:5 += 20
ENDIF

SIF FLAG:5 & 32
	PRINTL  

;欲情フラグ
SETBIT CFLAG:A:503, 9

RETURN 0

;----------------------------
@SYOKUSYU_FLOOR_TRAP
;----------------------------
;触手床の罠


;落下フラグ
IF CFLAG:A:503 & 64
	SIF FLAG:5 & 32
		PRINTFORM 落下先は触手の巣だ!!(気力-20) 
	BASE:A:1 -= 20
ELSE
	SIF FLAG:5 & 32
		PRINT 突然床から触手が襲いかかってきた!! 
ENDIF


Z = RAND:100

IF Z > 70
	SIF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%は武器で触手を薙ぎ払い脱出した...
	RETURN 1
ELSEIF Z < 15
	IF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%は触手に捕まり,口の中にまで催淫効果のある体液を塗りたくられた...
		PRINTL 触手経験+1
		PRINTFORML 欲情の珠+{100 + FLAG:85 * 2}
	ENDIF
	EXP:A:55 += 1
	JUEL:A:5 += 120 + FLAG:85 * 2
	SIF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%は必死にもがき,かなり気力を消耗してしまった(気力-200)
	BASE:A:1 -= 200
ELSE
	IF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%は触手に捕まり催淫効果のある体液を塗りたくられた...
		PRINTL 触手経験+1
		PRINTFORML 欲情の珠+{60 + FLAG:85 * 2}
	ENDIF
	EXP:A:55 += 1
	JUEL:A:5 += 60 + FLAG:85 * 2
	SIF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%は必死にもがき,かなり気力を消耗してしまった(気力-100)
	BASE:A:1 -= 100
ENDIF

IF TALENT:A:60 == 1
	IF FLAG:5 & 32
		PRINTFORML 媚薬体液にあてられた%SAVESTR:A%は思わず自慰をしてしまった
		PRINTL 自慰経験+1
		PRINTL 欲情の珠+10
		PRINTL 快Cの珠+10
	ENDIF
	EXP:A:10 += 1
	JUEL:A:0 += 20
	BASE:A:1 -= 10
	JUEL:A:5 += 20
ENDIF

RETURN 0


;----------------------------
@LOVE_BATH_TRAP
;----------------------------
;媚薬沼の罠

;落下フラグ
IF CFLAG:A:503 & 64
	SIF FLAG:5 & 32
		PRINTFORM 落下先は媚薬で満たされたプ-ルだ!!(気力-20) 
	BASE:A:1 -= 20
ELSE
	SIF FLAG:5 & 32
		PRINT 突然床が陥没し媚薬で満たされたプ-ルに落とされた! 
ENDIF


IF RAND:10 < 2
	IF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%は媚薬プ-ルで溺れてしまった!
		PRINTL 薬物経験+1
		PRINTFORML 欲情の珠+{200 + FLAG:85 * 5}
	ENDIF
	EXP:A:57 += 1
	JUEL:A:5 += 200 + FLAG:85 * 5
	SIF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%は必死にもがき,かなり体力と気力を消耗してしまった(体力-200 気力-200)
	BASE:A:0 -= 200
	BASE:A:1 -= 200
ELSE
	IF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%は大量に媚薬を浴びてふらふらしている
		PRINTL 薬物経験+1
		PRINTFORML 欲情の珠+{100 + FLAG:85 * 5}
	ENDIF
	EXP:A:57 += 1
	JUEL:A:5 += 100 + FLAG:85 * 5
	SIF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%は必死にもがき,かなり気力を消耗してしまった(気力-100)
	BASE:A:1 -= 100
ENDIF

IF TALENT:A:60 == 1
	IF FLAG:5 & 32
		PRINTFORML 媚薬で変な気分になった%SAVESTR:A%は思わず自慰をしてしまった
		PRINTL 自慰経験+1
		PRINTL 欲情の珠+10
		PRINTL 快Cの珠+10
	ENDIF
	EXP:A:10 += 1
	JUEL:A:0 += 20
	BASE:A:1 -= 10
	JUEL:A:5 += 20
ENDIF

;欲情フラグ
SETBIT CFLAG:A:503, 9

RETURN 0


;----------------------------
@SELF_SAIMIN_TRAP
#DIM DICE
;----------------------------
;自慰催眠の罠

SIF FLAG:5 & 32
	PRINT とつぜん目眩がして身体の自由が利かなくなる... 

DICE = RAND:100
TARGET = A

;欲情フラグ
SIF GETBIT(CFLAG:A:503, 9)
	TIMES DICE, 0.80

IF DICE > 60
	SIF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%は頬をつねりまやかしを断ちきった
	RETURN 1
ELSEIF DICE < 10
	IF FLAG:5 & 32
		SIF GETBIT(CFLAG:A:503, 9)
			PRINT 盛りのついた
		PRINTFORML %SAVESTR:A%は催眠状態で装備を脱ぎ捨て一心不乱に自慰を始めた	
	ENDIF
	CALL BEFORE_AUTOTRAIN
	CALL COM3_AUTO
	CALL SOURCE_CHECK_AUTO
	SIF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%はモンスタ-が駆け寄る足音すら聞こえないようだ(攻撃力と防御力が０に!)
	CFLAG:A:11 = 0
	CFLAG:A:12 = 0
ELSE
	IF FLAG:5 & 32
		SIF GETBIT(CFLAG:A:503, 9)
			PRINT 盛りのついた
		PRINTFORML %SAVESTR:A%は催眠状態で装備をはだけさせて一心不乱に自慰を始めた	
	ENDIF
	CALL BEFORE_AUTOTRAIN
	CALL COM3_AUTO
	CALL SOURCE_CHECK_AUTO
	SIF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%はモンスタ-が駆け寄る足音すら聞こえないようだ(攻撃力と防御力が半分に!)
	CFLAG:A:11 /= 2
	CFLAG:A:12 /= 2
ENDIF

RETURN 0

;------------------------------
@IMITATER_TRAP
;------------------------------
;擬態部屋の罠

Z = RAND:100

;落下フラグ
IF CFLAG:A:503 & 64
	SIF FLAG:5 & 32
		PRINTFORM 落下先は生き物の体内だ!!
	Z -= 10
ELSE
	IF FLAG:5 & 32
		PRINTL 突然部屋の壁が四方から襲いかかってきた!
		PRINTL どうやら擬態した生き物の体内に入り込んでしまっていたらしい
	ENDIF
ENDIF

IF Z > 60
	SIF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%は急いで脱出した
	RETURN 1
ELSEIF Z < 10
	IF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%は丸飲み状態で四肢を固定され
		PRINTFORML 触手に器用に装備を剥かれ体中に媚薬を塗りたくられてしまった
		PRINTFORML 激しい興奮状態の%SAVESTR:A%には駆け寄るモンスタ-の足音さえ聞こえなかった
		PRINTFORML 欲情の珠+{50 + FLAG:85 * 10}
		PRINTFORML 屈服の珠+{50 + FLAG:85 * 10}
		PRINTFORML 恥情の珠+{50 + FLAG:85 * 10}
		PRINTFORML 快Bの珠+{50 + FLAG:85 * 10}
		PRINTFORML 快Cの珠+{50 + FLAG:85 * 10}
	ENDIF
	JUEL:A:6 += 50 + FLAG:85 * 10
	JUEL:A:8 += 50 + FLAG:85 * 10
	JUEL:A:14 += 50 + FLAG:85 * 10
	JUEL:A:0 += 50 + FLAG:85 * 10
	BASE:A:1 -= 50 + FLAG:85 * 10
	JUEL:A:5 += 50 + FLAG:85 * 10
	IF FLAG:5 & 32
		PRINTFORML 全裸の%SAVESTR:A%はこのあと起こる戦闘さえ忘れて絶頂した(攻撃力と防御力が０に!)
		PRINTL 絶頂経験+1
	ENDIF
	EXP:A:2 += 1
	CFLAG:A:11 = 0
	CFLAG:A:12 = 0
ELSE
	IF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%は丸飲み状態で四肢を固定され
		PRINTFORML 触手が器用に装備の間に潜り込み体中に媚薬を塗りたくられてしまった
		PRINTFORML 激しい興奮状態の%SAVESTR:A%には駆け寄るモンスタ-の足音さえ聞こえなかった
		PRINTFORML 欲情の珠+{30 + FLAG:85 * 6}
		PRINTFORML 屈服の珠+{30 + FLAG:85 * 6}
		PRINTFORML 恥情の珠+{30 + FLAG:85 * 6}
		PRINTFORML 快Bの珠+{30 + FLAG:85 * 6}
		PRINTFORML 快Cの珠+{30 + FLAG:85 * 6}
	ENDIF
	JUEL:A:6 += 30 + FLAG:85 * 6
	JUEL:A:8 += 30 + FLAG:85 * 6
	JUEL:A:14 += 30 + FLAG:85 * 6
	JUEL:A:0 += 30 + FLAG:85 * 6
	BASE:A:1 -= 30 + FLAG:85 * 6
	JUEL:A:5 += 30 + FLAG:85 * 6
	IF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%はこのあと起こる戦闘さえ忘れて絶頂した(攻撃力と防御力が半分に!)
		PRINTL 絶頂経験+1
	ENDIF
	EXP:A:2 += 1
	CFLAG:A:11 /= 2
	CFLAG:A:12 /= 2
ENDIF


RETURN 0

;------------------------------
@SUMMON_TRAP
;------------------------------
;召喚の罠

SIF FLAG:5 & 32
	PRINTFORM %SAVESTR:A%は召喚の魔法陣を踏んでしまった 

Z = RAND:2

IF Z > 0
	SIF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%はとっさに魔法陣を破壊してやり過ごした
	RETURN 1
ELSE
	SIF FLAG:5 & 32
		PRINTFORML ダンジョンにモンスタ-が召喚された!
ENDIF

CALL SUMMON_MONSTER, -1

IF FLAG:85 > 0
	SIF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%は魔法陣の魔力を浴びてしまった(体力-{FLAG:85 * 10})
	BASE:A:0 -= FLAG:85 * 10
ENDIF

RETURN 0

;--------------------------------------------
@SUCCUBUS_TRAP
#DIM DICE
;--------------------------------------------
;夢魔の罠

IF FLAG:5 & 32
	PRINTL 陵辱された後の勇者らしき少女がいる...
	PRINTL 助けなければ...
ENDIF

DICE = RAND:100

;欲情フラグ
SIF GETBIT(CFLAG:A:503, 9)
	TIMES DICE, 0.80

IF DICE > 60
	SIF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%は罠だと思い無視した
	RETURN 1
ELSEIF DICE < 10
	IF FLAG:5 & 32
		SIF GETBIT(CFLAG:A:503, 9)
			PRINT ムラムラとした情欲に突き動かされ
		PRINTFORML %SAVESTR:A%は助け出そうとした少女に唇を奪われた
		PRINTFORML 激しく抱き合い,%SAVESTR:A%はいつしか理性が飛んで欲望だけがどんどん湧いてくるのを感じた
		PRINTFORML 股間に手が伸びる...%SAVESTR:A%は何も疑問に感じないほど欲望に支配されてしまっていた
		SIF TALENT:A:オトコ == 0
			PRINTL レズ経験+6
		PRINTFORML 欲情の珠+{100 + FLAG:85 * 10}
		PRINTFORML 屈服の珠+{100 + FLAG:85 * 10}
		PRINTFORML 恥情の珠+{100 + FLAG:85 * 10}
		PRINTFORML 快Bの珠+{100 + FLAG:85 * 10}
		PRINTFORML 快Cの珠+{100 + FLAG:85 * 10}
	ENDIF
	SIF TALENT:A:オトコ == 0
		EXP:A:40 += 6
	JUEL:A:6 += 100 + FLAG:85 * 10
	JUEL:A:8 += 100 + FLAG:85 * 10
	JUEL:A:14 += 100 + FLAG:85 * 10
	JUEL:A:0 += 100 + FLAG:85 * 10
	BASE:A:1 -= 100 + FLAG:85 * 10
	JUEL:A:5 += 100 + FLAG:85 * 10
	IF FLAG:5 & 32
		PRINTFORML 全裸の%SAVESTR:A%はこのあと起こる戦闘さえ忘れて絶頂した(攻撃力と防御力が０に!)
		PRINTL 絶頂経験+1
	ENDIF
	EXP:A:2 += 1
	CFLAG:A:11 = 0
	CFLAG:A:12 = 0
ELSE
	IF FLAG:5 & 32
		SIF GETBIT(CFLAG:A:503, 9)
			PRINT ムラムラとした情欲に突き動かされ
		PRINTFORML %SAVESTR:A%は喜んで駆け寄ってきた少女に唇を奪われた
		PRINTFORML 激しく抱き合い,%SAVESTR:A%はいつしか欲望だけがどんどん湧いてくるのを感じた
		PRINTFORML 股間に手が伸びる...%SAVESTR:A%の理性の一部は警報を発するがそのうち欲望に支配されてしまっていた
		SIF TALENT:A:オトコ == 0
			PRINTL レズ経験+4
		PRINTFORML 欲情の珠+{60 + FLAG:85 * 6}
		PRINTFORML 屈服の珠+{60 + FLAG:85 * 6}
		PRINTFORML 恥情の珠+{60 + FLAG:85 * 6}
		PRINTFORML 快Bの珠+{60 + FLAG:85 * 6}
		PRINTFORML 快Cの珠+{60 + FLAG:85 * 6}
	ENDIF
	SIF TALENT:A:オトコ == 0
		EXP:A:40 += 4
	JUEL:A:6 += 60 + FLAG:85 * 6
	JUEL:A:8 += 60 + FLAG:85 * 6
	JUEL:A:14 += 60 + FLAG:85 * 6
	JUEL:A:0 += 60 + FLAG:85 * 6
	BASE:A:1 -= 60 + FLAG:85 * 6
	JUEL:A:5 += 60 + FLAG:85 * 6
	IF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%はこのあと起こる戦闘さえ忘れて絶頂した(攻撃力と防御力が半分に!)
		PRINTL 絶頂経験+1
	ENDIF
	EXP:A:2 += 1
	CFLAG:A:11 /= 2
	CFLAG:A:12 /= 2
ENDIF

RETURN 0

;----------------------------
@SLIME_ROOM_TRAP
#DIM DICE
;----------------------------
;スライム部屋の罠

DICE = RAND:100
TARGET = A


;落下フラグ
IF CFLAG:A:503 & 64
	SIF FLAG:5 & 32
		PRINTFORM 落下先はスライムの巣だ!! 
	DICE -= 20
ELSE
	SIF FLAG:5 & 32
	PRINT 突然四方の壁と天井からスライムが染み出してきた! 
ENDIF

IF DICE > 80
	SIF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%は命からがら脱出した...
	RETURN 1
ELSEIF DICE < 10
	SIF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%は全身をスライムになぶられて装備を溶かされ全裸になってしまった(攻撃力と防御力が1/2に!)
	BASE:A:1 -= 25 + FLAG:85 * 5
	CFLAG:A:11 /= 2
	CFLAG:A:12 /= 2

	IF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%の口と肛門に大量の粘液が流れ込む...(気力-{10 + FLAG:85 * 5})
		PRINTL A経験+1
	ENDIF
	EXP:A:1 += 1
	BASE:A:1 -= 10 + FLAG:85 * 5
ELSE
	SIF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%は全身をスライムになぶられて装備を半分溶かされてしまった(攻撃力と防御力が2/3に!)
	BASE:A:1 -= 25 + FLAG:85 * 5
	CFLAG:A:11 -= CFLAG:A:11 / 3
	CFLAG:A:12 -= CFLAG:A:12 / 3

	IF RAND:3 == 0
		IF FLAG:5 & 32
			PRINTFORML %SAVESTR:A%の口と肛門に大量の粘液が流れ込む...(気力-{10 + FLAG:85 * 5})
			PRINTL A経験+1
		ENDIF
		EXP:A:1 += 1
		BASE:A:1 -= 10 + FLAG:85 * 5
	ENDIF
ENDIF

;ローション自動調教
CALL BEFORE_AUTOTRAIN
CALL COM50_AUTO
CALL SOURCE_CHECK_AUTO
;ヌルヌル付与
SETBIT CFLAG:A:503, 3

RETURN 0

;------------------------------
@NET_TRAP
;------------------------------
;蜘蛛の巣

LOCAL = 10 + FLAG:85 * 10

;気力最大値によるキャップ
SIF LOCAL > MAXBASE:A:1 / 10
	LOCAL = MAXBASE:A:1 / 10

SIF FLAG:5 & 32
	PRINTFORM %SAVESTR:A%は蜘蛛の巣を払うのに気力を使ってしまった(気力-{LOCAL}) 
BASE:A:1 -= LOCAL

IF TALENT:0:328
	;魔蟲知識によって気力ダメージの1.5倍のHPダメージ
	LOCAL += LOCAL / 2
	;HP最大値によるキャップ
	SIF LOCAL > MAXBASE:A:0 / 10
		LOCAL = MAXBASE:A:0 / 10
	SIF FLAG:5 & 32
		PRINTFORM 毒蜘蛛が次々と襲いかかる!(HP-{LOCAL})  
	BASE:A:0 -= LOCAL
ENDIF

SIF FLAG:5 & 32
	PRINTL  

RETURN 0

;--------------------------------
@SHOP_TRAP
#DIM COST
;--------------------------------
;行商人
;COST = 代金

SIF FLAG:5 & 32
	PRINTFORML %SAVESTR:A%に割高な回復アイテムを売り付けた

COST = RAND:5 * 50

IF COST == 0
	SIF FLAG:5 & 32
		PRINTL 勇者は何も買わなかったようだ
	RETURN 1
ENDIF

COST += FLAG:85 * 10
COST = COST * (99 + POWER(CFLAG:A:501, 3)) / 100

;IF CFLAG:A:580 < COST
;	IF FLAG:5 & 32
;		printformw %SAVESTR:A%は手持ちの金がないようだ...
;	ENDIF
;	RETURN 0
;ENDIF

SIF FLAG:5 & 32
	PRINTFORML 全部で{COST}ポイントの売り上げになった!

MONEY += COST
;CFLAG:A:580 -= COST
CFLAG:A:582 -= COST


BASE:A:0 += COST
SIF BASE:A:0 > MAXBASE:A:0
	BASE:A:0 = MAXBASE:A:0
;気力の回復
BASE:A:1 = MAXBASE:A:1
SIF FLAG:5 & 32
	PRINTFORML %SAVESTR:A%は回復した

RETURN 0


;--------------------------------
@BLACKOUT_TRAP
#DIM DICE
;--------------------------------
;暗闇の罠

SIF FLAG:5 & 32
	PRINTFORML %SAVESTR:A%は突然視界を奪われた

DICE = RAND:3

IF DICE == 2
	SIF FLAG:5 & 32
		PRINTL 勇者は暗闇から脱出した
	RETURN 1
ELSEIF DICE == 1
	SIF FLAG:5 & 32
		PRINTFORML 暗闇の中は何も見えない!(攻撃力が半分になった!)
	CFLAG:A:11 /= 2
	SIF FLAG:5 & 32
		PRINTFORML 暗闇の向こうから何か恐ろしい声が聞こえる!(気力-100)
	BASE:A:1 -= 100
	IF FLAG:85 > 0
		SIF FLAG:5 & 32
			PRINTFORML %SAVESTR:A%は闇の中から毒矢を受けてしまった!(体力-{FLAG:85 * 10})
		BASE:A:0 -= FLAG:85 * 10
	ENDIF
ELSE
	SIF FLAG:5 & 32
		PRINTFORML 暗闇の中は何も見えない!(攻撃力が半分になった!)
	CFLAG:A:11 /= 2

	IF FLAG:85 > 0
		SIF FLAG:5 & 32
			PRINTFORML %SAVESTR:A%は闇の中から毒矢を受けてしまった!(体力-{FLAG:85 * 10})
		BASE:A:0 -= FLAG:85 * 10
	ENDIF
ENDIF

RETURN 0


;--------------------------------
@SHOOT_TRAP
;--------------------------------
;シュート

IF D:20 < 40
	;個数増加による帳尻合わせではなくRETURNの戻り値を変えることで,そもそもの個数を減少させないように
	;ITEM:75 += 1
	RETURN 1
ENDIF

;落下フラグ
IF CFLAG:A:503 & 64
	SIF FLAG:5 & 32
		PRINTFORML 落下先はさらなる落下の連鎖だ!!(気力-20)
	BASE:A:1 -= 20
ELSE
	SIF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%は突然落下した!
	;落下フラグON
	CFLAG:A:503 += 64
ENDIF


Z = RAND:3

IF Z > 0
	SIF FLAG:5 & 32
		PRINTL 勇者は手を伸ばし落下から逃れた
	RETURN 1
ENDIF

SIF FLAG:5 & 32
	PRINTFORML *シュ-ト!*
IF CFLAG:A:501 == 9
	Z = RAND:300
	BASE:A:0 -= Z
	IF FLAG:5 & 32
		PRINTFORML 最下層のはずなのにさらに下まで落ちていく!奇妙な浮遊感を覚えると転送陣によって第9階層の天井から%SAVESTR:A%は床に叩き付けられた(体力-{z})
		PRINTFORML %SAVESTR:A%は迷ってしまった...
	ENDIF
	CFLAG:A:509 = 1
	D:1 = 1
ELSEIF CFLAG:A:501 == 8
	CFLAG:A:501 += 1
	Z = RAND:300
	BASE:A:0 -= Z
	SIF FLAG:5 & 32
		PRINTFORML ここが最下層のようだ.%SAVESTR:A%はお尻をしこたま打ちつけた(体力-{z})
		PRINTFORML %SAVESTR:A%は迷ってしまった...
	CFLAG:A:509 = 1
	;パーティが分断される
	CALL PARTY_DEL, A
	D:1 = 1
ELSE
	CFLAG:A:501 += 1
	SIF FLAG:5 & 32
		PRINTFORML 下の階に落ちてしまい,%SAVESTR:A%は迷ってしまった...
	CFLAG:A:509 = 1
	;パーティが分断される
	CALL PARTY_DEL, A
	D:1 = 1
ENDIF

IF FLAG:85 > 0
	SIF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%は落下の衝撃に呻いている...(体力-{FLAG:85 * 10})
	BASE:A:0 -= FLAG:85 * 10
ENDIF

RETURN 0


;--------------------------------
@DISPELL_TRAP
;--------------------------------
;魔力拡散の罠

SIF FLAG:5 & 32
	PRINTFORML %SAVESTR:A%は呪いの魔法陣に触れてしまった!

Z = RAND:2

IF Z > 0
	SIF FLAG:5 & 32
		PRINTL 勇者は呪いに抵抗した
	RETURN 1
ENDIF

SIF FLAG:5 & 32
	PRINTFORML 魔力の渦が炸裂する!

IF CFLAG:A:503 & 2
	SIF FLAG:5 & 32
		PRINTFORML すでに呪いを受けているようだ
ELSE
	SIF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%は呪われてしまった...
	CFLAG:A:503 += 2
ENDIF

IF FLAG:85 > 0
	SIF FLAG:5 & 32
		PRINTFORML 魔力の渦が%SAVESTR:A%の精神を蝕んだ(気力-{FLAG:85 * 10})
	BASE:A:1 -= FLAG:85 * 10
ENDIF

RETURN 0

;------------------------------
@OIL_TRAP
;------------------------------
;油壺の罠

SIF FLAG:5 & 32
	PRINTL 油壺の罠だ!

Z = RAND:100

IF Z < 30
	SIF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%はとっさに回避した...
	RETURN 1
ELSE
	Z += FLAG:85 * 10
	SIF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%は油をもろに被り気力に{Z}のダメ-ジ!
	BASE:A:1 -= Z
	IF CFLAG:A:503 & 8
		SIF FLAG:5 & 32
			PRINTFORML ヌルヌルだ...
	ELSE
		CFLAG:A:503 += 8
	ENDIF
ENDIF

RETURN 0

;------------------------------
@FIRE_TRAP
#DIM DICE
;------------------------------
;火炎放射の罠

SIF FLAG:5 & 32
	PRINTL 火炎放射の罠だ!

DICE = RAND:200

IF DICE < 100
	SIF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%はとっさに炎を回避した...
	RETURN 1
ELSE
	DICE += FLAG:85 * 10
	SIF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%に{DICE}のダメ-ジ!
	BASE:A:0 -= DICE
	IF CFLAG:A:503 & 8
		SIF FLAG:5 & 32
			PRINTFORML ヌルヌルに火が引火し追加ダメ-ジ!({30 + FLAG:85 * 5})
		BASE:A:0 -= 30 + FLAG:85 * 5
	ENDIF
ENDIF



RETURN 0

;------------------------------
@A_WORM_TRAP
#DIM DICE
;------------------------------
;アナルワームの罠

SIF FLAG:5 & 32
	PRINTL アナルワ-ムの罠だ!

DICE  = RAND:100

;ヌルヌル状態で威力アップ
SIF GETBIT(CFLAG:A:503, 3)
	TIMES DICE, 1.30

IF DICE < 35
	SIF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%はアナルワ-ムに気付き,一撃で殺した
	RETURN 1
ENDIF

SIF GETBIT(CFLAG:A:503, 3) && FLAG:5 & 32
	PRINTFORM ぬるりと滑り込む!

DICE += FLAG:85 * 7 + 30
SIF FLAG:5 & 32
	PRINTFORML %SAVESTR:A%のアナルにワ-ムが食らいつき気力に{DICE}のダメ-ジ!
BASE:A:1 -= DICE

;A経験が多いと,中に入られてしまう
IF TALENT:A:Aワーム == 0 && EXP:A:1 > 30
	SETCOLORBYNAME LightSalmon
	SIF FLAG:5 & 32
		PRINTFORML %SAVESTR:A%の腸内でアナルワ-ムが蠢いている......
	RESETCOLOR
	TALENT:A:193 = 1
ELSEIF TALENT:A:Aワーム
	PLAYER = 0
	TARGET = A
	;アナルワーム自動調教
	CALL BEFORE_AUTOTRAIN
	CALL COM13_AUTO
	CALL SOURCE_CHECK_AUTO
ENDIF

EXP:A:1 += 1
SIF FLAG:5 & 32
	PRINTFORML A経験+1

RETURN 0

;------------------------------
@LOVE_BUG_TRAP
#DIM DICE
;------------------------------
;淫虫の罠

IF CFLAG:A:503 & 64
	SIF FLAG:5 & 32
		PRINTFORM 落下先は淫虫の巣だ!(気力-10) 
	BASE:A:1 -= 10
ELSE
	SIF FLAG:5 & 32
		PRINT 淫虫の巣に足を踏み入れてしまった! 
ENDIF

DICE = RAND:100

IF TALENT:A:314 == 6
	;天使
	SIF FLAG:5 & 32
		PRINTFORM 天使の%SAVESTR:A%は滞空して地面の巣を回避した... 
	RETURN 1
ELSEIF TALENT:A:314 == 8
	;堕天使
	SIF FLAG:5 & 32
		PRINTFORM 堕天使の%SAVESTR:A%は滞空して地面の巣を回避した... 
	RETURN 1
ENDIF

IF DICE < 5
	SIF FLAG:5 & 32
		PRINTFORM %SAVESTR:A%は走って逃げた 
	RETURN 1
ELSEIF DICE >= 80
	DICE = RAND:40 + FLAG:85 * 3 + 1
	DICE *= 2
	SIF FLAG:5 & 32
		PRINTFORM %SAVESTR:A%は落ちた時に足を挫いてしまった!{DICE}のダメ-ジ! 
ELSE
	DICE = RAND:40 + FLAG:85 * 3 + 1
	SIF FLAG:5 & 32
		PRINTFORM %SAVESTR:A%に{DICE}のダメ-ジ!
ENDIF
BASE:A:0 -= DICE

SIF FLAG:5 & 32
	PRINTL  

PLAYER = 0
TARGET = A
;愛撫自動調教
CALL BEFORE_AUTOTRAIN
CALL COM0_AUTO
CALL SOURCE_CHECK_AUTO

IF TALENT:A:10 == 1
	SIF FLAG:5 & 32
		PRINTFORML 臆病な%SAVESTR:A%は死ぬほど驚いている...(気力-10) 
	BASE:A:1 -= 10
ENDIF

RETURN 0

;------------------------------
@DARK_JUEL_TRAP
#DIM DICE
;------------------------------
;宝石の罠

SIF FLAG:5 & 32
	PRINTFORML %SAVESTR:A%に高価な宝石を拾わせた......

DICE = RAND:5 * 50

;カルマが高いと誘惑に打ち勝つ判定
SIF CFLAG:A:151 > 150 && RAND:4 == 0
	DICE = 0

IF DICE == 0
	SIF FLAG:5 & 32
		PRINTL 勇者は誘惑に打ち勝ったようだ
	RETURN 1
ENDIF

DICE += FLAG:85 * 10
DICE = DICE * (99 + POWER(CFLAG:A:501, 3)) / 100

;好奇心ボーナス
SIF TALENT:A:好奇心
	DICE += 5
;金のためボーナス
SIF TALENT:A:勇者になったきっかけ == 2
	DICE += 10
;ホビットボーナス
SIF TALENT:A:種族 == 10
	DICE += 15
;ドワーフボーナス
SIF TALENT:A:種族 == 11
	DICE += 15
;盗賊は収入が多い(1.2倍)
SIF TALENT:A:盗賊
	DICE += DICE / 5

SIF FLAG:5 & 32
	PRINTFORML 全部で{DICE}の価値がある宝石が勇者の懐に入った......

CFLAG:A:581 += DICE

JUEL:A:6 += 5
SIF FLAG:5 & 32
	PRINTFORML 屈服の珠+5

;そしてカルマが下がる
CALL KARMA, A, -1

RETURN 0

;------------------------------
@DEF_DOWN_TRAP
#DIM DICE
;------------------------------
;攻撃陣地の罠

SIF FLAG:5 & 32
	PRINTFORML %SAVESTR:A%に効果的な攻撃を行う陣を敷いた......

DICE = RAND:(FLAG:85 + 1) + RAND:20 + 1

SIF FLAG:5 & 32
	PRINTFORML %SAVESTR:A%の防御値が{DICE}％弱化した......

CFLAG:A:680 += DICE

RETURN 0

;------------------------------
@ATK_DOWN_TRAP
#DIM DICE
;------------------------------
;防御陣地の罠

SIF FLAG:5 & 32
	PRINTFORML %SAVESTR:A%に効果的な防御を行う陣を敷いた......

DICE = RAND:(FLAG:85 + 1) + RAND:20 + 1

SIF FLAG:5 & 32
	PRINTFORML %SAVESTR:A%の攻撃値が{DICE}％弱化した......

CFLAG:A:681 += DICE

RETURN 0

;------------------------------
@MAG_DOWN_TRAP
#DIM DICE
;------------------------------
;魔法陣地の罠

SIF FLAG:5 & 32
	PRINTFORML %SAVESTR:A%に効果的な魔法攻撃を行う陣を敷いた......

DICE = RAND:(FLAG:85 + 1) + RAND:20 + 1

SIF FLAG:5 & 32
	PRINTFORML %SAVESTR:A%の受ける魔法ダメ-ジが{DICE}％強化された......

CFLAG:A:682 += DICE

RETURN 0

;------------------------------
@ALL_DOWN_TRAP
#DIM DICE
;------------------------------
;腕の罠

SIF FLAG:5 & 32
	PRINTFORML %SAVESTR:A%に白い腕が絡みつき,呪いをかけた......

DICE = (RAND:(FLAG:85 + 1) + RAND:20) / 2 + 1

SIF FLAG:5 & 32
	PRINTFORML %SAVESTR:A%は{DICE}％弱化した......

CFLAG:A:680 += DICE
CFLAG:A:681 += DICE
CFLAG:A:682 += DICE

RETURN 0

;-------------------------------------------
@SLAVE_TRAP_SET
;-------------------------------------------
;迎撃中に罠を補充する

;補充行動か
SIF CFLAG:A:500 != 2
	RETURN 0

LOCAL = CFLAG:A:501 + 299
	
$TRAP_LOOP_2
	
LOCAL:1 = FLAG:LOCAL

IF LOCAL:1 > 0
	
	;補充
	SIF ITEM:(LOCAL:1) > 0 && ITEM:(LOCAL:1) < 99
		ITEM:(LOCAL:1) += 1
	;余りを換金
	IF ITEM:(LOCAL:1) >= 99
		P = LOCAL:1
		CALL TRAP_PRICE
		MONEY += RESULT
		SIF FLAG:5 & 32
			PRINTFORML 罠の余りを売却し{RESULT}の収入を得た
	ENDIF
ENDIF

LOCAL += 10

SIF LOCAL < 330
	GOTO TRAP_LOOP_2

RETURN 0

;---------------------------------------------
@TRAP_PRICE
;---------------------------------------------
;罠の値段をRETURNで返す関数
;引き値はP

SIF P == 60
	RETURN 10
SIF P == 61
	RETURN 50
SIF P == 62
	RETURN 80
SIF P == 63
	RETURN 100
SIF P == 64
	RETURN 120
SIF P == 65
	RETURN 150
SIF P == 66
	RETURN 170
SIF P == 67
	RETURN 180
SIF P == 68
	RETURN 190
SIF P == 69
	RETURN 1000
SIF P == 70
	RETURN 180
SIF P == 71
	RETURN 90
SIF P == 72
	RETURN 10
SIF P == 73
	RETURN 70
SIF P == 74
	RETURN 200
SIF P == 75
	RETURN 180
SIF P == 76
	RETURN 300
SIF P == 77
	RETURN 50
SIF P == 78
	RETURN 110
SIF P == 79
	RETURN 80
SIF P == 80
	RETURN 60
SIF P == 81
	RETURN 20
SIF P == 82
	RETURN 40
SIF P == 83
	RETURN 40
SIF P == 84
	RETURN 40
SIF P == 85
	RETURN 100

RETURN 100



















































