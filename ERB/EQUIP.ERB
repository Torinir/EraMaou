;装備品
;魔力を持ったアーティファクトたち
;WEAPONのW配列にすべてを格納して扱う
;指輪は購入後設置
;武器はUSE_EX_ITEM.ERBで入手

;W:1  = 識別番号(0～999)
;W:2  = 強度(0～10)
;W:3  = 効果(強度で効果が増減する)
;W:4  = 値段(実際は強度によって補正がかかる)
;W:5  = 呪いフラグ
;W:6  = 特殊
;	0ビット(&  1):毒追加
;	1ビット(&  2):火炎効果
;	2ビット(&  4):冷気効果
;	3ビット(&  8):電撃効果
;	4ビット(& 16):
;	5ビット(& 32):
;	6ビット(& 64):
;	7ビット(&128):
;	8ビット(&256):
;W:7  = 装備個所(0-武装 1-装飾)
;W:8  = メモ.主に引数
;W:9  = ダメージ強化(ダメージが何％になるか)
;W:10 = 弾消費(数値が大きいほど多く消費する.弾はCFLAG:571)
;W:11 = ミス(何％でミスをするか)
;W:12 = 気力回復(数値が大きいほど多く回復する.負だと気力消費)
;W:13 = 連続攻撃(何％で連続攻撃に連鎖するか)
;W:14 = 防御ダメージ(防御へのダメージが何％になるか)
;W:15 = 弾が尽きたときの挙動(1:ダメージ半減 2:攻撃不可)
;W:16 = 気力ダメージ(気力へのダメージが何％になるか)
;W:17 = 接頭語

;(CFLAG:550～559は装備品枠)
;CFLAG:550 = 武装
;CFLAG:551 = 装飾
;CFLAG:552 = 装飾2
;ここに格納するのは識別番号と強度を合成した数字
;格納番号 = (接頭語 * 100000) + (強度 * 1000) + 識別番号

;効果(強度がマイナスの場合,逆の効果)
;0  なし
;1  ダメージ増加
;2  装備強化
;3  速度UP
;4  HP回復
;5  気力回復
;6  即落ち
;7  攻撃変動
;8  防御変動
;9  支配
;10 経験値増加
;11 装備劣化
;12 速度減少
;13 HP-気力減少
;14 攻撃-防御減少
;15 洗脳
;16 罠避け
;17 侵攻強化
;18 キャンプ
;19 侵攻弱化＆キャンプ禁止
;20 罠誘発

;-------------------------------------------------
@EQUIP_CHECK
;-------------------------------------------------
;W:8と同一の効果の強度合計を算出する.Aはキャラ

SIF A < 0
	RETURN 0
LOCAL = 0
W:0 = CFLAG:A:551
CALL EQUIP_DATABASE
IF RESULT
	SIF W:3 == W:8
		LOCAL += W:2
ENDIF

W:0 = CFLAG:A:552
CALL EQUIP_DATABASE
IF RESULT
	SIF W:3 == W:8
		LOCAL += W:2
ENDIF

RETURN LOCAL

;------------------------------------------------
@REMOVE_CURSE
;------------------------------------------------
;呪われたアイテムを解呪してきれいなアイテムにする
;対象のアイテムはITEMを格納したW:8.解呪者はA.階層はCFLAG:A:501
;RETURN 0は装備しない 1は装備

CALL GET_EQUIP_NUM

;入手階層に応じた強度になる
W:0 += CFLAG:A:501 * 1000

CALL EQUIP_DATABASE

SIF RESULT == 0
	RETURN 0

;呪われてないならリターン
SIF W:5 == 0
	RETURN 0

;神官と忍者以外は高確率で失敗し呪い品装着
IF TALENT:A:202 == 0 && TALENT:A:207 == 0 && RAND:3 == 0
	PRINTFORMW %SAVESTR:A%は解呪に失敗してしまった!
	RETURN 1
ELSEIF RAND:8 == 0
	PRINTFORMW %SAVESTR:A%は解呪に失敗してしまった!
	RETURN 1
ENDIF

PRINTFORMW %SAVESTR:A%は解呪に成功した

D = RAND:100
IF D < 20
	W:1 = 8
ELSEIF D < 40
	W:1 = 7
ELSEIF D < 60
	W:1 = 4
ELSEIF D < 80
	W:1 = 5
ELSEIF D < 85
	W:1 = 17
ELSEIF D < 90
	W:1 = 16
ELSEIF D < 95
	W:1 = 18
ELSEIF D < 96
	W:1 = 3
ELSEIF D < 97
	W:1 = 2
ELSEIF D < 98
	W:1 = 9
ELSEIF D < 99
	W:1 = 10
ELSEIF D < 100
	W:1 = 1
ELSE
	W:1 = 0
ENDIF

;解呪品は地味に強度アップする
SIF W:2 < 10
	W:2 += 1

W:0 = W:1 + W:2 * 1000
CALL EQUIP_DATABASE
RETURN 1
;------------------------------------------------
@CURSE_EQUIP_RING
;------------------------------------------------
;装飾の指輪を呪われたアイテムにする
;対象のアイテムはW:0 ITEM:300
;最大10個まで

REPEAT 10
	SIF ITEM:300 <= 0
		RETURN 0
	
	ITEM:300 -= 1
	
	D = RAND:100
	
	IF D < 20
		W:1 = 13
	ELSEIF D < 40
		W:1 = 14
	ELSEIF D < 60
		W:1 = 19
	ELSEIF D < 80
		W:1 = 20
	ELSEIF D < 90
		W:1 = 12
	ELSEIF D < 95
		W:1 = 11
	ELSEIF D < 98
		W:1 = 6
	ELSEIF D < 100
		W:1 = 15
	ELSE
		W:1 = 13
	ENDIF
	
	;初期レベルは0
	W:2 = 0
	
	W:0 = W:1
	
	PRINT あなたは装飾の指輪から
	CALL PRINT_EQUIPTYPE_RING
	PRINTL を製造した
	
	CALL EQUIP_GET
REND
WAIT
RETURN 1

;------------------------------------------------
@EQUIP_SELECT
;------------------------------------------------
;宝箱のアイテムを装備するか,階層で判定する.Aはキャラ.階層はCFLAG:A:501
;呪われたアイテムを装備している場合装備更新できない

SIF A < 0
	RETURN 0

;宝箱チェック
IF CFLAG:A:1 == 12
	;イベント中
	CALL CAMPAIGN_EQUIP_SELECT,CFLAG:A:501
	X = RESULT
	SIF X < 300 
		RETURN 0
ELSE
	Y = CFLAG:A:501 + 339
	X = FLAG:Y
	SIF X < 300 
		RETURN 0
	;アイテム消費
	IF ITEM:X <= 0
		RETURN 0
	ELSE
		ITEM:X -= 1
	ENDIF
ENDIF

PRINTW 勇者は宝箱を見つけた!

W:8 = X

W:0 = CFLAG:A:551

CALL EQUIP_DATABASE

IF RESULT && W:2 < CFLAG:A:501 && W:5 == 0
	CALL REMOVE_CURSE
	IF RESULT && W:7 == 1
		CFLAG:A:551 = W:0
		PRINT 勇者は
		CALL PRINT_EQUIPTYPE_RING
		PRINTW を装備した
		RETURN 0
	ENDIF
ENDIF

W:0 = CFLAG:A:552

CALL EQUIP_DATABASE

IF RESULT && W:2 < CFLAG:A:501 && W:5 == 0
	CALL REMOVE_CURSE
	IF RESULT && W:7 == 1
		CFLAG:A:552 = W:0
		PRINT 勇者は
		CALL PRINT_EQUIPTYPE_RING
		PRINTW を装備した
		RETURN 0
	ENDIF
ENDIF

PRINTW めぼしいものは無かったようだ

RETURN 0

;------------------------------------------------
@EQUIP_DATABASE
;------------------------------------------------

SIF W:0 < 0
	RETURN 0

;W:0に入れた格納番号から識別番号をW:1に,強度をW:2に,接頭語をW:17に
W:1 = W:0 % 1000
W:2 = W:0 % 100000
W:2 /= 1000
W:17 = W:0 / 100000


IF W:1 == 0
	;装飾指輪
	W:3 = 0
	W:4 = 100
	W:5 = 0
	W:6 = 0
	W:7 = 1
ELSEIF W:1 == 1
	;破壊の指輪
	W:3 = 1
	W:4 = 10000
	W:5 = 0
	W:6 = 0
	W:7 = 1
ELSEIF W:1 == 2
	;護りの指輪
	W:3 = 2
	W:4 = 10000
	W:5 = 0
	W:6 = 0
	W:7 = 1
ELSEIF W:1 == 3
	;加速の指輪
	W:3 = 3
	W:4 = 50000
	W:5 = 0
	W:6 = 0
	W:7 = 1
ELSEIF W:1 == 4
	;再生の指輪
	W:3 = 4
	W:4 = 20000
	W:5 = 0
	W:6 = 0
	W:7 = 1
ELSEIF W:1 == 5
	;意思の指輪
	W:3 = 5
	W:4 = 20000
	W:5 = 0
	W:6 = 0
	W:7 = 1
ELSEIF W:1 == 6
	;欲望の指輪
	W:3 = 6
	W:4 = 5000
	W:5 = 1
	W:6 = 0
	W:7 = 1
ELSEIF W:1 == 7
	;怪力の指輪
	W:3 = 7
	W:4 = 8000
	W:5 = 0
	W:6 = 0
	W:7 = 1
ELSEIF W:1 == 8
	;強靭の指輪
	W:3 = 8
	W:4 = 8000
	W:5 = 0
	W:6 = 0
	W:7 = 1
ELSEIF W:1 == 9
	;支配の指輪
	W:3 = 9
	W:4 = 100000
	W:5 = 0
	W:6 = 0
	W:7 = 1
ELSEIF W:1 == 10
	;成長の指輪
	W:3 = 10
	W:4 = 70000
	W:5 = 0
	W:6 = 0
	W:7 = 1
ELSEIF W:1 == 11
	;虚弱の指輪
	W:3 = 11
	W:4 = 1000
	W:5 = 1
	W:6 = 0
	W:7 = 1
ELSEIF W:1 == 12
	;鈍重の指輪
	W:3 = 12
	W:4 = 1000
	W:5 = 1
	W:6 = 0
	W:7 = 1
ELSEIF W:1 == 13
	;死の指輪
	W:3 = 13
	W:4 = 1000
	W:5 = 1
	W:6 = 0
	W:7 = 1
ELSEIF W:1 == 14
	;衰弱の指輪
	W:3 = 14
	W:4 = 1000
	W:5 = 1
	W:6 = 0
	W:7 = 1
ELSEIF W:1 == 15
	;洗脳の指輪
	W:3 = 15
	W:4 = 1000
	W:5 = 1
	W:6 = 0
	W:7 = 1
ELSEIF W:1 == 16
	;罠避けの指輪
	W:3 = 16
	W:4 = 1000
	W:5 = 0
	W:6 = 0
	W:7 = 1
ELSEIF W:1 == 17
	;侵攻の指輪
	W:3 = 17
	W:4 = 1000
	W:5 = 0
	W:6 = 0
	W:7 = 1
ELSEIF W:1 == 18
	;結界の指輪
	W:3 = 18
	W:4 = 1000
	W:5 = 0
	W:6 = 0
	W:7 = 1
ELSEIF W:1 == 19
	;試練の指輪
	W:3 = 19
	W:4 = 1000
	W:5 = 1
	W:6 = 0
	W:7 = 1
ELSEIF W:1 == 20
	;不幸の指輪
	W:3 = 20
	W:4 = 1000
	W:5 = 1
	W:6 = 0
	W:7 = 1
ELSEIF W:1 == 40
	;ソード
	W:3 = 0
	W:4 = 100
	W:5 = 0
	W:6 = 0
	W:7 = 0
	W:9 = 100
	W:10 = 0
	W:11 = 0
	W:12 = 0
	W:13 = 0
	W:14 = 100
	W:15 = 0
	W:16 = 100
ELSEIF W:1 == 41
	;スタッフ
	W:3 = 0
	W:4 = 100
	W:5 = 0
	W:6 = 0
	W:7 = 0
	W:9 = 80
	W:10 = 0
	W:11 = 0
	W:12 = 20
	W:13 = 0
	W:14 = 100
	W:15 = 0
	W:16 = 100
ELSEIF W:1 == 42
	;ウィップ
	W:3 = 0
	W:4 = 100
	W:5 = 0
	W:6 = 0
	W:7 = 0
	W:9 = 80
	W:10 = 0
	W:11 = 0
	W:12 = 0
	W:13 = 0
	W:14 = 120
	W:15 = 0
	W:16 = 100
ELSEIF W:1 == 43
	;ダガー
	W:3 = 0
	W:4 = 100
	W:5 = 0
	W:6 = 0
	W:7 = 0
	W:9 = 70
	W:10 = 0
	W:11 = 0
	W:12 = 0
	W:13 = 30
	W:14 = 100
	W:15 = 0
	W:16 = 100
ELSEIF W:1 == 44
	;シュリケン
	W:3 = 0
	W:4 = 100
	W:5 = 0
	W:6 = 0
	W:7 = 0
	W:9 = 100
	W:10 = 1
	W:11 = 0
	W:12 = 0
	W:13 = 30
	W:14 = 100
	W:15 = 1
	W:16 = 100
ELSEIF W:1 == 45
	;アロー
	W:3 = 0
	W:4 = 100
	W:5 = 0
	W:6 = 0
	W:7 = 0
	W:9 = 150
	W:10 = 1
	W:11 = 0
	W:12 = 0
	W:13 = 00
	W:14 = 100
	W:15 = 2
	W:16 = 100
ELSEIF W:1 == 46
	;メイス
	W:3 = 0
	W:4 = 100
	W:5 = 0
	W:6 = 0
	W:7 = 0
	W:9 = 120
	W:10 = 0
	W:11 = 10
	W:12 = 0
	W:13 = 0
	W:14 = 100
	W:15 = 0
	W:16 = 100
ELSEIF W:1 == 47
	;ハンマー
	W:3 = 0
	W:4 = 100
	W:5 = 0
	W:6 = 0
	W:7 = 0
	W:9 = 150
	W:10 = 0
	W:11 = 30
	W:12 = 0
	W:13 = 0
	W:14 = 100
	W:15 = 0
	W:16 = 100
ELSEIF W:1 == 48
	;サイズ
	W:3 = 0
	W:4 = 100
	W:5 = 0
	W:6 = 0
	W:7 = 0
	W:9 = 100
	W:10 = 0
	W:11 = 10
	W:12 = 20
	W:13 = 0
	W:14 = 100
	W:15 = 0
	W:16 = 100
ELSEIF W:1 == 49
	;テンタクル
	W:3 = 0
	W:4 = 100
	W:5 = 0
	W:6 = 0
	W:7 = 0
	W:9 = 100
	W:10 = 0
	W:11 = 0
	W:12 = -10
	W:13 = 0
	W:14 = 100
	W:15 = 0
	W:16 = 120
ELSEIF W:1 == 50
	;レイピア
	W:3 = 0
	W:4 = 100
	W:5 = 0
	W:6 = 0
	W:7 = 0
	W:9 = 80
	W:10 = 0
	W:11 = 0
	W:12 = 0
	W:13 = 50
	W:14 = 100
	W:15 = 0
	W:16 = 100
ELSEIF W:1 == 51
	;クレセントブレード
	W:3 = 0
	W:4 = 100
	W:5 = 0
	W:6 = 0
	W:7 = 0
	W:9 = 135
	W:10 = 0
	W:11 = 20
	W:12 = 0
	W:13 = 0
	W:14 = 100
	W:15 = 0
	W:16 = 100
ELSEIF W:1 == 52
	;ナックル
	W:3 = 0
	W:4 = 100
	W:5 = 0
	W:6 = 0
	W:7 = 0
	W:9 = 65
	W:10 = 0
	W:11 = 0
	W:12 = 0
	W:13 = 40
	W:14 = 100
	W:15 = 0
	W:16 = 100
ELSE
	;黒の指輪-ソード
	W:0 = 0
	W:1 = 0
	W:2 = 0
	W:3 = 0
	W:4 = 100
	W:5 = 1
	W:6 = 0
	W:7 = 1
	W:9 = 100
	W:10 = 0
	W:11 = 0
	W:12 = 0
	W:13 = 0
	W:14 = 100
	W:15 = 0
	W:16 = 100
ENDIF

;エンチャント効果
IF W:17 == 1
	;ジャイアント
	;ミス増加-ダメージ強化
	W:9 += 30
	W:11 += 20
ELSEIF W:17 == 2
	;ポイズン
	;毒フラグ-ダメージ弱化
	W:6 += 1
	W:9 -= 10
ELSEIF W:17 == 3
	;デス
	;気力消費-ダメージ強化
	W:9 += 40
	W:12 -= 30
ELSEIF W:17 == 4
	;スラッシュ
	;連続効果-ダメージ弱化
	W:9 -= 10
	W:13 += 20
ELSEIF W:17 == 5
	;ファイア
	;ミス増加-火炎フラグ
	W:6 += 2
	W:11 += 10
ELSEIF W:17 == 6
	;アイス
	;気力消費-冷気フラグ
	W:6 += 4
	W:12 -= 10
ELSEIF W:17 == 7
	;サンダー
	;ミス増加-ダメージ増加-気力消費-電撃フラグ
	W:6 += 8
	W:9 += 20
	W:11 += 10
	W:12 -= 10
ELSEIF W:17 == 8
	;マジカル
	;ダメージ減少-気力回復
	W:9 -= 10
	W:12 += 20
ELSEIF W:17 == 9
	;ダーク
	;気力消費-気力ダメージ増加
	W:12 -= 10
	W:16 += 20
ENDIF

;+強化によってダメージ増加
W:9 += W:2 * 5

RETURN 1

;-----------------------------------------------------
;名前表示
;-----------------------------------------------------
;武装
@PRINT_EQUIPTYPE_WEAPON

;W:0に入れた格納番号から識別番号をW:1に,強度をW:2に,接頭語をW:17に
W:1 = W:0 % 1000
W:2 = W:0 % 100000
W:2 /= 1000
W:17 = W:0 / 100000

SETCOLORBYNAME LightSalmon

;接頭語名

IF W:17 == 1
	PRINT Giant 
ELSEIF W:17 == 2
	PRINT Poisonous 
ELSEIF W:17 == 3
	PRINT Death 
ELSEIF W:17 == 4
	PRINT Slashing 
ELSEIF W:17 == 5
	PRINT Fire 
ELSEIF W:17 == 6
	PRINT Ice 
ELSEIF W:17 == 7
	PRINT Thunder 
ELSEIF W:17 == 8
	PRINT Magical 
ELSEIF W:17 == 9
	PRINT Dark 
ENDIF

;武器の識別番号は40～69を指定

IF W:1 == 40
	PRINT Sword
ELSEIF W:1 == 41
	PRINT Staff
ELSEIF W:1 == 42
	PRINT Whip
ELSEIF W:1 == 43
	PRINT Dagger
ELSEIF W:1 == 44
	PRINT Shuriken
ELSEIF W:1 == 45
	PRINT Arrow
ELSEIF W:1 == 46
	PRINT Mace
ELSEIF W:1 == 47
	PRINT Hammer
ELSEIF W:1 == 48
	PRINT サイズ
ELSEIF W:1 == 49
	PRINT Tentacle
ELSEIF W:1 == 50
	PRINT Rapier
ELSEIF W:1 == 51
	PRINT Crescent Blade
ELSEIF W:1 == 52
	PRINT Knuckle
ELSEIF W:1 == 53
	PRINT 
ELSEIF W:1 == 54
	PRINT 
ELSEIF W:1 == 55
	PRINT 
ELSEIF W:1 == 56
	PRINT 
ELSEIF W:1 == 57
	PRINT 
ELSEIF W:1 == 58
	PRINT 
ELSEIF W:1 == 59
	PRINT 
ELSEIF W:1 == 60
	PRINT 
ELSE
	PRINT Sword
	W:0 = 40
	W:1 = 40
	W:2 = 0
ENDIF

SIF W:2 != 0
	PRINTFORM +{W:2}

RESETCOLOR

;-------------------------------------------------------------------
;装飾.RINGだけど指輪以外もそのうち追加するかも
@PRINT_EQUIPTYPE_RING
;-------------------------------------------------------------------
;W:0に入れた格納番号から識別番号をW:1に,強度をW:2に,接頭語をW:17に
W:1 = W:0 % 1000
W:2 = W:0 % 100000
W:2 /= 1000
W:17 = W:0 / 100000

;指輪の識別番号は0～39を指定

SETCOLORBYNAME LightSalmon

IF W:1 == 0
	PRINT Decorative Ring
ELSEIF W:1 == 1
	PRINT Ring of Destruction
ELSEIF W:1 == 2
	PRINT Ring of Defense
ELSEIF W:1 == 3
	PRINT Ring of Acceleration
ELSEIF W:1 == 4
	PRINT Rind of Regeneration
ELSEIF W:1 == 5
	PRINT 意思の指輪
ELSEIF W:1 == 6
	PRINT Ring of Desire
ELSEIF W:1 == 7
	PRINT 怪力の指輪
ELSEIF W:1 == 8
	PRINT 強靭の指輪
ELSEIF W:1 == 9
	PRINT Ring of Domination
ELSEIF W:1 == 10
	PRINT 成長の指輪
ELSEIF W:1 == 11
	PRINT 虚弱の指輪
ELSEIF W:1 == 12
	PRINT 鈍重の指輪
ELSEIF W:1 == 13
	PRINT 死の指輪
ELSEIF W:1 == 14
	PRINT 衰弱の指輪
ELSEIF W:1 == 15
	PRINT 洗脳の指輪
ELSEIF W:1 == 16
	PRINT 罠避けの指輪
ELSEIF W:1 == 17
	PRINT 侵攻の指輪
ELSEIF W:1 == 18
	PRINT 結界の指輪
ELSEIF W:1 == 19
	PRINT 試練の指輪
ELSEIF W:1 == 20
	PRINT 不幸の指輪
ELSE
	PRINT 黒の指輪
	W:0 = 0
	W:1 = 0
	W:2 = 0
ENDIF

SIF W:2 != 0
	PRINTFORM +{W:2}

RESETCOLOR

RETURN 0

;-------------------------------------------------------------------
;入手
@EQUIP_GET
;-------------------------------------------------------------------
;W:0に入れた格納番号から識別番号をW:1に.基本的に一個入手

;無しの場合終了
SIF W:0 < 0
	RETURN 0

W:1 = W:0 % 1000

X = 300 + W:1

SIF X < 300
	X = 300

ITEM:X += 1

SIF ITEM:X > 99
	ITEM:X = 99

RETURN 0

;-------------------------------------------------------------------
;ITEMナンバーから識別番号入手
@GET_EQUIP_NUM
;-------------------------------------------------------------------
;W:8に入れたITEMナンバーから識別番号をW:0に

W:0 = W:8 - 300

SIF W:0 < 0
	W:0 = 0

RETURN 0

;-------------------------------------------------------------------
@EQUIP_POWERUP, ARG:0
;-------------------------------------------------------------------
;Wに入れた装備品の能力をキャラの能力で強化する
;EQUIP_DATABASE後に使用すること
;ARG:0がキャラ

;初心者(ダメージ減少-ミス増加)
IF TALENT:(ARG:0):291 == 1
	W:9 -= 10
	W:11 += 10
ENDIF

;悪魔尻尾(ダメージ強化)
SIF TALENT:(ARG:0):246 == 1
	W:9 += 10

;悪魔目(気力ダメージ強化)
SIF TALENT:(ARG:0):247 == 1
	W:16 += 10

;隻眼(ダメージ強化-ミス増加)
IF TALENT:(ARG:0):259 == 1
	W:9 += 10
	W:11 += 10
ENDIF

;額の眼(気力ダメージ強化-気力消費)
IF TALENT:(ARG:0):260 == 1
	W:16 += 10
	W:12 -= 10
ENDIF

;角(ダメージ強化-気力消費)
IF TALENT:(ARG:0):264 == 1
	W:9 += 10
	W:12 -= 10
ENDIF

;エルフの弓装備で連続ボーナス
SIF W:1 == 45 && TALENT:(ARG:0):314 == 1
	W:13 += 10

;天使は気力が回復する
SIF TALENT:(ARG:0):314 == 6
	W:12 += 10

;ダークエルフ(気力ダメージ強化)
SIF TALENT:(ARG:0):314 == 7
	W:16 += 10

;堕天使は連続攻撃
SIF TALENT:(ARG:0):314 == 8
	W:13 += 10

;魔族は防御ダメージ
SIF TALENT:(ARG:0):314 == 9
	W:14 += 10

;能力者効果

IF TALENT:(ARG:0):275
	;火の能力者
	GETBIT W:6, 1
	IF RESULT
		;火属性の場合,火力強化
		W:9 += 10
	ELSE
		;無属性の場合,ノーリスクで火属性化
		W:6 += 2
	ENDIF
ENDIF

IF TALENT:(ARG:0):276
	;氷の能力者
	GETBIT W:6, 2
	IF RESULT
		;冷気属性の場合,連続強化
		W:13 += 10
	ELSE
		;無属性の場合,ノーリスクで冷気属性化
		W:6 += 4
	ENDIF
ENDIF

IF TALENT:(ARG:0):277
	;雷の能力者
	GETBIT W:6, 3
	IF RESULT
		;電撃属性の場合,連続-攻撃強化
		W:13 += 5
		W:9 += 5
	ELSE
		;無属性の場合,ノーリスクで電撃属性化
		W:6 += 8
	ENDIF
ENDIF

IF TALENT:(ARG:0):278
	;光の能力者
	IF W:12 > 0
		;気力回復の場合,連続-攻撃-気力回復強化
		W:13 += 5
		W:9 += 5
		W:12 += 5
	ELSE
		;気力回復が無い場合,気力回復強化
		W:12 += 10
	ENDIF
ENDIF

IF TALENT:(ARG:0):279
	;闇の能力者
	IF W:16 > 0
		;気力ダメージ強化の場合,連続-攻撃-気力ダメージ強化
		W:13 += 5
		W:9 += 5
		W:16 += 5
	ELSE
		;気力ダメージ強化が無い場合,気力ダメージ強化
		W:16 += 10
	ENDIF
ENDIF

RETURN 0

;-------------------------------------------------------------------
@EQUIP_ST_SHOW, ARG:0
;-------------------------------------------------------------------
;Wに入れた装備品の能力を表示する
;ARG:0がキャラ

W:0 = CFLAG:ARG:550

CALL PRINT_EQUIPTYPE_WEAPON
PRINTL  
CALL EQUIP_DATABASE
CALL EQUIP_POWERUP,ARG

SIF W:5
	PRINTL *それは呪われている
SIF W:6 & 1
	PRINTL *それは毒が滴っている
SIF W:6 & 2
	PRINTL *それは炎を帯びている
SIF W:6 & 4
	PRINTL *それは冷気を帯びている
SIF W:6 & 8
	PRINTL *それは電気を帯びている
;SIF W:7 == 0
;	PRINTL *それは武器である
;SIF W:7 == 1
;	PRINTL *それは装飾品である
SIF W:9
	PRINTFORML *それは{W:9}の打撃力を持つ
SIF W:11
	PRINTFORML *それは{W:11}％の確率で空振りする
SIF W:12 > 0
	PRINTFORML *それは振るうたび気力が{W:12}回復する
SIF W:12 < 0
	PRINTFORML *それは振るうたび気力を{W:12 * -1}消費する
SIF W:13
	PRINTFORML *それは{W:13}％の確率で2段攻撃をする
SIF W:14 != 100
	PRINTFORML *それは防御力への打撃を{W:14}％にする
SIF W:16 != 100
	PRINTFORML *それは気力への打撃を{W:16}％にする

RETURN 2

;-----------------------------------------------
@SHOW_BUTTON_EQUIP(NUM, ARG)
#DIM NUM
;-----------------------------------------------
;キャラの能力表示において[装備詳細]ボタンを表示する
;引数NUMはボタンの数値,ARGは対象キャラの番号

LOCAL = CHECK_ABLE_TO_SHOW_EQUIP(ARG)
IF LOCAL == 1
	; 条件に合わないならボタン自体を表示しない
	RETURN 0
ENDIF
PRINTFORM [{NUM}] Equipment Information
RESETCOLOR
RETURN 0

;-----------------------------------------------
@CHECK_ABLE_TO_SHOW_EQUIP,ARG
#FUNCTION
;-----------------------------------------------
;装備品のステータスを見れるか判定する
;ARGはキャラ

;売却可なら見れる
SIF CFLAG:ARG:0 > 0
	RETURNF 0
;信頼度20以上なら見れる
SIF CFLAG:ARG:2 >= 20
	RETURNF 0
;従順1以上なら見れる
SIF ABL:ARG:10 > 0
	RETURNF 0
;目立ちたがりなら見れる
SIF TALENT:ARG:28
	RETURNF 0
;カルマ0以下なら見れる
SIF CFLAG:ARG:151 <= 0
	RETURNF 0

;見せられないよ
RETURNF 1

