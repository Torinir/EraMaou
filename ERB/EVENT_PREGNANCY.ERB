;EVENT_P,EVENT_PREGNANCY,妊娠関連のイベント
;eratohoAより流用
;eraIM@Sから導入しました(eramaou)

;---------------------------------------------------------
;フラグ管理　口上用のフラグと被るので,変更
;---------------------------------------------------------

;CFLAG
;101→マスターによる膣内射精カウント用
;102→誰によって妊娠させられたか(マスター = 1, 助手 = 2, 奴隷 = 3, 客 = 4, 犬 = 5, モンスター-触手 = 6, 狂王 = 7)
;103→助手→奴隷間での膣内射精カウント用
;104→奴隷→助手間での膣内射精カウント用
;105→娼館などの客(NTR乱交等)と奴隷間の膣内射精カウント用
;106→ノラ犬から奴隷への膣内射精カウント用
;107→モンスター-触手から奴隷への膣内射精カウント用
;108→狂王から奴隷への膣内射精カウント用
;109→排卵誘発剤の使用の有無
;110→出産日
;111→父親は??
;112→父親のモンスターID
;113→妊娠箇所(0=子宮 1=乳房 2=精巣)
;121→妊娠時の増加体重
;122→妊娠時の増加バスト
;123→妊娠時の増加ウェスト
;124→妊娠時の増加ヒップ
;追加 FLAG
;97→臨月or育児中の奴隷はいるか?
;98→射精箇所選択機能および妊娠機能のON-OFF
;99→射精選択の自動モード時射精先選択機能のON-OFF
;TFLAG
;700→育児部屋コマンド実行した?

;---------------------------------------------------------
;関数群統括関数
;---------------------------------------------------------

@IN_VAGINA_ALL

SIF TARGET < 0 || TARGET >= CHARANUM
	RETURN 0
SIF ASSI >= CHARANUM
	RETURN 0
CALL IN_VAGINA_M_TO_T
CALL IN_VAGINA_M_TO_A
CALL IN_VAGINA_T_TO_M
CALL IN_VAGINA_A_TO_T
CALL IN_VAGINA_T_TO_A
CALL IN_VAGINA_D_TO_T
CALL IN_VAGINA_SYOKU_TO_T
CALL IN_VAGINA_SYOKU_TO_M
;売春-狂王-獣姦ショーによる妊娠判定は@EVENTTURNENDでまとめて行う
;CALL IN_VAGINA_EXTRA
;CALL IN_VAGINA_KYOUOU_TO_T
;CALL IN_VAGINA_NTRD_TO_T
@CONCEPTION_CHECK_ALL
SIF TARGET < 0 || TARGET >= CHARANUM
	RETURN 0
SIF ASSI >= CHARANUM
	RETURN 0
CALL CONCEPTION_CHECK_M_TO_T
CALL CONCEPTION_CHECK_M_TO_A
CALL CONCEPTION_CHECK_T_TO_M
CALL CONCEPTION_CHECK_A_TO_T
CALL CONCEPTION_CHECK_T_TO_A
CALL CONCEPTION_CHECK_D_TO_T
CALL CONCEPTION_CHECK_SYOKU_TO_T
CALL CONCEPTION_CHECK_SYOKU_TO_M
;売春-狂王-獣姦ショーによる妊娠判定は@EVENTTURNENDでまとめて行う
;CALL CONCEPTION_CHECK_EXTRA
;CALL CONCEPTION_CHECK_KYOUOU_TO_T
;CALL CONCEPTION_CHECK_NTRD_TO_T

@GET_CHILD_ALL
SIF TARGET < 0 || TARGET >= CHARANUM
	RETURN 0
SIF ASSI >= CHARANUM
	RETURN 0
REPEAT CHARANUM
	CALL GET_CHILD
REND

;---------------------------------------------------------
;膣内射精-妊娠フラグに絡む関数群
;---------------------------------------------------------

@IN_VAGINA_M_TO_T

;膣内射精による妊娠,非妊娠のチェック
IF TARGET >= 1
	;奴隷の妊娠-同族妊娠できないチェック
	IF TALENT:TARGET:153 == 0 && TALENT:TARGET:158 == 0
		CALL NAKADASHI_CHECK, TARGET, 1
	ENDIF
ENDIF

@IN_VAGINA_M_TO_A
;膣内射精による妊娠,非妊娠のチェック
;助手がいるか
IF ASSI >= 1
	;助手の妊娠チェック
	IF TALENT:ASSI:153 == 0 && TALENT:ASSI:158 == 0
		CALL NAKADASHI_CHECK, ASSI, 1
	ENDIF
ENDIF

@IN_VAGINA_T_TO_M
;奴隷→主人への膣内射精による妊娠,非妊娠のチェック
;主人の妊娠チェック
IF MASTER == 0
	IF TALENT:MASTER:153 == 0 && TALENT:MASTER:158 == 0
		CALL NAKADASHI_CHECK, MASTER, 3
	ENDIF
ENDIF

@IN_VAGINA_A_TO_T
;助手→奴隷への膣内射精による妊娠判定
;助手とターゲットがいるか
IF ASSI >= 1 && TARGET >= 1
	;奴隷の妊娠チェック
	IF TALENT:TARGET:153 == 0 && TALENT:TARGET:158 == 0
		CALL NAKADASHI_CHECK, TARGET, 2
	ENDIF
ENDIF

@IN_VAGINA_T_TO_A
;奴隷→助手への膣内射精による妊娠判定
;助手がいるか
IF ASSI >= 1
	;助手の妊娠チェック
	IF TALENT:ASSI:153 == 0 && TALENT:ASSI:158 == 0
		CALL NAKADASHI_CHECK, ASSI, 3
	ENDIF
ENDIF

@IN_VAGINA_D_TO_T
;ノラ犬→奴隷への膣内射精による妊娠判定
IF TARGET >= 1
	;奴隷の妊娠チェック(人狼-ケモノの場合,異種妊娠可か)
	IF TALENT:TARGET:153 == 0
		CALL NAKADASHI_CHECK, TARGET, 5
	ENDIF
ENDIF

@IN_VAGINA_SYOKU_TO_T
;モンスター-触手→奴隷への膣内射精による妊娠判定
IF TARGET >= 1
	;奴隷の妊娠チェック
	IF TALENT:TARGET:153 == 0
		CALL NAKADASHI_CHECK, TARGET, 6
	ENDIF
ENDIF

@IN_VAGINA_SYOKU_TO_M
;モンスター-触手→主人への膣内射精による妊娠判定
IF MASTER == 0
	;奴隷の妊娠チェック
	IF TALENT:MASTER:153 == 0
		CALL NAKADASHI_CHECK, MASTER, 6
	ENDIF
ENDIF

@IN_VAGINA_KYOUOU_TO_T
;狂王→奴隷への膣内射精による妊娠判定
REPEAT CHARANUM
	;奴隷の妊娠チェック
	IF TALENT:COUNT:153 == 0 && TALENT:COUNT:158 == 0
		CALL NAKADASHI_CHECK, COUNT, 7
	ENDIF
REND

@IN_VAGINA_NTRD_TO_T
;獣姦ショー→奴隷への膣内射精による妊娠判定
REPEAT CHARANUM
	;奴隷の妊娠チェック
	IF TALENT:COUNT:153 == 0
		CALL NAKADASHI_CHECK, COUNT, 5
	ENDIF
REND

@IN_VAGINA_EXTRA
;娼館やレンタル時に客に膣射された場合の妊娠判定
REPEAT CHARANUM
	IF TALENT:COUNT:153 == 0 && TALENT:COUNT:158 == 0
		CALL NAKADASHI_CHECK, COUNT, 4
	ENDIF
REND

@NAKADASHI_CHECK, ARG:0, ARG:1
#DIM NAKADASHI_PARTNER
#DIM HAIRANZAI
#DIM OTHER_POOL
;ARG:0はキャラ登録番号,ARG:1はCFLAG:102フラグ
;102→誰によって妊娠させられたか(マスター = 1, 助手 = 2, 奴隷 = 3, 客 = 4, 犬 = 5, モンスター-触手 = 6 狂王 = 7)
;中出しされた精子量に応じて確率処理
;膣外フラグを一度退避
OTHER_POOL = TALENT:(ARG:0):113
;膣外フラグを初期化(受精失敗用に)
TALENT:(ARG:0):113 = 0
;妊娠出産機能オフ時
IF GETBIT(FLAG:5,2) == 0
	NAKADASHI_PARTNER = 101 + ARG:1
	SIF ARG:1 == 1
		NAKADASHI_PARTNER = 101
	;妊娠不可でも膣射のリセット
	CFLAG:(ARG:0):NAKADASHI_PARTNER = 0
	RETURN 0
ENDIF

;チェックするフラグ設定
;NAKADASHI_PARTNERは101,103～108　CFLAG:(ARG:0):NAKADASHI_PARTNERでCFLAG:101,CFLAG:103,CFLAG:104,CFLAG:105,CFLAG:106,CFLAG:107,CFLAG:108になる.
NAKADASHI_PARTNER = 101 + ARG:1
SIF ARG:1 == 1
	NAKADASHI_PARTNER = 101

;対象が男か未熟なら関数終了
SIF TALENT:(ARG:0):122 || TALENT:(ARG:0):135
	RETURN 0

;獣姦で対象が動物耳じゃないなら関数終了
SIF ARG:1 == 5 && TALENT:(ARG:0):124 == 0
	RETURN 0

;中だしされてないなら関数終了
SIF CFLAG:(ARG:0):NAKADASHI_PARTNER == 0
	RETURN 0

;妊娠が既に確定している場合は関数終了
IF CFLAG:(ARG:0):110 > 0
	CFLAG:(ARG:0):NAKADASHI_PARTNER = 0
	RETURN 0
ENDIF

;妊娠中もしくは育児中は関数終了
IF TALENT:(ARG:0):153 || TALENT:(ARG:0):154
	CFLAG:(ARG:0):NAKADASHI_PARTNER = 0
	RETURN 0
ENDIF

;排卵剤の有無による定数設定
HAIRANZAI = 3 - (CFLAG:(ARG:0):109 * 2)

;人狼の場合,満月時に妊娠しやすく
IF TALENT:(ARG:0):314 == 2 && DAY:2 >= 14 && DAY:2 <= 16 && CFLAG:(ARG:0):109 == 1
	HAIRANZAI = 1
ELSEIF TALENT:(ARG:0):314 == 2 && DAY:2 >= 14 && DAY:2 <= 16
	HAIRANZAI = 2
ENDIF

IF CFLAG:(ARG:0):NAKADASHI_PARTNER >= 30
	SIF RAND:((1 + TALENT:(ARG:0):100 * 2) * HAIRANZAI) <= 2
		CFLAG:(ARG:0):102 = ARG:1
ELSEIF CFLAG:(ARG:0):NAKADASHI_PARTNER >= 20
	SIF RAND:((2 + TALENT:(ARG:0):100 * 2) * HAIRANZAI) <= 2
		CFLAG:(ARG:0):102 = ARG:1
ELSEIF CFLAG:(ARG:0):NAKADASHI_PARTNER >= 15
	SIF RAND:((3 + TALENT:(ARG:0):100 * 2) * HAIRANZAI) <= 2
		CFLAG:(ARG:0):102 = ARG:1
ELSEIF CFLAG:(ARG:0):NAKADASHI_PARTNER >= 10
	SIF RAND:((4 + TALENT:(ARG:0):100 * 2) * HAIRANZAI) <= 2
		CFLAG:(ARG:0):102 = ARG:1
ELSEIF CFLAG:(ARG:0):NAKADASHI_PARTNER >= 5
	SIF RAND:((5 + TALENT:(ARG:0):100 * 2) * HAIRANZAI) <= 2
		CFLAG:(ARG:0):102 = ARG:1
ELSEIF CFLAG:(ARG:0):NAKADASHI_PARTNER >= 1
	SIF RAND:((6 + TALENT:(ARG:0):100 * 2) * HAIRANZAI) <= 2
		CFLAG:(ARG:0):102 = ARG:1
ENDIF

;判定を行った膣射のリセット
CFLAG:(ARG:0):NAKADASHI_PARTNER = 0
;膣外フラグを戻す
TALENT:(ARG:0):113 = OTHER_POOL

;---------------------------------------------------------
;妊娠判定関数郡
;---------------------------------------------------------

@CONCEPTION_CHECK_M_TO_T
;マスターの精液でターゲット妊娠確定時の処理
;奴隷がいるか
IF TARGET >= 1
	IF TALENT:TARGET:153 == 0 && CFLAG:TARGET:102 == 1 && CFLAG:TARGET:110 == 0
		;出産系のフラグ設定
		CFLAG:TARGET:110 = DAY + 10 + RAND:6
		CFLAG:TARGET:111 = NO:MASTER
	ENDIF
ENDIF


@CONCEPTION_CHECK_M_TO_A
;マスターの精液で助手妊娠確定時の処理
;助手がいるか
IF ASSI >= 1
	IF TALENT:ASSI:153 == 0 && CFLAG:ASSI:102 == 1 && CFLAG:ASSI:110 == 0
		;出産系のフラグ設定
		CFLAG:ASSI:110 = DAY + 10 + RAND:6
		CFLAG:ASSI:111 = NO:MASTER
	ENDIF
ENDIF


@CONCEPTION_CHECK_T_TO_M
;奴隷の精液でマスター妊娠確定時の処理
IF MASTER == 0
	IF TALENT:MASTER:153 == 0 && CFLAG:MASTER:102 == 3 && CFLAG:MASTER:110 == 0
		;出産系のフラグ設定
		CFLAG:MASTER:110 = DAY + 10 + RAND:6
		CFLAG:MASTER:111 = TARGET
		CSTR:MASTER:2 = %SAVESTR:TARGET%
	ENDIF
ENDIF


@CONCEPTION_CHECK_A_TO_T
;助手の精液で奴隷妊娠確定時の処理
;助手と奴隷がいるか
IF ASSI >= 1 && TARGET >= 1
	IF TALENT:TARGET:153 == 0 && CFLAG:TARGET:102 == 2 && CFLAG:TARGET:110 == 0
		;出産系のフラグ設定
		CFLAG:TARGET:110 = DAY + 10 + RAND:6
		CFLAG:TARGET:111 = ASSI
		CSTR:TARGET:2 = %SAVESTR:ASSI%
	ENDIF
ENDIF


@CONCEPTION_CHECK_T_TO_A
;奴隷の精液で助手妊娠確定時の処理
;助手がいるか
IF ASSI >= 1 && TARGET >= 1
	IF TALENT:ASSI:153 == 0 && CFLAG:ASSI:102 == 3 && CFLAG:ASSI:110 == 0
		;出産系のフラグ設定
		CFLAG:ASSI:110 = DAY + 10 + RAND:6
		CFLAG:ASSI:111 = TARGET
		CSTR:ASSI:2 = %SAVESTR:TARGET%
	ENDIF
ENDIF

@CONCEPTION_CHECK_D_TO_T
;犬の精液で奴隷妊娠確定時の処理
;奴隷がいるか
IF TARGET >= 1
	IF TALENT:TARGET:153 == 0 && CFLAG:TARGET:102 == 5 && CFLAG:TARGET:110 == 0
		;出産系のフラグ設定
		;犬の仔なら妊娠期間短い
		CFLAG:TARGET:110 = DAY + 10 + RAND:6
		CFLAG:TARGET:111 = -2
	ENDIF
ENDIF


@CONCEPTION_CHECK_EXTRA
;娼館やレンタル時の客の精液で妊娠確定時の処理
REPEAT CHARANUM
	IF TALENT:COUNT:153 == 0 && CFLAG:COUNT:102 == 4 && CFLAG:COUNT:110 == 0
		;出産系のフラグ設定
		CFLAG:COUNT:110 = DAY + 10 + RAND:6
		CFLAG:COUNT:111 = -1
	ENDIF
REND

@CONCEPTION_CHECK_SYOKU_TO_T
;モンスター-触手の精液で奴隷妊娠確定時の処理
;奴隷がいるか
IF TARGET >= 1
	IF TALENT:TARGET:153 == 0 && CFLAG:TARGET:102 == 6 && CFLAG:TARGET:110 == 0
		;出産系のフラグ設定
		;苗床なので妊娠期間短い
		CFLAG:TARGET:110 = DAY + 10 + RAND:6
		CFLAG:TARGET:111 = -3
	ENDIF
ENDIF


@CONCEPTION_CHECK_SYOKU_TO_M
;モンスター-触手の精液で主人妊娠確定時の処理
IF MASTER == 0
	IF TALENT:MASTER:153 == 0 && CFLAG:MASTER:102 == 6 && CFLAG:MASTER:110 == 0
		;出産系のフラグ設定
		;苗床なので妊娠期間短い
		CFLAG:MASTER:110 = DAY + 10 + RAND:6
		CFLAG:MASTER:111 = -3
	ENDIF
ENDIF

@CONCEPTION_CHECK_KYOUOU_TO_T
;狂王の精液で奴隷妊娠確定時の処理
REPEAT CHARANUM
	IF TALENT:COUNT:153 == 0 && CFLAG:COUNT:102 == 7 && CFLAG:COUNT:110 == 0
		;出産系のフラグ設定
		;促成栄養剤を投与されてるので妊娠期間短い
		CFLAG:COUNT:110 = DAY + 10 + RAND:6
		CFLAG:COUNT:111 = -4
	ENDIF
REND

@CONCEPTION_CHECK_NTRD_TO_T
;獣姦ショーの精液で奴隷妊娠確定時の処理
REPEAT CHARANUM
	IF TALENT:COUNT:153 == 0 && CFLAG:COUNT:102 == 5 && CFLAG:COUNT:110 == 0
		;出産系のフラグ設定
		;促成栄養剤を投与されてるので妊娠期間短い
		CFLAG:COUNT:110 = DAY + 10 + RAND:6
		CFLAG:COUNT:111 = -2
	ENDIF
REND

;---------------------------------------------------------
;妊娠処理関数郡
;---------------------------------------------------------

@GET_CHILD
;既に妊娠中-育児中ならそのまま戻る
SIF TALENT:COUNT:153 || TALENT:COUNT:154
	RETURN 0

;妊娠出産機能オフ時
SIF GETBIT(FLAG:5,2) == 0
	RETURN 0

;乳内妊娠で異常妊娠体質でない
SIF CFLAG:COUNT:113 == 1 && TALENT:COUNT:340 == 0
	RETURN 0
;精巣妊娠で異常妊娠体質でない
SIF CFLAG:COUNT:113 == 2 && TALENT:COUNT:340 == 0
	RETURN 0
;精巣妊娠でオトコでもふたなりでもない
SIF CFLAG:COUNT:113 == 2 && TALENT:COUNT:121 == 0 && TALENT:COUNT:122 == 0
	RETURN 0

;マスターの精液で妊娠発覚時の処理
IF  CFLAG:COUNT:102 == 1 && CFLAG:COUNT:110 <= DAY+54
	PRINTFORML Something appears to be wrong with %SAVESTR:COUNT%...
	PRINTFORM %SAVESTR:COUNT%は%CALLNAME:MASTER%の子供を
	CALL PREG_TALENT_GET
	CALL CHANGE_N_STATUS, COUNT
;助手の精液で妊娠発覚時の処理
ELSEIF CFLAG:COUNT:102 == 2 && CFLAG:COUNT:110 <= DAY+54
	PRINTFORML Something appears to be wrong with %SAVESTR:COUNT%...
	PRINTFORM %SAVESTR:COUNT%は%CSTR:COUNT:2%の子供を
	CALL PREG_TALENT_GET
	CALL CHANGE_N_STATUS, COUNT
;奴隷の精液で妊娠発覚時の処理
ELSEIF CFLAG:COUNT:102 == 3 && CFLAG:COUNT:110 <= DAY+54
	PRINTFORML Something appears to be wrong with %SAVESTR:COUNT%...
	PRINTFORM %SAVESTR:COUNT%は%CSTR:COUNT:2%の子供を
	CALL PREG_TALENT_GET
	CALL CHANGE_N_STATUS, COUNT
;客の精液で妊娠発覚時の処理
ELSEIF CFLAG:COUNT:102 == 4 && CFLAG:COUNT:110 <= DAY+54
	PRINTFORML Something appears to be wrong with %SAVESTR:COUNT%...
	PRINTFORM %SAVESTR:COUNT%は名も知らぬ男の子供を
	CALL PREG_TALENT_GET
	CALL CHANGE_N_STATUS, COUNT
;犬の精液で妊娠発覚時の処理
ELSEIF CFLAG:COUNT:102 == 5 && CFLAG:COUNT:110 <= DAY+24
	PRINTFORML Something appears to be wrong with %SAVESTR:COUNT%...
	PRINTFORML %SAVESTR:COUNT%は野良犬の仔を
	CALL PREG_TALENT_GET
	PRINTFORML 異種妊娠経験+1
	EXP:COUNT:62 += 1
	WAIT
	CALL CHANGE_N_STATUS, COUNT
;モンスターの精液で妊娠発覚時の処理
ELSEIF CFLAG:COUNT:102 == 6 && CFLAG:COUNT:110 <= DAY+24
	PRINTFORML Something appears to be wrong with %SAVESTR:COUNT%...
	PRINTFORM %SAVESTR:COUNT%は
	
	IF CFLAG:COUNT:112 != 0 && RAND:3 == 0
		PRINTFORM %ITEMNAME:(CFLAG:COUNT:112)%
	ELSE
		PRINT モンスタ-
		CFLAG:COUNT:112 = 0
	ENDIF
	
	PRINTFORM の子供を
	CALL PREG_TALENT_GET
	PRINTFORML 異種妊娠経験+1
	EXP:COUNT:62 += 1
	WAIT
	CALL CHANGE_N_STATUS, COUNT
;狂王の精液で妊娠発覚時の処理
ELSEIF CFLAG:COUNT:102 == 7 && CFLAG:COUNT:110 <= DAY+24
	PRINTFORML Something appears to be wrong with %SAVESTR:COUNT%...
	PRINTFORM %SAVESTR:COUNT%は狂王の子供を
	CALL PREG_TALENT_GET
	CALL CHANGE_N_STATUS, COUNT
ENDIF

@PREG_TALENT_GET
;妊娠素質を得る
;ここでCFLAG:113を初期化する
IF CFLAG:COUNT:113 == 1
	PRINTL 乳房に孕んだようだ
ELSEIF CFLAG:COUNT:113 == 2
	PRINTL 精巣に孕んだようだ
ELSE	
	PRINTL 身篭った様だ
ENDIF
PRINTL 
PRINTFORML %SAVESTR:COUNT%は[%TALENTNAME:153%]した
IF CFLAG:COUNT:113 == 1
	PRINTFORML %SAVESTR:COUNT%は[%TALENTNAME:341%]した
	TALENT:COUNT:341 = 1
ELSEIF CFLAG:COUNT:113 == 2
	PRINTFORML %SAVESTR:COUNT%は[%TALENTNAME:342%]した
	TALENT:COUNT:342 = 1
ENDIF
WAIT
TALENT:COUNT:153 = 1
CFLAG:COUNT:113 = 0
RETURN 1

@CHANGE_N_STATUS, ARG:0
;妊娠時のステータスの変化
;共通処理
;体力上限を減らす
MAXBASE:(ARG:0):0 -= 500
SIF BASE:(ARG:0):0 > MAXBASE:(ARG:0):0
	BASE:(ARG:0):0 = MAXBASE:(ARG:0):0

;乳がワンランクパワーアップする
CALL GROW_B_SIZE, ARG:0

;母乳出るようになる
IF TALENT:(ARG:0):130 == 0
	PRINTFORML %SAVESTR:(ARG:0)%は母乳が出るようになった
	WAIT
	TALENT:(ARG:0):130 = 1
ENDIF

;父親が誰なのかと母親の状態によるストレス値の分岐
LOCAL = 0
LOCAL:2 = CFLAG:(ARG:0):111

;父親が主人で母親が[愛]持ち
IF TALENT:(ARG:0):85 && CFLAG:(ARG:0):102 == 1
	LOCAL += 0
;父親が主人で母親が[淫乱]持ち
ELSEIF TALENT:(ARG:0):76 && CFLAG:(ARG:0):102 == 1
	LOCAL += 30
ENDIF

;父親が助手-奴隷で母親が[愛]持ち
IF TALENT:(ARG:0):85 && (CFLAG:(ARG:0):102 == 2 || CFLAG:(ARG:0):102 == 3)
	IF RELATION:(ARG:0):(LOCAL:2) != 0
		LOCAL += 10 * ((200-RELATION:(ARG:0):(LOCAL:2)) / 100) + 10
	ELSE
		LOCAL += 20
	ENDIF
;父親が助手-奴隷で母親が[淫乱]持ち
ELSEIF TALENT:(ARG:0):76 && (CFLAG:(ARG:0):102 == 2 || CFLAG:(ARG:0):102 == 3)
	IF RELATION:(ARG:0):(LOCAL:2) != 0
		LOCAL += 10 * ((200-RELATION:(ARG:0):(LOCAL:2)) / 100)
	ELSE
		LOCAL += 10
	ENDIF
ENDIF

;父親が顧客で母親が[愛]持ち
IF TALENT:(ARG:0):85 && CFLAG:(ARG:0):102 == 4
	LOCAL += 80
;父親が顧客で母親が[淫乱]持ち
ELSEIF TALENT:(ARG:0):76 && CFLAG:(ARG:0):102 == 4
	LOCAL += 50
ENDIF

;父親がノラ犬で母親が[愛]持ち
IF TALENT:(ARG:0):85 && CFLAG:(ARG:0):102 == 5
	LOCAL += 100
	;[牝犬]が付いてればかなり減る
	SIF TALENT:(ARG:0):136
		LOCAL -= 40
	;野良犬が旦那様ならかなり減る
	SIF CFLAG:(ARG:0):601 == 900
		LOCAL -= 40
;父親がノラ犬で母親が[淫乱]持ち
ELSEIF TALENT:(ARG:0):76 && CFLAG:(ARG:0):102 == 5
	LOCAL += 80
	;[牝犬]が付いてればかなり減る
	SIF TALENT:(ARG:0):136
		LOCAL -= 40
	;野良犬が旦那様ならかなり減る
	SIF CFLAG:(ARG:0):601 == 900
		LOCAL -= 40
;それ以外で父親がノラ犬."陥落素質なし"とあわせて+90
ELSEIF CFLAG:(ARG:0):102 == 5
	LOCAL += 40
	;[牝犬]が付いてればかなり減る
	SIF TALENT:(ARG:0):136
		LOCAL -= 40
	;野良犬が旦那様ならかなり減る
	SIF CFLAG:(ARG:0):601 == 900
		LOCAL -= 40
ENDIF

;父親がモンスターで母親が[愛]持ち
IF TALENT:(ARG:0):85 && CFLAG:(ARG:0):102 == 6
	LOCAL += 100
	;母親が魔族ならストレスがかなり減る
	SIF TALENT:(ARG:0):314 == 9
		LOCAL -= 40
	;モンスターが旦那様ならストレスがかなり減る
	SIF CFLAG:(ARG:0):601 >= 1 && CFLAG:(ARG:0):601 <= 12
		LOCAL -= 40
;父親がモンスターで母親が[淫乱]持ち
ELSEIF TALENT:(ARG:0):76 && CFLAG:(ARG:0):102 == 6
	LOCAL += 80
	;母親が魔族ならストレスがかなり減る
	SIF TALENT:(ARG:0):314 == 9
		LOCAL -= 40
	;モンスターが旦那様ならストレスがかなり減る
	SIF CFLAG:(ARG:0):601 >= 1 && CFLAG:(ARG:0):601 <= 12
		LOCAL -= 40
;それ以外で父親がモンスター."陥落素質なし"とあわせて+90
ELSEIF CFLAG:(ARG:0):102 == 6
	LOCAL += 40
	;母親が魔族ならストレスがかなり減る
	SIF TALENT:(ARG:0):314 == 9
		LOCAL -= 40
	;モンスターが旦那様ならストレスがかなり減る
	SIF CFLAG:(ARG:0):601 >= 1 && CFLAG:(ARG:0):601 <= 12
		LOCAL -= 40
ENDIF

;父親が狂王で母親が[愛]持ち
IF TALENT:(ARG:0):85 && CFLAG:(ARG:0):102 == 7
	LOCAL += 80
;父親が狂王で母親が[淫乱]持ち
ELSEIF TALENT:(ARG:0):76 && CFLAG:(ARG:0):102 == 7
	LOCAL += 30
ENDIF

;陥落素質がついていない
SIF TALENT:(ARG:0):85 == 0 && TALENT:(ARG:0):76 == 0
	LOCAL += 50
;未陥落時は出産経験数でストレスが増える.
IF EXP:(ARG:0):60 > 0 && TALENT:(ARG:0):85 == 0 && TALENT:(ARG:0):76 == 0
	LOCAL += EXP:(ARG:0):60 * 5
ENDIF

;気丈
SIF TALENT:(ARG:0):12
	LOCAL -= 20
;母性
SIF TALENT:(ARG:0):155
	LOCAL -= 40
;心弱い
SIF TALENT:(ARG:0):134
	LOCAL += 20
;出産経験あり
SIF EXP:(ARG:0):60
	LOCAL -= 20

;ストレス値の累積が100を超えた場合
;妊娠したのがあなたなら崩壊しない
IF LOCAL < 100 || ARG:0 == 0
	PRINTFORML %SAVESTR:(ARG:0)%は嬉しそうにお腹を撫でている.........
ELSEIF LOCAL >= 100 && TALENT:(ARG:0):9 == 0
	PRINTFORML %SAVESTR:(ARG:0)%は呆然とした面持ちをしている
	PRINTFORML %SAVESTR:(ARG:0)%の中で何かが壊れてしまったようだ......
	PRINTFORML %SAVESTR:(ARG:0)%の精神は[%TALENTNAME:9%]してしまった
	WAIT
	IF TALENT:(ARG:0):85
		PRINTFORML %SAVESTR:(ARG:0)%は[%TALENTNAME:85%]を失った
		TALENT:(ARG:0):85 = 0
	ENDIF
	IF TALENT:(ARG:0):76
		PRINTFORML %SAVESTR:(ARG:0)%は[%TALENTNAME:76%]を失った
		TALENT:(ARG:0):76 = 0
	ENDIF
	TALENT:(ARG:0):9 = 1
	WAIT
ENDIF
;妊娠発覚時のイベント口上
TARGET = ARG:0
TFLAG:13 = 11
CALL SELF_KOJO

DRAWLINE

;臨月到達
@REACH_FULL_TERM(ARG)
PRINTFORML %SAVESTR:ARG%は臨月を迎えました
IF CFLAG:ARG:1 == 9
	PRINTFORML %SAVESTR:ARG%は出産に備え,狂王の城の育児室へ移動します.
	WAIT
ELSE
	SIF CFLAG:ARG:1 != 3 && CFLAG:ARG:1 != 7 && CFLAG:ARG:1 != 8
		PRINTFORMW %SAVESTR:ARG%は出産に備え育児室へ移動します
	WAIT
	;育児室へ
	;臨月時調教可,育児室には行かない.
	IF GETBIT(FLAG:5,10)
		SIF FLAG:1 == ARG
			FLAG:1 = -1
		SIF FLAG:2 == ARG
			FLAG:2 = -1
	;臨月到達したので調教-助手不可
	;調教中と迎撃中なら育児室へ.
	ELSE
		IF CFLAG:ARG:1 == 3
			PRINTFORMW ダンジョンにいた%SAVESTR:ARG%は出産に備え育児室へ帰還させました
			CFLAG:ARG:507 = 0
		ENDIF
		SIF CFLAG:ARG:1 == 0 || CFLAG:ARG:1 == 3
			CFLAG:ARG:1 = 10
		SIF FLAG:1 == ARG
			FLAG:1 = -1
		SIF FLAG:2 == ARG
			FLAG:2 = -1
	ENDIF
ENDIF

DRAWLINE

;出産
@CHILD_BIRTH(ARG)
;ARGはキャラ登録番号
LOCAL = CFLAG:ARG:111

EXP:ARG:52 += 1
EXP:ARG:60 += 1
SIF CFLAG:ARG:1 == 3
	PRINTFORML %SAVESTR:ARG%はダンジョン内で激しい陣痛に襲われると,通路の隅っこの方にしゃがみこんだ.........
IF CFLAG:ARG:111 == 0 && LOCAL == 0
	PRINTFORM %SAVESTR:ARG%は,%SAVESTR:LOCAL%の子供を
	CALL CHILD_BIRTH_PLACE,ARG
	PRINTL 無事出産しました
ELSEIF CFLAG:ARG:111 >= 0 && LOCAL >= 0
	PRINTFORM %SAVESTR:ARG%は,%CSTR:ARG:2%の子供を
	CALL CHILD_BIRTH_PLACE,ARG
	PRINTL 無事出産しました
ELSEIF CFLAG:ARG:111 == -1
	PRINTFORM %SAVESTR:ARG%は,父親のわからない子供を
	CALL CHILD_BIRTH_PLACE,ARG
	PRINTL 無事出産しました
ELSEIF CFLAG:ARG:111 == -2
	PRINTFORM %SAVESTR:ARG%は,野良犬の子供を
	CALL CHILD_BIRTH_PLACE,ARG
	PRINTL 産み落としました
ELSEIF CFLAG:ARG:111 == -3
	PRINTFORM %SAVESTR:ARG%は,モンスタ-の子供を
	CALL CHILD_BIRTH_PLACE,ARG
	PRINTL 産み落としました
ELSEIF CFLAG:ARG:111 == -4
	PRINTFORM %SAVESTR:ARG%は,狂王の子供を
	CALL CHILD_BIRTH_PLACE,ARG
	PRINTL 産み落としました
ELSE
	PRINTFORM %SAVESTR:ARG%は,父親のいない子供を
	CALL CHILD_BIRTH_PLACE,ARG
	PRINTL 無事出産しました
ENDIF
WAIT

;出産時のイベント口上
;NTR時
IF CFLAG:ARG:1 == 9
	TARGET = ARG
	TFLAG:13 = 12
	CALL NTR_CHILD_BIRTH
;通常時
ELSE
	TARGET = ARG
	TFLAG:13 = 12
	CALL SELF_KOJO

	;[愛]持ちの奴隷に産ませた子供が3人以上でマスターに[父性]が付く
	SIF TALENT:ARG:85 && CFLAG:ARG:111 == NO:MASTER
		FLAG:32 += 1
	IF FLAG:32 >= 3 && TALENT:MASTER:156 == 0 && TALENT:MASTER:122
		PRINTFORML %CALLNAME:MASTER%は[%TALENTNAME:156%]に目覚めた
		WAIT
		TALENT:MASTER:156 = 1
	ENDIF
ENDIF

;妊娠を失う
TALENT:ARG:153 = 0
;再生処女は失われる
;乳内-精巣妊娠はその限りではない
IF TALENT:ARG:0 == 1 && TALENT:ARG:341 == 0 && TALENT:ARG:342 == 0
	PRINTFORML %SAVESTR:ARG%の再生処女膜は出産によって失われた.........
	TALENT:ARG:0 = 0
ENDIF
;乳内妊娠-精巣妊娠も失う
TALENT:ARG:341 = 0
TALENT:ARG:342 = 0

;調教中か育児室にいるときは育児
IF CFLAG:ARG:1 == 0 || CFLAG:ARG:1 == 10
	CALL CHILD_CARE(ARG)
;NTR時,迎撃時,苗床,晒し台なら育児しない
ELSE
	;NTR時はどんな子供でも処分,迎撃時,苗床,晒し台はその場で産んで即戦力
	;母親があなたなら
	IF ARG == 0
		CALL PREGNANCY_MASTER(ARG)
	ELSE
		CALL SUMMON_MONSTER(ARG)
	ENDIF
	;妊娠時に大きくなった胸のサイズを一段小さくする
	;ただし超乳は元に戻らないものとする.いいね?
	IF TALENT:ARG:119
		PRINTFORML %SAVESTR:ARG%の超乳はだらしなく膨らんだまま元に戻ることはなかった
	ELSE
		CALL REVERSE_B_SIZE(ARG)
		PRINTFORML %SAVESTR:ARG%の胸は母乳を与える役割を放棄したため小さくなりました
	ENDIF
	;母乳出なくなる
	IF TALENT:ARG:130 == 1
		TALENT:ARG:130 = 0
		PRINTFORML %SAVESTR:ARG%は母乳が出なくなった
	ENDIF

	;育児しないと母性に目覚めません
	;妊娠時に減った体力の最大値が回復,妊娠フラグリセット
	MAXBASE:ARG:0 += 500
	CALL CLEAR_FLAG(ARG)
ENDIF

DRAWLINE
RETURN 0

;出産場所
@CHILD_BIRTH_PLACE(ARG)
;ARGはキャラ登録番号
IF TALENT:ARG:341
	PRINT 巨大な乳房から
ELSEIF TALENT:ARG:342
	PRINT 巨大な陰嚢からペニスを通して
ENDIF
RETURN 0

;妊娠パッチまわりのフラグをリセット
@CLEAR_FLAG(ARG)
SIF CFLAG:ARG:1 == 10
	CFLAG:ARG:1 = 0

FOR LOCAL, 101, 109
	CFLAG:ARG:LOCAL = 0
NEXT
CFLAG:ARG:110 = 0
CFLAG:ARG:111 = 0
CSTR:ARG:2 = 

;妊娠or子育て開始時の胸サイズの膨張処理
@GROW_B_SIZE, ARG:0
;乳がワンランクパワーアップする
;絶→貧　貧→普　普→巨　巨→爆　爆→超　超→そのまま
IF TALENT:(ARG:0):116 == 1
	PRINTFORML %SAVESTR:(ARG:0)%の胸が[%TALENTNAME:116%]から[%TALENTNAME:109%]ぐらいに大きくなった
	TALENT:(ARG:0):116 = 0
	TALENT:(ARG:0):109 = 1
ELSEIF TALENT:(ARG:0):109 == 1
	PRINTFORML %SAVESTR:(ARG:0)%の胸が[%TALENTNAME:109%]から普通程度に大きくなった
	TALENT:(ARG:0):109 = 0
ELSEIF TALENT:(ARG:0):110 == 0 && TALENT:(ARG:0):114 == 0 && TALENT:(ARG:0):119 == 0
	PRINTFORML %SAVESTR:(ARG:0)%の胸が普通程度から[%TALENTNAME:110%]サイズに大きくなった
	TALENT:(ARG:0):110 = 1
ELSEIF TALENT:(ARG:0):110 == 1
	PRINTFORML %SAVESTR:(ARG:0)%の胸が[%TALENTNAME:110%]から[%TALENTNAME:114%]サイズになった
	TALENT:(ARG:0):110 = 0
	TALENT:(ARG:0):114 = 1
ELSEIF TALENT:(ARG:0):114 == 1
	PRINTFORML %SAVESTR:(ARG:0)%の胸が[%TALENTNAME:114%]から[%TALENTNAME:119%]サイズになった
	TALENT:(ARG:0):114 = 0
	TALENT:(ARG:0):119 = 1
ENDIF
;対象があなたでなく,コンフィグ設定してあるならバストサイズ更新
IF ARG:0 != 0
	IF GETBIT(FLAG:5,12) || GETBIT(FLAG:5,15)
		CALL CHAR_SIZE_GENERATE, ARG:0, CFLAG:(ARG:0):451, 1
		CFLAG:(ARG:0):454 = RESULT:0
		CFLAG:(ARG:0):455 = RESULT:1
	ENDIF
ENDIF

;子育て終了or妊娠強制終了時の胸サイズの縮小処理
@REVERSE_B_SIZE(ARG)

;爆→巨　巨→普　普→貧　貧-絶→そのまま
IF TALENT:ARG:114
	TALENT:ARG:114 = 0
	TALENT:ARG:110 = 1
ELSEIF TALENT:ARG:110
	TALENT:ARG:110 = 0
ELSEIF TALENT:ARG:109 == 0 && TALENT:ARG:116 == 0  && TALENT:ARG:119 == 0
	TALENT:ARG:109 = 1
ENDIF
;対象があなたでなく,コンフィグ設定してあるならバストサイズ更新
IF ARG != 0
	IF GETBIT(FLAG:5,12) || GETBIT(FLAG:5,15)
		CALL CHAR_SIZE_GENERATE, ARG, CFLAG:ARG:451, 1
		CFLAG:ARG:454 = RESULT:0
		CFLAG:ARG:455 = RESULT:1
	ENDIF
ENDIF

;育児関係
;ARGはキャラ番号
;CFLAG:1 = キャラの状態　0=調教中　1=待機　2=侵攻中　3=迎撃中　4=死亡　5=迎撃成功　6=迎撃失敗 7=苗床 8=晒し台 9=NTR中 10=育児室
;NTR中= 9と迎撃中= 3 苗床= 7 晒し台= 8 での出産は育児無し
;調教可能= 0 なら育児室へ= 10
@CHILD_CARE(ARG)
;崩壊して育児放棄をした場合
IF TALENT:ARG:9
	;育児放棄の際の赤子の処置
	LOCAL = 0
	REPEAT CHARANUM
		SIF TALENT:COUNT:153 == 0 && TALENT:COUNT:155 && TALENT:COUNT:9 == 0 && CFLAG:COUNT:0 == 2
			LOCAL += 1
	REND
	IF LOCAL > 0
		PRINTFORML 産まれた赤ん坊を他の奴隷に任せるか,里子に出すかを選択してください
		WAIT
		PRINTL 誰に任せますか?
		PRINTL ..............................................................................
		REPEAT CHARANUM
			;現在[妊娠]状態でも[育児中]でもなく,[母性]持ちで崩壊してなく,調教可能で,かつ助手可能なキャラを羅列
			IF TALENT:COUNT:153 == 0 && TALENT:COUNT:154 == 0 && TALENT:COUNT:155 && TALENT:COUNT:9 == 0 && CFLAG:COUNT:0 == 2 && CFLAG:COUNT:1 == 0
				PRINTFORML  [{COUNT}] %SAVESTR:COUNT% 
			ENDIF
		REND
		PRINTL ..............................................................................
		PRINTFORML  [100] - 里子に出す

		DO
			LOCAL = 0
			INPUT
			IF RESULT == 100
				PRINTFORM 崩壊した%SAVESTR:ARG%に育児は出来ないと判断した
				PRINTFORML %CALLNAME:MASTER%はやむなく,赤ん坊を里子に出しました
				WAIT
			ELSEIF RESULT < 0 || RESULT >= CHARANUM
				LOCAL = 1
			ELSEIF TALENT:RESULT:153 == 1 || RESULT:154 == 1 || TALENT:RESULT:155 == 0 || TALENT:RESULT:9 || CFLAG:RESULT:0 < 2 || CFLAG:RESULT:1 != 0
				LOCAL = 1
			ENDIF
		LOOP LOCAL
		IF RESULT != 100
			PRINTFORML %CALLNAME:MASTER%は%SAVESTR:RESULT%に乳母をやってもらうことにしました
			;育児期間を設定する
			CFLAG:RESULT:110 = DAY
			;育児室へ
			CFLAG:RESULT:1 = 10
			TALENT:RESULT:154 = 1
			SIF RESULT == TARGET
				TARGET = -1
			SIF RESULT == ASSI
				ASSI = -1
			;育児中でない奴隷なら乳がワンランクパワーアップする
			LOCAL:2 = RESULT
			PRINTFORM %SAVESTR:ARG%の子供に乳を与えるために
			CALL GROW_B_SIZE, LOCAL:2
			;母乳出るようになる
			IF TALENT:(LOCAL:2):130 == 0
				PRINTFORML %SAVESTR:(LOCAL:2)%は母乳が出るようになった
				TALENT:(LOCAL:2):130 = 1
			ENDIF
			WAIT
		ENDIF
	ELSE
		PRINTFORM 崩壊した%SAVESTR:ARG%に育児は出来ないと判断した
		PRINTFORML %CALLNAME:MASTER%はやむなく,赤ん坊を里子に出しました
		WAIT
	ENDIF
	;妊娠時に減った体力の最大値が回復
	MAXBASE:ARG:0 += 500
	CALL CLEAR_FLAG(ARG)
	;妊娠時に大きくなった胸のサイズを一段小さくする
	CALL REVERSE_B_SIZE(ARG)
	PRINTFORML %SAVESTR:ARG%の胸は母乳を与える役割を放棄したため小さくなりました
	;母乳出なくなる
	IF TALENT:ARG:130 == 1
		TALENT:ARG:130 = 0
		PRINTFORML %SAVESTR:ARG%は母乳が出なくなった
	ENDIF
;育児に入った場合
ELSE
	IF ARG == 0
		PRINTFORML あなたは育児を始めた
	ELSE
		PRINTFORML %SAVESTR:ARG%は育児室で育児を始めた
	ENDIF
	IF CFLAG:ARG:1 == 0
		CFLAG:ARG:1 = 10
		SIF FLAG:1 == ARG
			FLAG:1 = -1
		SIF FLAG:2 == ARG
			FLAG:2 = -1
	ENDIF
	;育児中
	TALENT:ARG:154 = 1
	;[愛]持ちで父親が主人なら[母性]に目覚める
	IF TALENT:ARG:85 && CFLAG:ARG:111 == NO:MASTER && TALENT:ARG:155 == 0
		PRINTFORML %SAVESTR:ARG%はあなたとの間に産まれた子を優しくあやしている.........
		PRINTFORML どうやら%SAVESTR:ARG%は[%TALENTNAME:155%]に目覚めたようだ
		TALENT:ARG:155 = 1
	ENDIF
	WAIT
ENDIF

DRAWLINE

;親離れ
;勇者の子はモンスターとして産まれないので配下には加わりません.
@DEPEARENT(ARG)
PRINTFORML %SAVESTR:ARG%の育てていた子供が親離れしました
PRINTFORML %SAVESTR:ARG%は再び調教可能になりました
DRAWLINE
WAIT
;親離れ口上
TARGET = ARG
TFLAG:13 = 14
CALL SELF_KOJO

;maouバージョン改変
;母親があなたなら
IF ARG == 0
	CALL PREGNANCY_MASTER(ARG)
ELSE
	CALL SUMMON_MONSTER(ARG)
ENDIF

;妊娠時に減った体力の最大値が回復
MAXBASE:ARG:0 += 500
CALL CLEAR_FLAG(ARG)
;育児中解除
TALENT:ARG:154 = 0
;妊娠時に大きくなった胸のサイズを一段小さくする
CALL REVERSE_B_SIZE(ARG)
PRINTFORML %SAVESTR:ARG%の胸は母乳を与える役割を放棄したため小さくなりました
;母乳出なくなる
IF TALENT:ARG:130 == 1
	TALENT:ARG:130 = 0
	PRINTFORML %SAVESTR:ARG%は母乳が出なくなった
ENDIF

DRAWLINE


;育児室の訪問関係
;-----------------------------------------------
@SHOW_BUTTON_CHILD_CARE(NUM, ARG)
#DIM NUM
;-----------------------------------------------
;キャラの能力表示において[育児室の訪問]ボタンを表示する
;引数NUMはボタンの数値,ARGは対象キャラの番号

LOCAL = CHECK_ABLE_TO_CHILD_CARE(ARG)
IF LOCAL == 2
	; 侵攻中の勇者ならボタン自体を表示しない
	RETURN 0
ELSEIF LOCAL != 0
	; 育児室にいないならボタン出さない
	RETURN 0
	; 奴隷で実行不可なら灰色にする
	SETCOLOR 0x646464
ENDIF
PRINTFORM [{NUM}] 育児室の訪問　
RESETCOLOR
RETURN 0

;-----------------------------------------------
@CHECK_ABLE_TO_CHILD_CARE(ARG)
#FUNCTION
;-----------------------------------------------
;ARG番のキャラに対して,育児室にいるかの判断を行い,結果に対応する値を返す式中関数
;可なら0を返す

IF ARG == 0
	; あなたは育児室にいない
	RETURNF 1
ELSEIF CFLAG:ARG:1 == 2
	; 侵攻中の勇者だ
	RETURNF 2
ELSEIF CFLAG:ARG:1 != 10
	; そのキャラは育児室にいない
	RETURNF 3
ENDIF
RETURNF 0

;-----------------------------------------------
@CHILD_CARE_CHARA(ARG)
;-----------------------------------------------
;ARG番のキャラの育児室訪問
;先に育児室に行けるかチェックして,ダメなら値に対応する処理をしてリターン0
LOCAL = CHECK_ABLE_TO_CHILD_CARE(ARG)
IF LOCAL != 0
	IF LOCAL == 1
		PRINTW あなたは育児室にいない
	ELSEIF LOCAL == 2
		;侵攻中の勇者ではボタンが表示されないが,それでも入力すればここに来る.
		RETURN 2
	ELSEIF LOCAL == 3
		PRINTW そのキャラは育児室にいない
	ENDIF
	RETURN 0
ENDIF

PRINTFORML あなたは%SAVESTR:ARG%の育児室を訪問しました.
PRINTL
TARGET = ARG
TFLAG:13 = 13
CALL SELF_KOJO
PRINT

