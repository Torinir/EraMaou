;------------------------------------------------------------
;通信機能
;------------------------------------------------------------
@MAOUNET
#DIM LINE, 1
LINE = LINECOUNT
$START
CLEARLINE LINECOUNT - LINE
PRINTL [0] Send Slaves To Another Dungeon (Save File)
PRINTL [1] Bring Heroes From Another Dungeon (Save File)
PRINTL [2] Remove The Heroes That Came From Other Dungeons
PRINTFORML %"[3] Set The Maximum Starting Level For Arriving Heroes", 55, LEFT% Currently: Lv {FLAG:76}
PRINTFORML %"[4] All Heroes Start At Level 1", 55, LEFT% Currently: \@FLAG:77? ON # OFF \@
PRINTL [9] Return
INPUT
IF RESULT == 0
	CALL EXPORT
ELSEIF RESULT == 1
	CALL INPORT_A
ELSEIF RESULT == 2
	VARSET GLOBALS, "", 0, 100
	SAVEGLOBAL
	PRINTW 登録勇者情報を削除しました
ELSEIF RESULT == 3
	PRINTFORML やってくる勇者のレベルの上限を入力してください
	PRINTFORML なお,0以下の数値を入力することにより勇者の出現を無くせます
	INPUT
	FLAG:76 = RESULT
ELSEIF RESULT == 4
	INVERTBIT FLAG:77, 0
ELSEIF RESULT == 9
	RETURN 0
ENDIF
GOTO START

;------------------------------------------------------------
;出力キャラの決定関数
;------------------------------------------------------------
@EXPORT
#DIM CHOICE, 5
#DIM LINE, 2
VARSET CHOICE, -1
LOCAL:1 = 0
LINE = LINECOUNT
$START
CLEARLINE LINECOUNT - LINE
FOR LOCAL, 0, CHARANUM
	;主人は除外
	SIF LOCAL == MASTER
		CONTINUE
	;敵は除外
	SIF CFLAG:LOCAL:1
		CONTINUE
	RESETCOLOR
	;選択済みのキャラは水色
	SIF MATCH(CHOICE, LOCAL)
		SETCOLOR 0x66FFFF
 	PRINTFORM [{LOCAL}] %SAVESTR:LOCAL% %GET_JOB_NAME(LOCAL),8,LEFT% 
	PRINTFORML {CFLAG:LOCAL:9}LV　HP {BASE:LOCAL:0}／{MAXBASE:LOCAL:0} 調教回数 {CFLAG:LOCAL:10}
NEXT
RESETCOLOR
DRAWLINE
PRINTFORML よその大陸へ送り出す奴隷を選んでください({5-MATCH(CHOICE,-1)}/5)
DRAWLINE
SIF MATCH(CHOICE,-1) == 5
	SETCOLOR 0x404040
PRINTFORML [ 99] 決定
RESETCOLOR
PRINTFORML [100] キャンセル
$INPUT_LOOP1
INPUT
IF RESULT == 100
	RETURN 0
ELSEIF RESULT == 99
	SIF MATCH(CHOICE,-1) == 5
		GOTO INPUT_LOOP1
	FOR LOCAL, 0, 5
		SIF CHOICE:LOCAL == -1
			CONTINUE
		PRINTFORML %SAVESTR:(CHOICE:LOCAL)%
	NEXT
	PRINTFORML 以上の{5-MATCH(CHOICE,-1)}人でよろしいですか?
	PRINTL [0] はい
	PRINTL [1] いいえ
	$INPUT_LOOP2
	INPUT
	IF RESULT == 0
		SAVEDATA 999, "退避データ"
		FOR LOCAL, 0, CHARANUM
			IF MATCH(CHOICE, LOCAL)
				NICKNAME:LOCAL = %SAVESTR:LOCAL%
				CFLAG:LOCAL:190 = GETMILLISECOND()+LOCAL
			ELSE
				NICKNAME:LOCAL = 削除
			ENDIF
		NEXT
		FOR LOCAL, CHARANUM-1, -1, -1
			SIF NICKNAME:LOCAL == "削除"
				DELCHARA LOCAL
		NEXT
		PRINTFORML PT名を入力してください
		LINE:1 = LINECOUNT
		$INPUT_LOOP3
		INPUTS
		PRINTFORML "%RESULTS%"でよろしいですか?
		PRINTL [0] はい
		PRINTL [1] いいえ
		INPUT
		IF RESULT == 0
			LOCALS = %RESULTS% 
			GETTIME
			LOCALS = %RESULTS% %LOCALS%
			SAVEDATA 1000, LOCALS
			PRINTFORMW SAVE1000にPTを作りました!
			LOADDATA 999
		ELSEIF RESULT == 1
			GOTO INPUT_LOOP3
		ELSE
			CLEARLINE 1
			GOTO INPUT_LOOP2
		ENDIF
		LOADDATA 1020
	ELSEIF RESULT == 1
		GOTO START
	ELSE
		CLEARLINE 1
		GOTO INPUT_LOOP2
	ENDIF
ELSE
	IF RESULT != MASTER && RESULT < CHARANUM && RESULT >= 0 && !CFLAG:RESULT:1
		IF MATCH(CHOICE, RESULT)
			FOR LOCAL, 0, 5
				SIF CHOICE:LOCAL == RESULT
					CHOICE:LOCAL = -1
			NEXT
		ELSE
			IF !MATCH(CHOICE,-1)
				PRINTFORMW 一度におくれるのは5人までです!
				GOTO START
			ELSE
				FOR LOCAL, 0, 5
					IF CHOICE:LOCAL == -1
						CHOICE:LOCAL = RESULT
						BREAK
					ENDIF
				NEXT
			ENDIF
		ENDIF
		GOTO START
	ELSE
		CLEARLINE 1
		GOTO INPUT_LOOP1
	ENDIF
ENDIF
GOTO START

@INPORT_A
#DIM SAVE
DRAWLINE
PRINTL ロ-ドする番号を選んでください
DRAWLINE
FOR LOCAL, 0 , 20
	CHKDATA  LOCAL+1000
	PRINTFORML [{LOCAL ,3}] \@ !RESULT ? %RESULTS% # ---- \@
NEXT
PRINTL [99] キャンセル
$INPUT_LOOP
INPUT
SAVE = RESULT + 1000
IF RESULT == 99
	RETURN 0
ELSEIF RESULT >= 0 && RESULT <= 19
	CHKDATA SAVE
	IF RESULT
		CLEARLINE 1
		GOTO INPUT_LOOP
	ENDIF
	SAVEDATA 999, "作業"
	LOADDATA SAVE
ELSE
	CLEARLINE 1
	REUSELASTLINE 無効な値です
	GOTO INPUT_LOOP
ENDIF
GOTO INPUT_LOOP

;キャラデータをGLOBALに出力する
@INPORT_B
#DIM CHARA
#DIM ADD_NUM
FOR LOCAL, 0, 101
	IF LOCAL == 100
		PRINTW 他所からの勇者がいっぱいでこれ以上追加できません!
		BREAK
	ENDIF
	SIF CHARA >= CHARANUM
		BREAK
	SIF STRLENS(GLOBALS:LOCAL) > 0
		CONTINUE
	FOR LOCAL:1, 0, 100
		SPLIT GLOBALS:(LOCAL:1), "_", RESULTS
		LOCAL:2 = 0
		IF RESULTS == TOSTR(CFLAG:CHARA:190)
			LOCAL--
			CHARA++
			;PRINTW 同一勇者がいます!
			LOCAL:2 = 1
			BREAK
		ENDIF
	NEXT
	SIF LOCAL:2
		CONTINUE
	GLOBALS:LOCAL += @"{CFLAG:CHARA:190}_"
	GLOBALS:LOCAL += @"{NO:CHARA}_"
	GLOBALS:LOCAL += @"{CFLAG:CHARA:9}_"
	GLOBALS:LOCAL += @"%NICKNAME:CHARA%_"
	FOR LOCAL:1, 0, VARSIZE("ABL")
		SIF ABL:CHARA:(LOCAL:1)
			GLOBALS:LOCAL += @"{LOCAL:1},{ABL:CHARA:(LOCAL:1)}/"
	NEXT
	GLOBALS:LOCAL += "_"
	FOR LOCAL:1, 0, VARSIZE("BASE")
		SIF BASE:CHARA:(LOCAL:1)
			GLOBALS:LOCAL += @"{LOCAL:1},{BASE:CHARA:(LOCAL:1)}/"
	NEXT
	GLOBALS:LOCAL += "_"
	FOR LOCAL:1, 0, VARSIZE("BASE")
		SIF MAXBASE:CHARA:(LOCAL:1)
			GLOBALS:LOCAL += @"{LOCAL:1},{MAXBASE:CHARA:(LOCAL:1)}/"
	NEXT
	GLOBALS:LOCAL += "_"
	FOR LOCAL:1, 0, VARSIZE("CFLAG")
		SIF CFLAG:CHARA:(LOCAL:1)
			GLOBALS:LOCAL += @"{LOCAL:1},{CFLAG:CHARA:(LOCAL:1)}/"
	NEXT
	GLOBALS:LOCAL += "_"
	FOR LOCAL:1, 0, VARSIZE("EXP")
		SIF EXP:CHARA:(LOCAL:1)
			GLOBALS:LOCAL += @"{LOCAL:1},{EXP:CHARA:(LOCAL:1)}/"
	NEXT
	GLOBALS:LOCAL += "_"
	FOR LOCAL:1, 0, VARSIZE("EQUIP")
		SIF EQUIP:CHARA:(LOCAL:1)
			GLOBALS:LOCAL += @"{LOCAL:1},{EQUIP:CHARA:(LOCAL:1)}/"
	NEXT
	GLOBALS:LOCAL += "_"
	FOR LOCAL:1, 0, VARSIZE("PALAM")
		SIF JUEL:CHARA:(LOCAL:1)
			GLOBALS:LOCAL += @"{LOCAL:1},{JUEL:CHARA:(LOCAL:1)}/"
	NEXT
	GLOBALS:LOCAL += "_"
	FOR LOCAL:1, 0, VARSIZE("TALENT")
		SIF TALENT:CHARA:(LOCAL:1)
			GLOBALS:LOCAL += @"{LOCAL:1},{TALENT:CHARA:(LOCAL:1)}/"
	NEXT
	GLOBALS:LOCAL += "_"
	FOR LOCAL:1, 0, VARSIZE("MARK")
		SIF MARK:CHARA:(LOCAL:1)
			GLOBALS:LOCAL += @"{LOCAL:1},{MARK:CHARA:(LOCAL:1)}/"
	NEXT
	GLOBALS:LOCAL += "_"
	FOR LOCAL:1, 0, VARSIZE("CSTR")
		SIF STRLENS(CSTR:CHARA:(LOCAL:1))
			GLOBALS:LOCAL += @"{LOCAL:1},%CSTR:CHARA:(LOCAL:1)%/"
	NEXT
	CHARA++
	ADD_NUM++
NEXT
PRINTFORMW \@ADD_NUM ? {ADD_NUM}人の勇者を追加しました#追加できる勇者はいませんでした\@
SAVEGLOBAL
LOADDATA 999

