;=================================================
;BEGIN SHOP後最初に呼び出される関数
;=================================================
@EVENTSHOP

;バグ対策
SIF TARGET > CHARANUM - 1
	TARGET = -1

;バグ対策
SIF ASSI > CHARANUM - 1
	ASSI = -1

;一旦購入可能アイテムを空に
REPEAT 100
	ITEMSALES:COUNT = 0
REND


BOUGHT = -1

@SHOW_SHOP

SAVESTR:0 = あなた
IF BOUGHT >= 0 && BOUGHT < 54
	JUMP ITEM_SHOP
ELSEIF BOUGHT >= 54
	JUMP ITEM_SHOP_TRAP
ENDIF


SIF DAY:1 < 1
	DAY:1 = 1
SIF DAY:2 < 1
	DAY:2 = 1	

CALL DRAW_MAINMENU;メインメニュー描画処理はこっちの関数に丸投げ,本体は_DRAW_MAINMENU.ERB内

@USERSHOP
#DIM TEMP, 300

;店にいる場合は買い物中フラグを外す
IF RESULT == 999 && BOUGHT >= 0
	CALL CLEAR_SHOP
	BOUGHT = -1
ELSEIF RESULT == 998 && BOUGHT >= 0
	BOUGHT = 200
	CALL CLEAR_SHOP
	JUMP ITEM_SHOP_TRAP
ELSEIF RESULT == 997 && BOUGHT >= 0
	BOUGHT = 1
	CALL CLEAR_SHOP
	JUMP ITEM_SHOP
ELSEIF BOUGHT >= 0
	RETURN 0
ENDIF

IF RESULT == 100 && A > 0
;	CALL SELECT_TARGET
;	IF RESULT == 1
;		CALL SELECT_ASSI
;		SIF RESULT == 0
;			ASSI = -1
	IF TARGET <= 0
		CALL SELECT_TARGET
		SIF RESULT == 0
			RETURN 0
	ENDIF
	
	$SELECT_ASSI_LOOP
	IF ASSI <= 0
		TEMP:3 = 0
		REPEAT CHARANUM
			IF CFLAG:COUNT:0 == 2 && COUNT != 0 && CFLAG:COUNT:1 == 0 && TARGET != COUNT
				TEMP:3 += 1
			ENDIF
		REND
		SIF TEMP:3 >= 1
			CALL SELECT_ASSI
		SIF RESULT == 2
			RETURN 0
		SIF ASSI == 0
			ASSI = -1
		IF TARGET == ASSI
			ASSI = -1
			GOTO SELECT_ASSI_LOOP
		ENDIF
	ENDIF

	SIF ASSI >= 1 && TARGET == ASSI
		ASSI = -1
	SIF TARGET >= 1 && TARGET != ASSI
		BEGIN TRAIN
;	ENDIF
	RETURN 1
ELSEIF RESULT == 101
	CALL CHARA_INFO
	IF RESULT == 1
		BEGIN TURNEND
		RETURN 0
	ENDIF
ELSEIF RESULT == 102
	CALL DUNGEON_INFO2
ELSEIF RESULT == 103
	CALL EXECUTION
ELSEIF RESULT == 104
	CALL INTERCEPT
ELSEIF RESULT == 105
	CALL ABILITY_UP
ELSEIF RESULT == 106
	CALL CHARA_SALE
ELSEIF RESULT == 107
	BOUGHT = 1
ELSEIF RESULT == 108
	CALL TAILOR_MAIN
	TARGET = FLAG:1
ELSEIF RESULT == 109
	CALL INVASION
	IF RESULT == 1
		BEGIN TURNEND
		RETURN 1
	ENDIF
ELSEIF RESULT == 110 && TALENT:0:325 == 1
	CALL SECRET_LABO
ELSEIF RESULT == 111 && (FLAG:83 || FLAG:84)
	CALL INFRASTRUCTURE
ELSEIF RESULT == 199
	;休憩
	PRINTL You devoted time to internal affairs and took a break... (Tax +5%)
	FLAG:9 += 5
	BEGIN TURNEND
	RETURN 1
ELSEIF RESULT == 200
	SAVEGAME
ELSEIF RESULT == 300
	LOADGAME
ELSEIF RESULT == 777
	CALL CONFIG
ELSEIF RESULT == 888
	CALL MAOUNET
ELSEIF RESULT == 400
	CALL LABO

;ここから追加コマンド
ELSEIF RESULT == 496 && A > 0
	CALL SELECT_TARGET
ELSEIF RESULT == 497 && A > 0
	CALL SELECT_ASSI
ELSEIF RESULT == 498
	CALL CHARA_INFO_INDIVIDUAL, TARGET
ELSEIF RESULT == 499
	CALL CHARA_INFO_INDIVIDUAL, ASSI
ELSEIF RESULT == 500
	FLAG:36 = 0
ELSEIF RESULT == 501
	FLAG:36 = 1
ELSEIF RESULT == 504
	FLAG:36 = 4
ENDIF

RETURN 0


;調教するキャラを選ぶ
;----------------------------------------------
@SELECT_TARGET
;----------------------------------------------
CALL SHOW_LIST_TRAINABLE
;表示人数が返ってくる
IF RESULT < 1
	RETURN 0
ENDIF

PRINTL  
PRINTL Please choose a slave to train.
PRINTL [100] Return [101] Other
DRAWLINE

$INPUT_LOOP
INPUT
IF RESULT == 100
	;戻る
	RETURN 0
ELSEIF RESULT == 101
	;その他
	CALL MONSTER_PLAY
	RETURN RESULT
ELSEIF IS_TRAINABLE(RESULT) == 0
	;調教可能な対象
	TARGET = RESULT
	FLAG:1 = TARGET ;前回の調教対象を上書き
	RETURN 1
ENDIF
GOTO INPUT_LOOP

;助手を選ぶ
;----------------------------------------------
@SELECT_ASSI
;----------------------------------------------
CALL SHOW_LIST_ASSISTABLE
;表示人数が返ってくる
IF RESULT < 1
	RETURN 0
ENDIF

PRINTL  
PRINTL Please choose an assistant
PRINTL [100] No assistant
PRINTL [101] Return
DRAWLINE

$INPUT_LOOP
INPUT
IF RESULT == 100
	;助手は無し
	ASSI = -1
	FLAG:2 = ASSI ;前回の助手を上書き
	RETURN 0
ELSEIF RESULT == 101
	;戻る
	RETURN 2
ELSEIF IS_ASSISTABLE(RESULT) == 0
	;助手可能な対象
	ASSI = RESULT
	FLAG:2 = ASSI ;前回の助手を上書き
	RETURN 1
ENDIF
PRINTFORML {IS_ASSISTABLE(RESULT)}
GOTO INPUT_LOOP




