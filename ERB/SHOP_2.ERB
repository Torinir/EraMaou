;-------------------------------------------------
;能力値アップ
;-------------------------------------------------
@ABILITY_UP
#DIM SELECT_MENU = 998

$INPUT_LOOP_MENU

PRINTL Whose abilities would you like to raise?
IF SELECT_MENU == 997
	CALL LIFE_LIST_ENEMY
ELSE
	;イレギュラーも含めて正規の値に戻す
	SELECT_MENU = 998
	CALL LIFE_LIST
ENDIF
PRINTFORM [999] - Return 

;ダンジョンレベル20以上からアンロック
SIF CFLAG:0:9 < 20
	SETCOLOR (GETDEFCOLOR() - 0x444444)
PRINTFORM [998] - Slave List  [997] - Hero List

RESETCOLOR
PRINTL  

$INPUT_LOOP_0
INPUT
IF RESULT == 999
	RETURN 0
ELSEIF (RESULT > 990 && RESULT < 999) && CFLAG:0:9 < 20
	PRINTW このコマンドを解放するにはダンジョンレベルをさらに上げる必要があります
	CLEARLINE 2
	GOTO INPUT_LOOP_0
ELSEIF RESULT > 990 && RESULT < 999
	;メニューは一応991～998に含みを持たせています
	;イレギュラーは全て奴隷一覧になります
	SELECT_MENU = RESULT
	GOTO INPUT_LOOP_MENU
ELSEIF RESULT < 0 || RESULT >= CHARANUM
	PRINTW 範囲外の数値です
	CLEARLINE 2
	GOTO INPUT_LOOP_0
;臨死中のキャラは排除
ELSEIF  BASE:RESULT:0 < 1
	PRINTW 臨死中のため,選択出来ません
	CLEARLINE 2
	GOTO INPUT_LOOP_0
;自分は排除
ELSEIF RESULT == 0
	PRINTW 自分の能力は上げられません
	CLEARLINE 2
	GOTO INPUT_LOOP_0
;奴隷待機以外は除外
ELSEIF CFLAG:RESULT:1 != 0 && SELECT_MENU == 998
	PRINTW 待機状態でないため,選択出来ません
	CLEARLINE 2
	GOTO INPUT_LOOP_0
;侵攻中勇者以外は除外
ELSEIF CFLAG:RESULT:1 != 2 && SELECT_MENU == 997
	PRINTW 侵攻中状態でないため,選択出来ません
	CLEARLINE 2
	GOTO INPUT_LOOP_0
ENDIF

T = TARGET
TARGET = RESULT

$INPUT_LOOP_1
PRINTFORML %SAVESTR:TARGET%
PRINTL ..............................................................................
CALL SHOW_INFO_EXP
CALL SHOW_JUEL
CALL SHOW_ABLUP_SELECT

INPUT

;Ｃ感覚
IF RESULT == 0
	CALL ABLUP0
;Ｂ感覚
ELSEIF RESULT == 1
	CALL ABLUP1
;V感覚
ELSEIF RESULT == 2
	CALL ABLUP2
;A感覚
ELSEIF RESULT == 3
	CALL ABLUP3
;F感覚
ELSEIF RESULT == 4
	CALL ABLUP4
;従順
ELSEIF RESULT == 10
	CALL ABLUP10
;欲望
ELSEIF RESULT == 11
	CALL ABLUP11
;技巧
ELSEIF RESULT == 12
	CALL ABLUP12
;奉仕技術
ELSEIF RESULT == 13
	CALL ABLUP13
;性交技術
ELSEIF RESULT == 14
	CALL ABLUP14
;話術
ELSEIF RESULT == 15
	CALL ABLUP15
;奉仕精神
ELSEIF RESULT == 16
	CALL ABLUP16
;露出癖
ELSEIF RESULT == 17
	CALL ABLUP17
;サドっ気
ELSEIF RESULT == 20
	CALL ABLUP20
;マゾっ気
ELSEIF RESULT == 21
	CALL ABLUP21
;レズっ気
ELSEIF RESULT == 22
	CALL ABLUP22
;ホモっ気
ELSEIF RESULT == 23
	CALL ABLUP23
;性交中毒
ELSEIF RESULT == 30
	CALL ABLUP30
;自慰中毒
ELSEIF RESULT == 31
	CALL ABLUP31
;精液中毒
ELSEIF RESULT == 32
	CALL ABLUP32
;レズ中毒
ELSEIF RESULT == 33
	CALL ABLUP33
;売春中毒
ELSEIF RESULT == 37
	CALL ABLUP37
;獣姦中毒
ELSEIF RESULT == 39
	CALL ABLUP39
;F中毒
ELSEIF RESULT == 40
	CALL ABLUP40
;反発刻印
ELSEIF RESULT == 99
	CALL ABLUP99
ELSEIF RESULT == 999
	CALL YOKUBO_UP_CHECK
	CALL CHECK_SELLASSIABLE
	CALL CHECK_SPECIALSKIL
	TARGET = T
	RESTART
ENDIF

GOTO INPUT_LOOP_1

;-----------------------------------
@INTERCEPT
#DIM SELECT
#DIM ROOMID
#DIM ROOM
#DIM FLOOR
#DIM WORK
#DIM ITEM_GET
#DIM COST
;------------------------------------
;迎撃指定
;SELECT = 指定された迎撃奴隷
;ROOMID = 施設の場所
;ROOM = 施設
;FLOOR = 出発階層
;WORK  = 迎撃時の行動
;ITEM_GET  = アイテム補給

$INPUT_LOOP_2

PRINTL Who will intercept the hero?
COST = 6000
PRINTFORML [売却可]でない場合,資金を{COST}pt必要とします
CALL LIFE_LIST
PRINTFORML [999] - Return

INPUT
IF RESULT == 999
	RETURN 0
ELSEIF RESULT < 0 || RESULT >= CHARANUM
	GOTO INPUT_LOOP_2
;臨死中のキャラは排除
ELSEIF  BASE:RESULT:0 < 1
	GOTO INPUT_LOOP_2
;自分は排除
ELSEIF RESULT == 0
	PRINTL You are unable to intercept yourself.
	WAIT
	GOTO INPUT_LOOP_2
;奴隷待機以外は除外
ELSEIF CFLAG:RESULT:1 != 0
	GOTO INPUT_LOOP_2
;売却可以外は除外
ELSEIF CFLAG:RESULT:0 == 0 && TALENT:RESULT:254 == 0
	PRINTFORMW %SAVESTR:RESULT% is not obedient enough to obey you.
	GOTO INPUT_LOOP_2
;お金が必要
ELSEIF CFLAG:RESULT:0 == 0 && TALENT:RESULT:254 == 1 && MONEY < COST
	PRINTFORMW お金が足りないため%SAVESTR:RESULT%は言うことを聞かない
	GOTO INPUT_LOOP_2
;妊婦は除外
ELSEIF TALENT:RESULT:153 == 1 && GETBIT(FLAG:5,10) == 0
	PRINTFORMW %SAVESTR:RESULT% can't intercept because they are pregant.
	GOTO INPUT_LOOP_2
ENDIF

SIF CFLAG:RESULT:0 == 0
	PRINT お金を支払い
PRINTFORMW * %SAVESTR:RESULT% will intercept the hero *

SELECT = RESULT

FLOOR = 9
WORK = CFLAG:SELECT:500
ITEM_GET = 0

$INPUT_LOOP_MAIN
PRINTFORML %SAVESTR:SELECT%の迎撃設定
DRAWLINE
PRINTFORML [0] 出発階層       - {FLOOR}階
PRINTFORM [1] 迎撃時の行動   - 
IF WORK == 1
	PRINTL 売春
ELSEIF WORK == 2
	PRINTL 罠の補充
ELSEIF WORK == 3
	PRINTL 施設拡張(2000G必要)
ELSEIF WORK == 4
	PRINTL 潜入工作
ELSEIF WORK == 5
	PRINTL 訓練
ELSE
	PRINTL 内職
ENDIF
PRINTFORM [2] アイテムの補給 - 
IF ITEM_GET == 1
	PRINTL 有(2000G必要)
ELSE
	PRINTL 無
ENDIF
PRINTL  
SETCOLORBYNAME DarkSeaGreen
PRINTFORM [998] 迎撃開始!　　
RESETCOLOR
PRINTFORML [999] もどる

INPUT

IF RESULT == 999
	GOTO INPUT_LOOP_2
ELSEIF RESULT == 0
	GOTO INPUT_LOOP_4
ELSEIF RESULT == 1
	GOTO INPUT_LOOP_3
ELSEIF RESULT == 2 && ITEM_GET == 0
	IF MONEY < 2000 
		PRINTFORMW * 資金が足りません *
		GOTO INPUT_LOOP_MAIN
	ELSEIF CFLAG:SELECT:0 == 0 && MONEY < (COST + 2000)
		PRINTFORMW * 資金が足りません *
		GOTO INPUT_LOOP_MAIN
	ENDIF
	ITEM_GET = 1
	GOTO INPUT_LOOP_MAIN
ELSEIF RESULT == 2
	ITEM_GET = 0
	GOTO INPUT_LOOP_MAIN
ELSEIF RESULT != 998
	GOTO INPUT_LOOP_MAIN
ENDIF

;出撃決定
CFLAG:SELECT:1 = 3
CFLAG:SELECT:500 = WORK
CFLAG:SELECT:501 = FLOOR
CFLAG:SELECT:502 = 90
CFLAG:SELECT:505 = 0
SIF CFLAG:SELECT:0 == 0
	MONEY -= COST

SIF WORK == 3
	MONEY -= 2000
IF ITEM_GET == 1
	MONEY -= 2000
	;入手アイテム数
	LOCAL:2 = 0
	FOR LOCAL:1, 0, 3
		CALL ADD_EX_ITEM, -1, SELECT , 2
		SIF RESULT > 0
			LOCAL:2 += 1
	NEXT
	;返金
	IF LOCAL:2 == 0
		PRINTFORML アイテムはいらないと,軍資金を返却してきた
		MONEY += 2000
	ENDIF
ENDIF

CALL GOHOUBI_REQUEST, SELECT

RETURN 0

;------------------------
;出発階層設定
;------------------------
$INPUT_LOOP_4
PRINTL 出発階設定
PRINTFORML 魔王の力で%SAVESTR:SELECT%を好きな階に転送することができます.
PRINTL 何階に送り出しますか? (1-9)
PRINTL [1] [2] [3] [4] [5] [6] [7] [8] [9]

INPUT
IF RESULT >= 1 && RESULT <= 9
	FLOOR = RESULT
ELSE
	GOTO INPUT_LOOP_4
ENDIF

IF WORK == 3
	ROOMID = FLOOR + 349
	
	ROOM = FLAG:ROOMID
	
	IF ROOM == 0
		PRINTFORMW {FLOOR}階には施設がありません
		GOTO INPUT_LOOP_MAIN
	ENDIF
	
	ROOMID += 10
	LOCAL = FLAG:ROOMID
	
	IF LOCAL == 3
		PRINTFORMW {FLOOR}階の%ITEMNAME:ROOM%はすでに限界まで拡張されています
		GOTO INPUT_LOOP_MAIN
	ENDIF
ENDIF

GOTO INPUT_LOOP_MAIN

;------------------------
;行動設定
;------------------------
$INPUT_LOOP_3

PRINTL ダンジョン内での行動
DRAWLINE
PRINTL [0] 内職 
IF CFLAG:0:9 >= 10
	PRINTL [1] 売春 
ELSE
	SETCOLOR 80,80,80
	PRINTL [---] 未開放 
	RESETCOLOR
ENDIF

IF CFLAG:0:9 >= 20
	PRINTL [2] 罠の補充 
ELSE
	SETCOLOR 80,80,80
	PRINTL [---] 未開放 
	RESETCOLOR
ENDIF


IF CFLAG:0:9 >= 30
	PRINTL [3] 施設拡張(2000G必要) 
ELSE
	SETCOLOR 80,80,80
	PRINTL [---] 未開放 
	RESETCOLOR
ENDIF

IF CFLAG:0:9 >= 40
	PRINTL [4] 潜入工作 
ELSE
	SETCOLOR 80,80,80
	PRINTL [---] 未開放 
	RESETCOLOR
ENDIF

IF CFLAG:0:9 >= 50
	PRINTL [5] 訓練
ELSE
	SETCOLOR 80,80,80
	PRINTL [---] 未開放 
	RESETCOLOR
ENDIF

DRAWLINE


INPUT
IF RESULT < 0
	GOTO INPUT_LOOP_3
ELSEIF RESULT == 1 && CFLAG:0:9 < 10
	GOTO INPUT_LOOP_3
ELSEIF RESULT == 2 && CFLAG:0:9 < 20
	GOTO INPUT_LOOP_3
ELSEIF RESULT == 3 && CFLAG:0:9 < 30
	GOTO INPUT_LOOP_3
ELSEIF RESULT == 4 && CFLAG:0:9 < 40
	GOTO INPUT_LOOP_3
ELSEIF RESULT == 5 && CFLAG:0:9 < 50
	GOTO INPUT_LOOP_3
ELSEIF RESULT >= 6
	GOTO INPUT_LOOP_3
ENDIF

IF RESULT == 1
	PRINTW ダンジョンのモンスタ-相手に売春を許可します
ELSEIF RESULT == 2
	PRINTW 罠の補充作業を行います
ELSEIF RESULT == 3
	ROOMID = FLOOR + 349
	
	ROOM = FLAG:ROOMID
	
	IF ROOM == 0
		PRINTFORMW {FLOOR}階には施設がありません
		GOTO INPUT_LOOP_MAIN
	ENDIF
	
	ROOMID += 10
	LOCAL = FLAG:ROOMID
	
	IF LOCAL == 3
		PRINTFORMW {FLOOR}階の%ITEMNAME:ROOM%はすでに限界まで拡張されています
		GOTO INPUT_LOOP_MAIN
	ENDIF
	
	PRINTFORMW {FLOOR}階の%ITEMNAME:ROOM%を拡張します.資金が2000必要です
	
	IF MONEY < 2000 
		PRINTFORMW * 資金が足りません *
		GOTO INPUT_LOOP_MAIN
	ENDIF

ELSEIF RESULT == 4
	PRINTW 勇者のパ-ティ-に潜入して工作活動を行います
ELSEIF RESULT == 5
	PRINTW 迎撃中に訓練を行い経験値を得ます
ELSE
	PRINTW 収入を得ます
ENDIF
WORK = RESULT

GOTO INPUT_LOOP_MAIN

RETURN 0

;------------------------
@GOHOUBI_REQUEST, ARG:0
#DIM SELECT
#DIM WISH
;------------------------
;SELECT = 選択された奴隷
;WISH = 希望するもの

SELECT = ARG:0

IF TALENT:SELECT:136 == 1 && RAND:3 == 0
	WISH = RAND:3 + 1
ELSEIF TALENT:SELECT:85 == 1
	WISH = RAND:3 + 4
	IF WISH == 6 && !TALENT:MASTER:121 && !TALENT:MASTER:122
		WISH = 4
	ENDIF
ELSEIF TALENT:SELECT:76 == 1
	WISH = RAND:3 + 7
ELSE
	WISH = 0
ENDIF


CFLAG:SELECT:504 = WISH
A = SELECT

CALL GOHOUBI_REQUEST_KOUJO

A = 0



