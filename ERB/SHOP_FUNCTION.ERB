




;----------------------------------------------
@SHOW_LIST_TRAINABLE
;----------------------------------------------
;調教可能奴隷の一覧表示
;ついでに調教可能人数を返す
LOCAL = 0
FOR COUNT, 1, CHARANUM
	IF IS_TRAINABLE(COUNT) == 0
		PRINTFORM [{COUNT,2}] %SAVESTR:COUNT,16,LEFT% %GET_JOB_NAME(COUNT),8,LEFT% Lv: {CFLAG:COUNT:9,2,RIGHT}　HP: {BASE:COUNT:0}／{MAXBASE:COUNT:0} Times Trained: ({CFLAG:COUNT:10})
		;愛,淫乱
		IF TALENT:COUNT:愛
			SETCOLOR 255,100,100
			PRINTL <Love>　
			RESETCOLOR
		ELSEIF TALENT:COUNT:淫乱
			SETCOLOR 255,100,100
			PRINTL <Lewd>
			RESETCOLOR
		ELSE
			SETCOLOR 100,100,100
			PRINTL <Nothing>
			RESETCOLOR
		ENDIF
		LOCAL += 1
	ENDIF
NEXT
RETURN LOCAL

;----------------------------------------------
@SHOW_LIST_ASSISTABLE
;----------------------------------------------
;助手可能奴隷の一覧表示
;ついでに助手可能人数を返す
LOCAL = 0
FOR COUNT, 1, CHARANUM
	IF IS_ASSISTABLE(COUNT) == 0
		PRINTFORM [{COUNT, 2}] %SAVESTR:COUNT% Lv: {CFLAG:COUNT:9}
		;愛,淫乱
		IF TALENT:COUNT:愛
			SETCOLOR 255,100,100
			PRINTL <Love>　
			RESETCOLOR
		ELSEIF TALENT:COUNT:淫乱
			SETCOLOR 255,100,100
			PRINTL <Lewd>
			RESETCOLOR
		ELSE
			SETCOLOR 100,100,100
			PRINTL <Nothing>
			RESETCOLOR
		ENDIF
		LOCAL += 1
	ENDIF
NEXT
RETURN LOCAL

;----------------------------------------------
@IS_TRAINABLE(ARG)
#FUNCTION
;----------------------------------------------
;ARG番のキャラが調教可能なら0,ダメならそれ以外を返す式中関数
SIF ARG < 1 || ARG >= CHARANUM ;キャラ登録範囲外はダメ
	RETURNF 1
SIF CFLAG:ARG:1 != 0 ;調教中でないとダメ
	RETURNF 2
RETURNF 0

;----------------------------------------------
@IS_ASSISTABLE(ARG)
#FUNCTION
;----------------------------------------------
;ARG番のキャラが助手可能なら0,ダメならそれ以外を返す式中関数
SIF ARG < 1 || ARG >= CHARANUM ;キャラ登録範囲外はダメ
	RETURNF 1
SIF CFLAG:ARG:0 != 2 ;助手可能でないとダメ
	RETURNF 2
SIF CFLAG:ARG:1 != 0 ;調教中でないとダメ
	RETURNF 3
SIF TARGET == ARG ;調教対象はダメ
	RETURNF 4
RETURNF 0

;----------------------------------------------
@GET_JOB_NAME(ARG)
#FUNCTIONS
;----------------------------------------------
;ARG番のキャラの職業名の文字列を返す式中関数
IF TALENT:ARG:200
	RETURNF "Warrior"
ELSEIF TALENT:ARG:201
	RETURNF "Magician"
ELSEIF TALENT:ARG:202
	RETURNF "Priest"
ELSEIF TALENT:ARG:203
	RETURNF "Thief"
ELSEIF TALENT:ARG:204
	RETURNF "Meat Toilet "
ELSEIF TALENT:ARG:205
	RETURNF "Knight"
ELSEIF TALENT:ARG:206
	RETURNF "Shrine Maiden"
ELSEIF TALENT:ARG:207
	RETURNF "Ninja"
ELSEIF TALENT:ARG:208
	RETURNF "Archer"
ELSEIF TALENT:ARG:209
	RETURNF "Seedbed"
ELSEIF TALENT:ARG:210
	RETURNF "Demon Warlord"
ELSEIF TALENT:ARG:211
	RETURNF "Magical Priest"
ENDIF

;職業ない場合は空白を返す
;マオちゃんとかがここに来る
RETURNF " "

