
;----------------------------------
;税収
;----------------------------------
;10.20.30日に税収が発生する
;その都度計算する

@TAX_GET
#DIM TAX,4

;TAX変数に計算した税を入れていく
;TAX:0は合計値

;税収日
IF DAY:2 == 10 || DAY:2 == 20 || DAY:2 == 30
	PRINTW Today is tax revenue day...
ELSE
	RETURN 0
ENDIF

TAX:0 = 0


DRAWLINE
PRINTL 
PRINTL - - - Tax Revenue - - -
PRINTL 

WAIT

;--------------
;魔界からの支援
;--------------

TAX:0 += 1000

PRINTL 
PRINTFORMW Assistance From Hell: {TAX:0}

;--------------
;土地税
;--------------
;侵攻度に応じて税が徴収される
;陥落するとボーナス
;メイン収入源にしたい
;TAX:1に合計値を格納

;初期値
TAX:1 = 0

PRINTL *Regional Taxes*

IF FLAG:82
	PRINTFORML ├ 地上の魔界領土 1200
	TAX:1 += 1200
ELSEIF FLAG:81 > 10
	PRINTFORML ├ Human World Colony: {FLAG:81 / 10}
	TAX:1 += FLAG:81 / 10
ENDIF

IF FLAG:87 == 2
	PRINTFORML ├ ダ-クエルフの領土 1200
	TAX:1 += 1200
ELSEIF FLAG:86 > 10
	PRINTFORML ├ エルフの領域の植民地 {FLAG:86 / 10}
	TAX:1 += FLAG:86 / 10
ENDIF

IF FLAG:89 == 2
	PRINTFORML ├ Chaos Dragon Mountain: 1200
	TAX:1 += 1200
ELSEIF FLAG:88 > 10
	PRINTFORML ├ 竜の山の植民地 {FLAG:88 / 10}
	TAX:1 += FLAG:88 / 10
ENDIF

IF FLAG:91 == 2
	PRINTFORML ├ 堕天使の淫界 1200
	TAX:1 += 1200
ELSEIF FLAG:90 > 10
	PRINTFORML ├ Heaven's Colony: {FLAG:90 / 10}
	TAX:1 += FLAG:90 / 10
ENDIF

IF FLAG:92 == 15
	PRINTFORML ├ アルカナナイトの売春砦 1500
	TAX:1 += 1500
ENDIF

;ダンジョンの収入
;レベルによって増加量が変動する

PRINTFORM └ Dungeon: 
IF CFLAG:0:9 < 20
	PRINTFORML {CFLAG:0:9 * 50 + 100}
	TAX:1 += CFLAG:0:9 * 50 + 100
ELSEIF CFLAG:0:9 < 40
	PRINTFORML {CFLAG:0:9 * 40 + 300}
	TAX:1 += CFLAG:0:9 * 40 + 300
ELSEIF CFLAG:0:9 < 80
	PRINTFORML {CFLAG:0:9 * 30 + 700}
	TAX:1 += CFLAG:0:9 * 30 + 700
ELSEIF CFLAG:0:9 < 150
	PRINTFORML {CFLAG:0:9 * 20 + 1500}
	TAX:1 += CFLAG:0:9 * 20 + 1500
ELSEIF CFLAG:0:9 < 300
	PRINTFORML {CFLAG:0:9 * 10 + 3000}
	TAX:1 += CFLAG:0:9 * 10 + 3000
ELSE
	PRINTFORML {CFLAG:0:9 * 5 + 4500}
	TAX:1 += CFLAG:0:9 * 5 + 4500
ENDIF



PRINTL 
PRINTFORMW Total: {TAX:1}

TAX:0 += TAX:1

;--------------
;肉便器税
;--------------
;肉便器処刑のひとたちも売り上げで貢献
;あんまり稼げない方が肉便器っぽい
;石像やはく製は観覧料
;TAX:2に合計値を格納

;初期値
TAX:2 = 0

PRINTL 
PRINTL *Meat Urinal Taxes*

IF FLAG:84 > 0
	PRINTFORML ├ Equipment Viewing Tax: {FLAG:84 * 3}
	TAX:2 += FLAG:84 * 3
ENDIF

IF FLAG:83 > 0
	PRINTFORML ├ Meat Toilet Usage Tax: {FLAG:83 * 3}
	TAX:2 += FLAG:83 * 3
ENDIF


;淫魔売春婦の収入
;ITEM:143 ウィッチ
;ITEM:152 サキュバス
;ITEM:182 リリス
;は売春してそうなのでカウント
PRINTFORML └ Succubus Prostitution Tax: {ITEM:143 + ITEM:152 + ITEM:182 + 20}
TAX:2 += ITEM:143 + ITEM:152 + ITEM:182 + 20

FOR LOCAL,1,10
	;娼館街1個につきボーナス
	SIF FLAG:(LOCAL + 349) == 507
		TIMES TAX:2, 1.1
NEXT

PRINTL 
PRINTFORMW Total: {TAX:2}

TAX:0 += TAX:2

;--------------
;税収補正
;--------------

PRINTL 
PRINTL *Demon King's Special Tax*

TAX:3 = TAX:0 * (FLAG:9 + 100)
TAX:3 /= 100
TAX:3 -= TAX:0

PRINTFORMW Total: {TAX:3}

TAX:0 += TAX:3

FLAG:9 = 0

;--------------
;合計税収
;--------------

DRAWLINE
PRINTL 

PRINTFORMW Total Tax Revenue: {TAX:0}

MONEY += TAX:0

RETURN 0


